# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname=libinput-stable-git
_pkgname=libinput
pkgver=1.20.1.0.g562f7bce
pkgrel=1
pkgdesc='A library to handle input devices in Wayland compositors'
arch=(x86_64)
url='http://freedesktop.org/wiki/Software/libinput/'
license=('MIT')
depends=('mtdev' 'systemd' 'libevdev' 'libwacom')
# upstream doesn't recommend building docs
makedepends=('gtk3' 'meson' 'wayland-protocols' 'check' 'git') # 'doxygen' 'graphviz' 'python-sphinx' 'python-recommonmark'
optdepends=('gtk3: libinput debug-gui'
            'python-pyudev: libinput measure'
            'python-libevdev: libinput measure')
conflicts=('libinput')
provides=("libinput=${pkgver}")
source=(git://anongit.freedesktop.org/wayland/libinput#branch=1.20-branch)
sha1sums=('SKIP')

pkgver() {
  cd libinput
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

build() {
  arch-meson libinput build \
    -D udev-dir=/usr/lib/udev \
    -D documentation=false

  # Print config
  meson configure build

  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  install -Dvm644 libinput/COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
