# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: <pavbaranov at gmail dot com>
# Contributor: Steffen Weber <-boenki-gmx-de->

pkgname=otter-browser-git
_pkgname=otter-browser
pkgver=1.0.81.r8009.g23c955d89
pkgrel=1
pkgdesc='Otter Browser, project aiming to recreate classic Opera (12.x) UI using Qt5 - git checkout'
arch=('x86_64')
url='http://otter-browser.org/'
license=('GPL3')
provides=('otter-browser')
conflicts=('otter-browser-weekly' 'otter-browser-beta' 'otter-browser-rc' 'otter-browser')
depends=('qt5-multimedia' 'qt5-webkit' 'qt5-svg' 'qt5-xmlpatterns' 'hicolor-icon-theme' 'hunspell' 'desktop-file-utils')
makedepends=('cmake' 'qt5-tools' 'qt5-webengine' 'git')
optdepends=('hunspell: for spell checking'
            'qt5-webengine: for experimental backend using Qt5-Webengine')
source=('git://github.com/OtterBrowser/otter-browser.git')
sha256sums=('SKIP')

prepare() {
    mkdir -p build
}

pkgver() {
    cd $_pkgname
    # git tags
    # git describe --long --tags | sed 's/^v//;s/-/./g'
    # cmakelist version
    _ver="$(cat CMakeLists.txt | grep -m3 -e MAJOR_VERSION -e MINOR_VERSION -e PATCH_VERSION | grep -o "[[:digit:]]*" | paste -sd'.')"
    printf "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
    cd $_pkgname
    lrelease resources/translations/*.ts
    cd $srcdir/build
    cmake -DCMAKE_INSTALL_PREFIX="/usr" \
        -DENABLE_QTWEBENGINE=ON \
        -DENABLE_QTWEBKIT=ON \
        -DENABLE_SPELLCHECK=ON \
        ../$_pkgname
    make 
}

package() {
    cd build
    make DESTDIR=$pkgdir install
}
