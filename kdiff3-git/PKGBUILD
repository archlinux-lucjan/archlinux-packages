# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>

pkgname=kdiff3-git
_pkgname=kdiff3
pkgver=20200614.r1337.g83b89e7
pkgrel=1
pkgdesc="A KDE file comparator/merge tool"
arch=('x86_64')
url="https://quickgit.kde.org/?p=scratch%2Fthomasfischer%2Fkdiff3.git"
license=('GPL')
depends=('kparts')
makedepends=('extra-cmake-modules' 'kdoctools' 'git' 'boost')
conflicts=('kdiff3')
provides=('kdiff3')
source=('git+https://anongit.kde.org/kdiff3.git')
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname
  echo "$(git show --format='%cI' -q master | sed 's/T.*//g;s/-//g').r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
