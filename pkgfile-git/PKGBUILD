# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=pkgfile-git
_pkgname=pkgfile
pkgver=21.r52.g9c45690
pkgrel=1
pkgdesc="a pacman .files metadata explorer"
arch=('x86_64')
url="http://github.com/falconindy/pkgfile"
license=('MIT')
depends=('libarchive' 'curl' 'pcre' 'pacman')
makedepends=('git' 'meson')
conflicts=('pkgfile')
provides=('pkgfile')
install=pkgfile.install
source=("git+https://github.com/falconindy/pkgfile"
        '0001-Fix-build-failure-missing-include-of-optional.patch')
md5sums=('SKIP'
         '2b2636e9d5a9a5de1830a1bfcfdfd52c')

prepare() {
 cd "$_pkgname"

 local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$_pkgname"

  arch-meson build
  ninja -v -C build
}

package() {
  cd "$_pkgname"

  DESTDIR=$pkgdir ninja -C build install
}

# vim: ft=sh syn=sh et
