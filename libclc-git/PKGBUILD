# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=libclc-git
pkgver=20171022.r399.gfabea1e
pkgrel=1
groups=('mesagit')
pkgdesc="Library requirements of the OpenCL C programming language (git version)"
arch=('x86_64')
url="https://libclc.llvm.org/"
license=('MIT')
provides=('libclc')
replaces=('libclc-amdgpu-git')
conflicts=('libclc-amdgpu-git' 'libclc')
makedepends=('clang' 'llvm' 'git' 'python2')
options=('staticlibs')
source=('libclc::git+https://llvm.org/git/libclc.git')
md5sums=(SKIP)

pkgver() {
  cd libclc

  echo "$(git show --format='%cI' -q master | sed 's/T.*//g;s/-//g').r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"

}

build() {
  cd libclc
  
  sed -i 's/"python < $in >/sys.executable + " < $in >/g' configure.py
#  sed -i 's/python < $in >/python2 < $in >/g' configure.py
  
  python2 ./configure.py --prefix=/usr
  make
}

package() {
  cd libclc
  
  make install DESTDIR="$pkgdir"
  install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
