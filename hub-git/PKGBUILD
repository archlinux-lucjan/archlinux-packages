# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Jonne Ha√ü <me@jhass.eu>

pkgname=hub-git
pkgver=2.9.0.r0.g28ebb7ce
pkgrel=1
pkgdesc="cli interface for Github"
url="https://hub.github.com"
arch=('x86_64' 'i686')
license=('MIT')
conflicts=('hub')
provides=('hub')
depends=('git')
makedepends=('go' 'git')
source=("git+https://github.com/github/hub.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname/-git/}"
  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname/-git/}"
  mkdir -p "$srcdir/src/github.com/github/hub"
  cp -r . "$srcdir/src/github.com/github/hub"
  rm -rf "$srcdir/src/github.com/github/hub/.git"
  sed -i 's@.man-pages.stamp: bin/ronn@.man-pages.stamp:@;s@bin/ronn@ronn@' "$srcdir/src/github.com/github/hub/Makefile" 
}

build() {
  cd "$srcdir/src/github.com/github/hub"

  export GOPATH="$srcdir"
  make -j1
  make -j1 man-pages
}

check() {
  cd "$srcdir/src/github.com/github/hub"

  GOPATH="$srcdir" make test
}

package() {
  cd "$srcdir/src/github.com/github/hub"

  make PREFIX="$pkgdir/usr" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname/-git/}/LICENSE"
  install -Dm644 etc/hub.bash_completion.sh "$pkgdir/usr/share/bash-completion/completions/hub"
  install -Dm644 etc/hub.zsh_completion "$pkgdir/usr/share/zsh/site-functions/_hub"
  install -Dm644 etc/hub.fish_completion "$pkgdir/usr/share/fish/vendor_completions.d/hub.fish"
}
