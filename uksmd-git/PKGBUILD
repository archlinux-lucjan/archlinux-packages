# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Oleksandr Natalenko <oleksandr@natalenko.name>
# Contributor: Damian N. <nycko123 at gmail>

pkgname=uksmd-git
gitname=uksmd
pkgver=1.2.9.r0.g3b026f1
pkgrel=3
epoch=1
pkgdesc="Userspace KSM helper daemon"
url="https://github.com/CachyOS/uksmd"
license=('GPL3')
arch=('x86_64')
depends=('procps-ng' 'libcap-ng')
makedepends=('meson' 'git')
conflicts=('uksmd')
provides=('uksmd')
source=('git+https://github.com/CachyOS/uksmd'
        'uksmd-systemd-restart'
        '90-uksmd-upgrade.hook')
sha256sums=('SKIP'
            '6771a9ce1bbf6e7235db506882c76e94d7aec55c525055d878a841bd684e54bf'
            '31fe98436761086aab380810bb5b908ec6da40e739cb75a574a7d3dbb94b5c2e')

pkgver() {
  cd $gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
 cd $gitname

 local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $gitname
  arch-meson . build
  meson compile -C build
}

package() {
  depends+=(UKSMD-BUILTIN)

  cd $gitname
  meson install -C build --destdir "${pkgdir}"
  install -Dm644 "${srcdir}/uksmd-systemd-restart" "${pkgdir}/usr/share/libalpm/scripts/uksmd-systemd-restart"
  install -Dm644 "${srcdir}/90-uksmd-upgrade.hook"  "${pkgdir}/usr/share/libalpm/hooks/90-uksmd-upgrade.hook"
}
