# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Oleksandr Natalenko <oleksandr@natalenko.name>
# Contributor: Damian N. <nycko123 at gmail>

pkgname=uksmd-git
gitname=uksmd
pkgver=1.2.9.r0.g3b026f1
pkgrel=2
epoch=1
pkgdesc="Userspace KSM helper daemon"
url="https://github.com/CachyOS/uksmd"
license=('GPL3')
arch=('x86_64')
depends=('procps-ng' 'libcap-ng')
makedepends=('meson' 'git')
conflicts=('uksmd')
provides=('uksmd')
source=('git+https://github.com/CachyOS/uksmd'
        '90-uksmd-install.hook'
        '90-uksmd-upgrade.hook')
sha256sums=('SKIP'
            '252cdd6fcafc0aecdf32ba8e3dc6b7faf05162d397a42554cae2da0e3c37403a'
            '908689eb01c4595f00edcc897dd7ed881e5f1866c113f8654914a4f52b2fcd0e')

pkgver() {
  cd $gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
 cd $gitname

 local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $gitname
  arch-meson . build
  meson compile -C build
}

package() {
  depends+=(UKSMD-BUILTIN)

  cd $gitname
  meson install -C build --destdir "${pkgdir}"
  install -Dm644 "${srcdir}/90-uksmd-install.hook" "${pkgdir}/usr/share/libalpm/hooks/90-uksmd-install.hook"
  install -Dm644 "${srcdir}/90-uksmd-upgrade.hook"  "${pkgdir}/usr/share/libalpm/hooks/90-uksmd-upgrade.hook"
}
