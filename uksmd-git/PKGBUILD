# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Oleksandr Natalenko <oleksandr@natalenko.name>

pkgname=uksmd-git
_pkgname=uksmd
pkgver=0.0.0.r31.ge9b06f4
pkgrel=1
epoch=1
pkgdesc="Userspace KSM helper daemon"
url="https://gitlab.com/post-factum/uksmd"
license=(GPL3)
arch=(x86_64)
depends=('UKSMD-BUILTIN' 'systemd' 'procps-ng' 'libcap-ng')
makedepends=('meson' 'git')
conflicts=('uksmd')
provides=("uksmd=${pkgver}")
source=('git+https://gitlab.com/post-factum/uksmd')
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat meson.build | grep -o "[[:digit:]]*" | paste -sd'.')"
  printf "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cd $_pkgname
  arch-meson . build
  meson compile -C build
}

package() {
  cd $_pkgname
  meson install -C build --destdir "${pkgdir}"
  install -Dm644 "COPYING" "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
}
