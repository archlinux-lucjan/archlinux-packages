# $Id$
# Original Core Repo
# ==================
# Maintainer: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

# build from head of release branch as bug fix releases are rare

# Modifications to Use Git Master Source
# ======================================
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: James Harvey <jamespharvey20@gmail.com>
#    * This PKGFILE as closely as possible matches core's binutils 

pkgname=binutils-test-git
_pkgname=binutils-gdb
pkgver=2.34.0.r100242.9d67d950a59
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files (git master developmental version)"
arch=('x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=('base-devel')
depends=('glibc' 'zlib')
makedepends=('git')
checkdepends=('dejagnu' 'bc')
provides=('binutils')
conflicts=('binutils-multilib' 'binutils')
options=('staticlibs' '!distcc' '!ccache')
source=(git://sourceware.org/git/binutils-gdb.git#branch=binutils-2_34-branch)
md5sums=('SKIP')

pkgver() {
   cd binutils-gdb
   echo $(cat binutils/configure | grep "PACKAGE_VERSION=" | sed "s|^PACKAGE_VERSION='||" | sed "s|'$||" | sed "s|\.51$||").r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd binutils-gdb

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  mkdir ${srcdir}/binutils-build
}

build() {
  cd binutils-build

  $srcdir/binutils-gdb/configure \
    --prefix=/usr \
    --with-lib-path=/usr/lib:/usr/local/lib \
    --with-bugurl=https://bugs.archlinux.org/ \
    --enable-cet \
    --enable-deterministic-archives \
    --enable-gold \
    --enable-ld=default \
    --enable-lto \
    --enable-plugins \
    --enable-relro \
    --enable-shared \
    --enable-targets=x86_64-pep \
    --enable-threads \
    --disable-gdb \
    --disable-werror \
    --with-pic \
    --with-system-zlib

  make configure-host
  make tooldir=/usr
}

#check() {
#  cd binutils-build
#  
#  # unset LDFLAGS as testsuite makes assumptions about which ones are active
#  # ignore failures in gold testsuite...
#  make -k LDFLAGS="" check || true
#}

package() {
  cd binutils-build
  make prefix="$pkgdir/usr" tooldir="$pkgdir/usr" install

  # Remove unwanted files
  rm -f "$pkgdir"/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*

  # No shared linking to these files outside binutils
  rm -f "$pkgdir"/usr/lib/lib{bfd,opcodes}.so
  echo 'INPUT( /usr/lib/libbfd.a -liberty -lz -ldl )' > "$pkgdir/usr/lib/libbfd.so"
  echo 'INPUT( /usr/lib/libopcodes.a -lbfd )' > "$pkgdir/usr/lib/libopcodes.so"
}
