# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Florian Bigard <florian.bigard@gmail.com>
# Thanks to Adria Arrufat <swiftscythe@gmail.com>

pkgname=clementine-qt5-git
pkgver=1.3.1.516.g5966ff51c
pkgrel=1
pkgdesc="Experimental Qt5 version of Clementine, a modern music player and library organiser."
arch=('i686' 'x86_64')
url="https://www.clementine-player.org/"
license=('GPL')
depends=('gst-plugins-base' 'gst-plugins-good' 'gst-plugins-bad' 'gst-plugins-ugly'
         'gst-libav' 'taglib' 'glew' 'desktop-file-utils' 'hicolor-icon-theme'
         'qt5-base' 'protobuf' 'crypto++' 'chromaprint'
         'libmygpo-qt5-git' 'qt5-x11extras')
makedepends=('git' 'boost' 'cmake' 'mesa' 'qt5-tools')
# Uncomment next lines to enable all the  features
#depends+=('libspotify' 'libgpod' 'liblastfm-qt5' 'sparsehash' 'libcdio' 'libmtp' 'fftw')
optdepends=(
  'libspotify: for Spotify support'
  'libgpod: for iPhone and iPod Touch support'
  'liblastfm-qt5: for LastFM support'
  'sparsehash: for cloud support (Dropbox, Google Drive...)'
  'libcdio: for CD support'
  'libmtp: for MTP support'
  'fftw: for moodbar support'
)
provides=('clementine')
conflicts=('clementine' 'clementine-lxqt' 'clementine-git')
replaces=('clementine' 'clementine-lxqt' 'clementine-git')
source=('git+httpss://github.com/clementine-player/Clementine.git#branch=qt5'
        'spotify_install.patch' 'spotify_blob.patch')
sha256sums=('SKIP'
            '05a438fb445790200258c536d396a6bda9df38a9b775d5259421376ec1878786'
            '440b7e0b9b70d3e379ec9404eb09c8eca584188d81269904fd57375671a74a8e')

pkgver() {
  cd Clementine
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  mkdir -p build
  cd Clementine
  patch -Np1 -i ../spotify_blob.patch
  patch -Np0 -i ../spotify_install.patch
}

build() {
  cd build
  cmake "../Clementine" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package(){
  cd build
  make DESTDIR=${pkgdir} install
}
