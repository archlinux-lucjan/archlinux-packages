# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor:   Simon Legner <Simon.Legner@gmail.com>
# Contributor:   Caleb Maclennan <caleb@alerque.com>
# Contributor:  Jeremy Asuncion <jeremyasuncion808@gmail.com>

pkgname=lab-git
_pkgname=lab
pkgver=0.15.2.r1.gb2da778
pkgrel=1
pkgdesc="A hub-like tool for GitLab (git release)"
arch=('x86_64')
url="https://zaquestion.github.io/lab/"
license=('custom:Unlicense')
depends=('git')
optdepends=('hub')
makedepends=('go' 'git')
conflicts=('lab')
provides=('lab')
source=("git+https://github.com/zaquestion/lab")
sha512sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${_pkgname}"
    go mod download
}

build () {
    cd "${_pkgname}"
    go build \
        -gcflags "all=-trimpath=$PWD" \
        -asmflags "all=-trimpath=$PWD" \
        -ldflags "-extldflags $LDFLAGS"
}

package() {
    cd "${_pkgname}"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname"/LICENSE
    install -Dm755 lab "$pkgdir"/usr/bin/lab
}
