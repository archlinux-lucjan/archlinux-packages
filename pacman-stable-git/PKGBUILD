# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: godane <slaxemulator@gmail.com.com>
# Contributor: Andres Perera <aepd87@gmail.com>

pkgname=pacman-stable-git
pkgver=6.0.2.0.gc2d4568d
pkgrel=5
pkgdesc="A library-based package manager with dependency support. git version."
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('bash' 'glibc' 'libarchive' 'curl'
         'gpgme' 'pacman-mirrorlist' 'archlinux-keyring')
makedepends=('asciidoc' 'git' 'meson' 'doxygen')
checkdepends=('python' 'fakechroot')
optdepends=('perl-locale-gettext: translation support in makepkg-template')
backup=(etc/pacman.conf
        etc/makepkg.conf)
provides=("pacman=$pkgver" 'libalpm.so')
conflicts=('pacman')
options=('strip' 'debug')
source=(git+https://gitlab.archlinux.org/pacman/pacman#branch=release/6.0.x
        pacman.conf
        makepkg.conf
        0001-add-support-for-x86_64_v-2-3-4-autodetection.patch
        0001-add-information-about-origin-db.patch
        pacman-fix.patch)
sha1sums=('SKIP'
          'da4fff7132858c15358129c5eea6a915d2d4f698'
          '543c8c6c7ece78e131a2e639cc35aeb30a493fdb'
          '590d3e4aba6b1ad61f2574bc5d06d4cfb8fab946'
          '034c38d92657e9ceb40488fceb3d19223a3912ab'
          '4496f7af607683c54d4cd1a41d9f7b83a9c66eb7')

pkgver() {
  cd pacman
  git describe --long | sed 's/^v//;s/-/./g'
}

prepare() {
  cd pacman
  ### Patching sources
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd pacman

  meson --prefix=/usr \
        --buildtype=plain \
        -Ddoc=enabled \
        -Ddoxygen=enabled \
        -Dscriptlet-shell=/usr/bin/bash \
        -Dldconfig=/usr/bin/ldconfig \
        build

  meson compile -C build
}

check() {
  cd pacman

  meson test -C build
}

package() {
  cd pacman

  DESTDIR="$pkgdir" meson install -C build

  # install Arch specific stuff
  install -dm755 "$pkgdir/etc"
  install -m644 "$srcdir/pacman.conf" "$pkgdir/etc"
  install -m644 "$srcdir/makepkg.conf" "$pkgdir/etc"
}
