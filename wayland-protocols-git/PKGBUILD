# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Emmanuel Gil Peyrot <emmanuel.peyrot@collabora.com>

pkgname=wayland-protocols-git
pkgver=1.11.0.gfc3305c
pkgrel=1
pkgdesc='Wayland protocols that add functionalities not available in the core protocol'
arch=('any')
url='https://cgit.freedesktop.org/wayland/wayland-protocols'
license=('MIT')
makedepends=('git' 'wayland')
conflicts=('wayland-protocols')
provides=('wayland-protocols')

source=('git://anongit.freedesktop.org/wayland/wayland-protocols')
sha1sums=('SKIP')

pkgver() {
    cd wayland-protocols

    git describe --long --tags | sed 's/^v//;s/-/./g'
}

prepare() {
    mkdir -p wayland-protocols/build
}

build() {
    cd wayland-protocols/build
    ../autogen.sh --prefix=/usr
    make
}

package() {
    cd wayland-protocols/build
    make DESTDIR="${pkgdir}" install
}
