# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: nontlikeuname

pkgname=meson-git
pkgver=0.51.0.282.g275c493ca
pkgrel=1
pkgdesc="SCons-like build system that use python as a front-end language and Ninja as a building backend"
arch=(any)
url="http://mesonbuild.com/"
license=('Apache')
depends=('python-setuptools' 'ninja')
makedepends=('git')
checkdepends=('gcc-objc' 'vala' 'rust' 'gcc-fortran' 'mono' 'boost' 'qt4' 'qt5-base' 'git' 'cython'
              'gtkmm3' 'gtest' 'gmock' 'protobuf' 'wxgtk' 'python-gobject' 'gobject-introspection'
              'itstool' 'gtk3' 'java-environment=8' 'gtk-doc' 'llvm' 'clang' 'sdl2' 'graphviz'
              'doxygen' 'vulkan-validation-layers' 'openssh' 'mercurial' 'gtk-sharp-2' 'qt5-tools'
              'libwmf' 'dmd' 'valgrind' 'cmake' 'netcdf-fortran' 'openmpi' 'cuda' 'ldc' 'nasm'
              'gnustep-base' 'libelf')
provides=('meson')
conflicts=('meson')
source=('git+https://github.com/mesonbuild/meson'
        'arch-meson'
        'skip-test.diff')
md5sums=('SKIP'
         'b85d62ecca0729f0c5168f759a2ff37d'
         '28b5b8cac7121217b38e0eb84541e574')

prepare() {
        cd meson
        patch -Np1 -i ../skip-test.diff
}

pkgver() {
	cd meson
	git describe --long --tags | sed 's/^v//;s/-/./g'
}

build() {
        cd meson
        python setup.py build
}

# check() {
#	cd meson

#       export LC_CTYPE=en_US.UTF-8 CPPFLAGS= CFLAGS= CXXFLAGS= LDFLAGS=
#       ./run_tests.py
# }

package() {
	cd meson
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

        install -d "${pkgdir}/usr/share/vim/vimfiles"
        cp -rt "${pkgdir}/usr/share/vim/vimfiles" data/syntax-highlighting/vim/*/

        install -Dt "${pkgdir}/usr/share/emacs/site-lisp" -m644 data/syntax-highlighting/emacs/*
        install -Dt "${pkgdir}/usr/share/zsh/site-functions" -m644 data/shell-completions/zsh/*

        # Arch packaging helper
        install -D ../arch-meson -t "${pkgdir}/usr/bin"
}
