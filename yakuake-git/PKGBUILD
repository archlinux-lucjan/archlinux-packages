# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=yakuake-git
pkgver=3.0.3.21.g2c26a6c
pkgrel=1
pkgdesc="A drop-down terminal emulator based on KDE Konsole technology. (GIT version)"
arch=('i686' 'x86_64')
url='https://www.kde.org/applications/system/yakuake'
license=('GPL')
depends=('konsole' 'knewstuff' 'kwayland' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'git')
conflicts=('yakuake')
provides=('yakuake')
source=('git://anongit.kde.org/yakuake.git')
sha1sums=('SKIP')

pkgver() {
  cd yakuake
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../yakuake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
