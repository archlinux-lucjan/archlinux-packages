# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Haruue Icymoon <haruue@caoyue.com.cn>

pkgbase=linux-usermode-rc
pkgname=('linux-usermode-rc' 'linux-usermode-rc-modules')
_kernelname=-usermodelinux-rc
_major=5.18
_minor=11
_minorc=$((_minor+1))
_rcver=rc1
pkgver=${_major}.${_minorc}
_stable=${_major}.${_minor}
_stablerc=${pkgver}-${_rcver}
_srcname=linux-${_stable}
pkgrel=0.2
pkgdesc="User mode Linux kernel and modules"
arch=('x86_64')
license=('GPL2')
url="http://user-mode-linux.sourceforge.net/"
depends=('coreutils')
makedepends=('bc' 'inetutils')

### Stable-review
# In case the stable-review repository ever stopped working,
# a solution to build rc kernels had to be prepared.

if [ "$_minor" = 0 ]; then
_patchver=${_major}
else
_patchver=${_major}.${_minor}
fi

source=("https://www.kernel.org/pub/linux/kernel/v5.x/linux-${_stable}.tar.xz"
        "https://www.kernel.org/pub/linux/kernel/v5.x/linux-${_stable}.tar.sign"
        "https://www.kernel.org/pub/linux/kernel/v5.x/stable-review/patch-${_stablerc}.xz"
        "https://www.kernel.org/pub/linux/kernel/v5.x/stable-review/patch-${_stablerc}.sign"
        # https://lkml.org/lkml/2019/8/23/712
        # "patch-${_stablerc}.diff::https://git.kernel.org/pub/scm/linux/kernel/git/stable/linux-stable-rc.git/patch/?id=linux-${_major}.y&id2=v${_patchver}"
        '0001-um-Add-missing-apply_returns.patch'
        'config'
        '70-uml.hook')

sha256sums=('e20d8e54a782f7079b22762e8ee8adec0d08351b1083553adb2d6885ad1815af'
            'SKIP'
            '051766cb7df214f9491e5c7586a658db7b8c663184e587173f231ac60a30f66e'
            'SKIP'
            '4344722c8e3fc08566d6dceb51cc41591d1f7972cf52b2f33510a3e8cc51af08'
            'b0e116603c5897552c1096ea66551aff5acab59101b32e7d1754c0cf86443b71'
            '05ea4e00d1e99bf8140a21c94e3c42acf17b9debad9c6f5decbe1dd1fe04332c')

validpgpkeys=(
              'ABAF11C65A2970B130ABE3C479BE3E4300411886' # Linus Torvalds
              '647F28654894E3BD457199BE38DBBDC86092693E' # Greg Kroah-Hartman
             )

prepare() {
  cd $_srcname

  echo "Add stable-review patch..."
  patch -p1 -i ../patch-${_stablerc}
  # patch -p1 -i ../patch-${_stablerc}.diff

  echo "Setting version..."
  sed -e "/^EXTRAVERSION =/s/=.*/=/" -i Makefile
  scripts/setlocalversion --save-scmversion
  echo "-$pkgrel" > localversion.10-pkgrel
  echo "$_kernelname" > localversion.20-pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done

  echo "Setting config..."
  cp ../config .config
  yes "" | make ARCH=um config >/dev/null
  diff -u ../config .config || :

  make ARCH=um kernelrelease > version
  echo "Prepared $pkgbase version $(<version)"

  echo "Save configuration for later reuse"
  cat .config > "${startdir}/config.last"
}

build() {
  cd ${_srcname}
  unset LDFLAGS CFLAGS
  make ARCH=um vmlinux modules
}

_package() {
  conflicts=('linux-usermode' 'linux-usermode-git' 'linux-usermode-rc-git')

  cd ${_srcname}
  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/kernel-usermode"
  install -m 644 System.map ${pkgdir}/usr/share/kernel-usermode/System.map
  install -m 755 vmlinux ${pkgdir}/usr/bin/
}

_package-modules() {
  conflicts=('linux-usermode-modules' 'linux-usermode-git-modules'
             'linux-usermode-rc-git-modules')
  depends=('kmod')

  cd ${_srcname}
  local kernver="$(<version)"
  make ARCH=um INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 \
    DEPMOD=/doesnt/exist modules_install  # Suppress depmod
  rm -f $pkgdir/usr/lib/modules/${kernver}/{source,build}
  # sed expression for following substitutions
  local _subst="
        s|%PKGBASE%|${pkgbase}|g
        s|%KERNVER%|${kernver}|g
  "
  # install pacman hook
  sed "${_subst}" ../70-uml.hook |
        install -Dm644 /dev/stdin "${pkgdir}/usr/share/libalpm/hooks/70-uml.hook"
}

pkgname=("${pkgbase}" "${pkgbase}-modules")
for _p in ${pkgname[@]}; do
  eval "package_${_p}() {
    $(declare -f "_package${_p#${pkgbase}}")
    _package${_p#${pkgbase}}
  }"
done

# vim:set ts=8 sts=2 sw=2 et:
