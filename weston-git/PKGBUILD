# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: Joel Teichroeb <joel@teichroeb.net>
# Contributor: Sébastien Luttringer
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

pkgname=weston-git
_pkgname=weston
pkgver=6.0.0.23.g923a1e96
pkgrel=1
pkgdesc='Reference implementation of a Wayland compositor'
arch=('x86_64')
url='http://wayland.freedesktop.org'
license=('MIT')
depends=('glibc' 'wayland' 'libxkbcommon' 'libinput' 'libunwind' 'pixman'
         'libdrm' 'pam' 'systemd-libs' 'cairo' 'libpng' 'libjpeg-turbo' 'libwebp'
         'mesa' 'libegl' 'libgles' 'glib2' 'pango' 'lcms2' 'mtdev' 'libx11'
         'libxcb' 'dbus' 'libva' 'libxcursor' 'colord' 'freerdp')
makedepends=('wayland-protocols' 'meson' 'ninja' 'git')
conflicts=('weston')
provides=('weston')
source=('git://anongit.freedesktop.org/wayland/weston')
sha1sums=('SKIP')

pkgver() {
   cd $_pkgname

   git describe --long --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  cd $_pkgname
  # apply patch from the source array (should be a pacman feature)
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  meson $_pkgname build \
    --prefix=/usr --libexec=lib/weston -Dsimple-dmabuf-drm=auto
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  # license
  install -Dm644 $_pkgname/COPYING "$pkgdir/usr/share/licenses/$_pkgname/COPYING"
}
