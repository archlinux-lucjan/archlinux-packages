# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>

pkgname=libsignal-protocol-c-git
_pkgname=libsignal-protocol-c
pkgver=2.3.2.4.g71954c5
pkgrel=1
pkgdesc="Signal Protocol C Library"
arch=('i686' 'x86_64')
url="https://github.com/WhisperSystems/libsignal-protocol-c"
license=('GPL3')
makedepends=('cmake')
conflicts=('libsignal-protocol-c')
provides=('libsignal-protocol-c')
source=("git://github.com/WhisperSystems/$_pkgname")
md5sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$_pkgname \
    -DCMAKE_POSITION_INDEPENDENT_CODE=True \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=0
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
