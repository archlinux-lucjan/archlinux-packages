# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Jan Neumann <neum DOT ja AT gmail DOT com>

pkgbase=mcmojave-kde-git
_pkgbase=McMojave-kde
pkgname=('mcmojave-kde-git' 'kvantum-theme-mcmojave-git')
pkgver=20200521.r59.g435fb0a
pkgrel=1
pkgdesc="McMojave theme for KDE Plasma 5"
arch=('any')
url="https://github.com/vinceliuice/${_pkgbase}"
license=('GPL3')
options=('!strip')
source=("${_pkgbase}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd ${_pkgbase}
  echo "$(git show --format='%cI' -q master | sed 's/T.*//g;s/-//g').r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

package_mcmojave-kde-git() {
    optdepends=('kvantum-theme-mcmojave-git: McMojave theme for Kvantum Qt style (recommended)'
                'tela-icon-theme-git: for a more consistent and beautiful experience (recommended)')
    conflicts=('mcmojave-kde')
    provides=('mcmojave-kde')
    cd ${_pkgbase}
    install -d "${pkgdir}"/usr/share

    mkdir -p ${pkgdir}/usr/share/aurorae/themes/{McMojave,McMojave-1.5x,McMojave-2.0x,McMojave-light}
    cp -r  ${srcdir}/${_pkgbase}/aurorae/* ${pkgdir}/usr/share/aurorae/themes

    mkdir -p ${pkgdir}/usr/share/color-schemes
    cp -r ${srcdir}/${_pkgbase}/color-schemes ${pkgdir}/usr/share

    mkdir -p ${pkgdir}/usr/share/plasma/desktoptheme/{McMojave-light,McMojave}
    cp -r ${srcdir}/${_pkgbase}/plasma/desktoptheme ${pkgdir}/usr/share/plasma

    mkdir -p ${pkgdir}/usr/share/plasma/look-and-feel/{com.github.vinceliuice.McMojave-light,com.github.vinceliuice.McMojave}
    cp -r ${srcdir}/${_pkgbase}/plasma/look-and-feel/ ${pkgdir}/usr/share/plasma

    mkdir -p ${pkgdir}/usr/share/sddm/themes/McMojave
    cp -r ${srcdir}/${_pkgbase}/sddm/McMojave ${pkgdir}/usr/share/sddm/themes
}

package_kvantum-theme-mcmojave-git() {
    pkgdesc="McMojave theme for Kvantum"
    depends=('kvantum-qt5')
    provides=('kvantum-theme-mcmojave')
    conflicts=('kvantum-theme-mcmojave')
    cd ${_pkgbase}
    install -d "${pkgdir}"/usr/share

    mkdir -p ${pkgdir}/usr/share/Kvantum/{McMojave-light,McMojave}
    cp -r ${srcdir}/${_pkgbase}/Kvantum ${pkgdir}/usr/share
}
