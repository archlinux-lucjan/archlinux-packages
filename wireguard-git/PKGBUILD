# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Jason A. Donenfeld <Jason@zx2c4.com>

pkgbase=wireguard-git
_pkgbase=wireguard
pkgname=(wireguard-dkms-git wireguard-tools-git)
pkgver=0.0.20190913.r0.g7bf34f5
pkgrel=1
pkgdesc='next generation secure network tunnel'
arch=('x86_64')
url='http://www.wireguard.com/'
license=('GPL')
makedepends=('git' 'libmnl')
source=("git://git.zx2c4.com/WireGuard.git")
sha256sums=('SKIP')

pkgver() {
        cd WireGuard

        git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
    
}

prepare() {
	cd WireGuard

	find contrib/examples/ -name '.gitignore' -delete
}

build() {
	cd WireGuard

	make -C src/tools/
}

package_wireguard-dkms-git() {
	depends=('dkms')
	provides=('WIREGUARD-MODULE' 'wireguard-dkms')
	conflicts=('wireguard-dkms')

	cd WireGuard

	make -C src/ \
		DESTDIR="${pkgdir}/" \
		DKMSDIR="/usr/src/wireguard/" \
		dkms-install
}

package_wireguard-tools-git() {
	depends=('libmnl')
	optdepends=('openresolv: for DNS functionality'
	            'wireguard-dkms: wireguard module, built by dkms'
	            'wireguard-arch: wireguard module for linux'
	            'wireguard-lts: wireguard module for linux-lts')
        provides=('wireguard-tools')
        conflicts=('wireguard-tools')

	cd WireGuard

	make -C src/tools/ \
		DESTDIR="${pkgdir}/" \
		WITH_BASHCOMPLETION=yes \
                WITH_WGQUICK=yes \
                WITH_SYSTEMDUNITS=yes \
                install

	install -d -m0755 "${pkgdir}"/usr/share/${_pkgbase}/
	cp -r contrib/examples/ "${pkgdir}"/usr/share/${_pkgbase}/
}

