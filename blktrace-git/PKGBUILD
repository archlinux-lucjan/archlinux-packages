# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>

pkgname=blktrace-git
_pkgname=blktrace
pkgver=1.2.0.r33.ge81829a
pkgrel=2
pkgdesc="Userspace utilities for tracing Linux block layer I/O"
arch=('x86_64')
license=('GPL2')
url="http://brick.kernel.dk/snaps/"
depends=('libaio')
makedepends=('git')
conflicts=('blktrace')
provides=('blktrace')
source=("git://git.kernel.org/pub/scm/linux/kernel/git/axboe/blktrace.git"
        '0001-blkparse-Handle-cgroup-information.patch'
        '0002-iowatcher-Use-blktrace_api.h.patch'
        '0003-iowatcher-Handle-cgroup-information.patch')
sha256sums=('SKIP'
            '74d7032a883bd618c08756d75d7f32aef539dc9e63114d77235fa0520878094e'
            '67ac8d78e29755534e50d66a411a36ca2641b55f271cad1a1c78ea3025509b14'
            '1529c64394483f2fd8af28c325a6cf686fde282258b807926b187a10be821201')

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/^blktrace.//;s/\([^-]*-g\)/r\1/;s/-/./g'
}  

prepare() {
  cd $_pkgname
  patch -p1 -i ../0001-blkparse-Handle-cgroup-information.patch
  patch -p1 -i ../0002-iowatcher-Use-blktrace_api.h.patch
  patch -p1 -i ../0003-iowatcher-Handle-cgroup-information.patch
}

build() {
  cd $_pkgname
  make
}

package() {
  cd $_pkgname
  make prefix=/usr mandir=/usr/share/man DESTDIR=$pkgdir/ install
}
