# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>.
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=poppler-git
pkgname=('poppler-git' 'poppler-glib-git' 'poppler-qt5-git')
pkgver=0.83.0.r29.g78840bb7
pkgrel=1
arch=('x86_64')
license=('GPL')
makedepends=('libjpeg' 'gcc-libs' 'cairo' 'fontconfig' 'openjpeg2' 'gtk3' 'pkgconfig' 'lcms2' 
             'gobject-introspection' 'icu' 'qt5-base' 'git' 'nss' 'gtk-doc' 'curl' 'poppler-data'
             'cmake' 'python' 'boost')
options=('!emptydirs')
url="http://poppler.freedesktop.org/"
source=('git://git.freedesktop.org/git/poppler/poppler')
sha256sums=('SKIP')
_standdardversion=$(echo $pkgver | cut -c -6)

pkgver() {
  cd poppler

  git describe --long --tags | sed 's/^poppler.//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../poppler \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DENABLE_UNSTABLE_API_ABI_HEADERS=ON \
    -DENABLE_GTK_DOC=ON
  make
}

package_poppler-git() {
  pkgdesc="PDF rendering library based on xpdf 3.0"
  depends=('libjpeg' 'gcc-libs' 'cairo' 'fontconfig' 'openjpeg2' 'lcms2' 'nss' 'curl')
  optdepends=('poppler-data: encoding data to display PDF documents containing CJK characters')
  conflicts=("poppler-qt3<${_standdardversion}" 'poppler')
  provides=("poppler=${_standdardversion}" 'poppler')

  cd build
  make DESTDIR="${pkgdir}" install

  # cleanup for splitted build
  rm -vrf "${pkgdir}"/usr/include/poppler/{glib,qt5}
  rm -vf "${pkgdir}"//usr/lib/libpoppler-{glib,qt5}.*
  rm -vf "${pkgdir}"/usr/lib/pkgconfig/poppler-{glib,qt5}.pc
  rm -vrf "${pkgdir}"/usr/{lib,share}/gir*
  rm -vrf "${pkgdir}"/usr/share/gtk-doc
}

package_poppler-glib-git() {
  pkgdesc="Poppler glib bindings"
  depends=('poppler-git' 'glib2')
  conflicts=('poppler-glib')
  provides=( 'poppler-glib')

  cd build

  make -C glib DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 poppler-glib.pc "${pkgdir}/usr/lib/pkgconfig/"
  rm -vf "${pkgdir}"/usr/lib/libpoppler.*
  rm -vf "${pkgdir}/usr/bin/poppler-glib-demo"
}

package_poppler-qt5-git() {
  pkgdesc="Poppler Qt5 bindings"
  depends=('poppler-git' 'qt5-base')
  conflicts=('poppler-qt5')
  provides=('poppler-qt5')

  cd build
  make -C qt5 DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 poppler-qt5.pc "${pkgdir}/usr/lib/pkgconfig/"
}
