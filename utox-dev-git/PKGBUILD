# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Håvard Pettersson <mail@haavard.me>
# Contributor: Madotsuki <madotsuki at cock dot li>

_pkgname=utox
_branch=develop
pkgname=utox-dev-git
pkgver=0.16.1.137.g9f8486e3
pkgrel=1
pkgdesc='Lightweight Tox client'
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url='https://github.com/uTox/uTox'
license=('GPL3')
depends=('fontconfig'
         'libfilteraudio-git'
         'hicolor-icon-theme'
         'libdbus'
         'libxext'
         'libxrender'
         'openal'
         'toxcore'
         'v4l-utils')
makedepends=('check' 'git')
optdepends=('gtk3: GTK file picker')
provides=("utox")
conflicts=("utox")
source=("$_pkgname::git+https://github.com/uTox/uTox#branch=$_branch")
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  cd $_pkgname
  install -d build
}

build() {
  cd $_pkgname/build
  cmake .. -DCMAKE_INSTALL_PREFIX="/usr"
  make $MAKEFLAGS
}

package() {
  cd $_pkgname/build
  make DESTDIR="$pkgdir" install
}
