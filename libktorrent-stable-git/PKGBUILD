# $Id$
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libktorrent-stable-git
_pkgname=libktorrent
pkgver=2.2.0.4.g8fc9083
pkgrel=1
pkgdesc="A BitTorrent protocol implementation"
arch=(x86_64)
url="https://www.kde.org/applications/internet/ktorrent/"
license=(GPL2)
depends=(kio qca-qt5)
makedepends=(extra-cmake-modules python boost doxygen git)
conflicts=('libktorrent')
provides=('libktorrent')
source=('git+https://anongit.kde.org/libktorrent#branch=2.2')
sha1sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe  --long | sed 's/^v//;s/-/./g'
}

build() {
  cmake -B build -S $_pkgname
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
