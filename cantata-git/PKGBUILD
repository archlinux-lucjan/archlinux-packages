# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=cantata-git
pkgver=2.2.0.54.g478e5c95
pkgrel=1
pkgdesc="Qt5 graphical client for Music Player Daemon (MPD) - git version."
arch=(i686 x86_64)
url="https://github.com/cdrummond/cantata"
license=(GPL3)
depends=(qt5-svg libmtp libmusicbrainz5 mpg123 vlc taglib-extras media-player-info libcdio-paranoia udisks2)
optdepends=('perl-uri: dynamic playlist' 'mpd: playback' 'ffmpeg: ReplayGain support')
makedepends=(cmake qt5-tools ffmpeg git)
conflicts=(cantata)
provides=(cantata)
source=("$pkgname::git+https://github.com/CDrummond/cantata.git")
sha1sums=('SKIP')

pkgver() {
    cd "${pkgname}"
    git describe --long --tags | sed 's/^v//;s/-/./g'
}

prepare() {
    if [[ -d build ]]; then
        rm -rf build
    fi
        mkdir build
}

build() {
    cd build
    cmake ../${pkgname} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DENABLE_TOUCH_SUPPORT=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
