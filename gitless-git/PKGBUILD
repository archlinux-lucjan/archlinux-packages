# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>

pkgname=gitless-git
_pkgname=gitless
pkgver=0.9.16.r817.g039b1ae
pkgrel=1
pkgdesc="A scientifically proven easier-to-use git interface"
arch=("any")
url="https://github.com/goldstar611/gitless"
license=("MIT")
depends=('python' 'git' 'python-pygit2>=1.4.0' 'python-setuptools' 'python-argcomplete')
conflicts=('gitless')
provides=('gitless')
# source=("git+https://github.com/sirlucjan/gitless")
source=("git+https://github.com/goldstar611/gitless"
        '0001-Adds-gl-revert-a-wrapper-for-git-revert.patch')
sha256sums=('SKIP'
            'dfd6140d2845d469e9264d646616a4753cbc1a590239e3a68001212f81e4f559')

pkgver() {
  cd $_pkgname
  
  # git describe
  # git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  
  # other
  _ver="$(cat $_pkgname/__init__.py  | grep -m1 __version__ | grep -o "[[:digit:]]*" | paste -sd'.')"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
  cd $_pkgname
  
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_pkgname
  python setup.py build
}

package() {
  cd $_pkgname
  python setup.py install --root=${pkgdir} --optimize=1 --skip-build
  
  # copy custom license:
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
