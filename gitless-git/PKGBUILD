# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>

pkgname=gitless-git
_pkgname=gitless
pkgver=0.8.8.r2.g29fb463
pkgrel=2
pkgdesc="A scientifically proven easier-to-use git interface"
arch=("any")
url="http://gitless.com/"
license=("GPL")
depends=('python' 'git' 'python-sh' 'python-clint' 'python-pygit2>=0.28.1' 'python-setuptools')
makedepends=('git')
conflits=('gitless')
provides=('gitless')
source=("git://github.com/sdg-mit/gitless"
        '0001-Bump-libgit2.patch')
sha256sums=('SKIP'
            '904a4c2bcbdce26632699e7bcc229c8540b7671c4d179161f84a9d49a186ed43')

pkgver() {
  cd ${srcdir}/${_pkgname}
  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${srcdir}/${_pkgname}
  patch -p1 -i ../0001-Bump-libgit2.patch
}

build() {
  cd ${srcdir}/${_pkgname}
  python setup.py build
}

package() {
  cd ${srcdir}/${_pkgname}
  python setup.py install --root=${pkgdir} --optimize=1 --skip-build
}
