# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Caleb Maclennan <caleb@alerque.com>

pkgname=gitless-git
_pkgname=gitless
pkgver=0.9.17.r6.g8c3041c
pkgrel=1
pkgdesc='A scientifically proven easier-to-use git interface'
arch=('any')
url="https://github.com/goldstar611/gitless"
license=('MIT')
depends=('python' 'git' 'python-pygit2' 'python-argcomplete')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
conflicts=('gitless')
provides=('gitless')
# source=("git+https://github.com/sirlucjan/gitless")
source=("git+https://github.com/goldstar611/gitless")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname

  # git describe
  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'

  # other
  #_ver="$(cat $_pkgname/__init__.py  | grep -m1 __version__ | grep -o "[[:digit:]]*" | paste -sd'.')"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cd $_pkgname
  python -m build -wn
}

package() {
  cd $_pkgname
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.md
}
