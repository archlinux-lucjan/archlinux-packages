# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>

pkgname=gitless-git
_pkgname=gitless
pkgver=0.8.8.r89.g982e47a
pkgrel=1
pkgdesc="A scientifically proven easier-to-use git interface"
arch=("any")
url="http://gitless.com/"
license=("GPL")
depends=('python' 'git' 'python-sh' 'python-clint' 'python-pygit2>=1.1.1' 'python-setuptools' 'python-argcomplete')
makedepends=('git')
conflits=('gitless')
provides=('gitless')
# source=("git+https://github.com/sirlucjan/gitless")
source=("git+https://github.com/gitless-vcs/gitless"
        '0001-Update-requirements.patch')
sha256sums=('SKIP'
            'd7162b958c2ec2d2e824fca5584942b4388973c53e6c255cc36192252499de27')

pkgver() {
  cd $_pkgname
  
  # git describe
  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  
  # other
  # _ver="$(cat $_pkgname/cli/gl.py | grep -m1 __version__ | grep -o "[[:digit:]]*" | paste -sd'.')"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
  cd $_pkgname
  
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_pkgname
  python setup.py build
}

package() {
  cd $_pkgname
  python setup.py install --root=${pkgdir} --optimize=1 --skip-build
}
