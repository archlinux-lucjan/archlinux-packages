# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>

pkgname=gitless-git
_pkgname=gitless
pkgver=0.8.8.r51.geccad4a
pkgrel=3
pkgdesc="A scientifically proven easier-to-use git interface"
arch=("any")
url="http://gitless.com/"
license=("GPL")
depends=('python' 'git' 'python-sh' 'python-clint' 'python-pygit2>=1.1.0' 'python-setuptools')
makedepends=('git')
conflits=('gitless')
provides=('gitless')
# source=("git://github.com/sirlucjan/gitless")
source=("git://github.com/gitless-vcs/gitless"
        '0001-Bump-to-pygit-1.0.0.patch'
        '0002-Bump-to-pygit-1.1.0.patch')
sha256sums=('SKIP'
            '3935c12f4b2b60717976bd9ebb79647ca4715275bca9a89f3073e3bdacb04cb3'
            '86ba1f72b9b5ea78ef631c7d2c243ebf9881b87a4721a865c8e06a452ab08914')

pkgver() {
  cd $_pkgname
  
  # git describe
  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  
  # other
  # _ver="$(cat $_pkgname/cli/gl.py | grep -m1 __version__ | grep -o "[[:digit:]]*" | paste -sd'.')"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
  cd $_pkgname
  # Bump pygit2
  patch -p1 -i ../0001-Bump-to-pygit-1.0.0.patch
  patch -p1 -i ../0002-Bump-to-pygit-1.1.0.patch
}

build() {
  cd $_pkgname
  python setup.py build
}

package() {
  cd $_pkgname
  python setup.py install --root=${pkgdir} --optimize=1 --skip-build
}
