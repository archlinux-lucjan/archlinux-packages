# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: Jiachen Yang <farseerfc@gmail.com>
# Contributor: Håvard Pettersson <mail@haavard.me>
# Contributor: Kevin MacMartin <prurigro at gmail dot com>

_pkgname=qtox
pkgname=qtox-git
pkgver=1.11.0.r130.g9c01eec2
pkgrel=1
pkgdesc='Powerful Tox client written in C++/Qt that follows the Tox design guidelines. GTK free.'
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url='https://github.com/tux3/qTox'
license=('GPL3')
depends=('desktop-file-utils'
         'libxss'
         'openal'
         'qrencode'
         'ffmpeg'
         'qt5-svg'
         'sqlcipher'
         'toxcore')
makedepends=('git' 'qt5-tools')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$_pkgname::git+https://github.com/tux3/qTox.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $_pkgname
  mkdir -p build
  cd build
  ##qmake-qt5 ENABLE_SYSTRAY_UNITY_BACKEND=NO GIT_VERSION=$_gitver GIT_DESCRIBE=v$pkgver ..
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
	  -DGIT_VERSION=$_gitver \
	  -DENABLE_GTK_SYSTRAY=False \
	  -DENABLE_STATUSNOTIFIER=False \
	  -DGIT_DESCRIBE=v$pkgver 
  make
}

package() {
  cd $_pkgname
  cd build
  make DESTDIR="$pkgdir" install
}
