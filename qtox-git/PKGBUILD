# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: Jiachen Yang <farseerfc@gmail.com>
# Contributor: Håvard Pettersson <mail@haavard.me>
# Contributor: Kevin MacMartin <prurigro at gmail dot com>

pkgname=qtox-git
_pkgname=qtox
pkgver=1.17.2.231.g1c29152f9
pkgrel=1
pkgdesc='Powerful Tox client written in C++/Qt that follows the Tox design guidelines. GTK free.'
arch=('x86_64')
url='https://github.com/qTox/qTox'
license=('GPL3')
depends=('desktop-file-utils'
         'libxss'
         'openal'
         'qrencode'
         'ffmpeg'
         'qt5-svg'
         'libexif'
         'sqlcipher'
         'toxcore')
makedepends=('git' 'qt5-tools' 'cmake')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$_pkgname::git+https://github.com/qTox/qTox")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe  --long | sed 's/^v//;s/-/./g'
}

build() {
  cmake -B build -S $_pkgname \
           -DCMAKE_INSTALL_PREFIX=/usr \
	   -DENABLE_GTK_SYSTRAY=False \
	   -DENABLE_STATUSNOTIFIER=False \
	   -DGIT_DESCRIBE=$pkgver 
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
