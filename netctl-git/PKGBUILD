# Maintainer: Jouke Witteveen <j.witteveen@gmail.com>

pkgname=netctl-git
pkgver=1.25.0.gcaa6c43
pkgrel=1
pkgdesc='Profile based systemd network management'
url='http://projects.archlinux.org/netctl.git/'
license=('GPL')
depends=('coreutils' 'iproute2' 'openresolv' 'systemd')
# The source tarball includes pre-built (using asciidoc) documentation.
makedepends=('pkg-config' 'asciidoc')
optdepends=('dialog: for the menu based wifi assistant'
            'dhclient: for DHCP support (or dhcpcd)'
            'dhcpcd: for DHCP support (or dhclient)'
            'wpa_supplicant: for wireless networking support'
            'ifplugd: for automatic wired connections through netctl-ifplugd'
            'wpa_actiond: for automatic wireless connections through netctl-auto'
            'ppp: for PPP connections'
            'openvswitch: for Open vSwitch connections'
           )
conflicts=('netctl')
provides=('netctl')
source=(git+https://gitlab.archlinux.org/archlinux/netctl)
arch=('any')
md5sums=('SKIP')
#validpgpkeys=('CFA6AF15E5C74149FC1D8C086D1655C14CE1C13E')  # Florian Pritz

pkgver() {
  cd netctl
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

package() {
  cd netctl
  make DESTDIR="$pkgdir" install

  # Shell Completion
  install -D -m644 contrib/bash-completion "$pkgdir/usr/share/bash-completion/completions/netctl"
  ln -s netctl "$pkgdir/usr/share/bash-completion/completions/netctl-auto"
  ln -s netctl "$pkgdir/usr/share/bash-completion/completions/wifi-menu"
  install -D -m644 contrib/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_netctl"
}

