# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Imanol Celaya <ornitorrincos@archlinux-es.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dan Vratil <progdan@progdansoft.com>
# Contributor: thotypous <matiasΘarchlinux-br·org>
# Contributor: delor <bartekpiech gmail com>

pkgbase=qtcreator-git
pkgname=(qtcreator-git qtcreator-git-devel)
pkgver=8.0.0.beta2.114.g1257a4c5c3
_clangver=14.0.6
pkgrel=1
pkgdesc='Lightweight, cross-platform integrated development environment'
arch=('x86_64')
url='http://qt-project.org'
license=('LGPL')
depends=('qt6-tools' 'qt6-svg' 'qt6-quick3d' 'qt6-webengine' 'qt6-serialport' 'qt6-shadertools' 'qt6-5compat'
         "clang=$_clangver" 'clazy' 'yaml-cpp' 'litehtml') # syntax-highlighting
makedepends=('cmake' 'llvm' 'python' 'git')
options=('docs')
optdepends=('qt6-doc: integrated Qt documentation'
            'qt6-examples: welcome page examples'
            'gdb: debugger'
            'cmake: cmake project support'
            'x11-ssh-askpass: ssh support'
            'git: git support'
            'mercurial: mercurial support'
            'bzr: bazaar support'
            'valgrind: analyze support'
            'perf: performer analyzer'
            'mlocate: locator filter')
conflicts=('qtcreator')
provides=('qtcreator')
source=("git://code.qt.io/qt-creator/qt-creator.git")
md5sums=('SKIP')

pkgver() {
    cd qt-creator
    git describe --long --match v* | sed 's/^v//;s/-/./g'
}

build() {
  cmake -B build -S qt-creator \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DWITH_DOCS=ON \
    -DBUILD_DEVELOPER_DOCS=ON \
    -DBUILD_QBS=OFF \
    -DQTC_CLANG_BUILDMODE_MATCH=ON \
    -DBUILD_HELVIEWERBACKEND_QTWEBENGINE=ON
  cmake --build build
  cmake --build build --target docs
}

package_qtcreator-git() {
  DESTDIR="$pkgdir" cmake --install build
# Install docs
  cp -r build/share/doc "$pkgdir"/usr/share

  install -Dm644 qt-creator/LICENSE.GPL3-EXCEPT "$pkgdir"/usr/share/licenses/qtcreator/LICENSE.GPL3-EXCEPT
}

package_qtcreator-git-devel() {
  pkgdesc+=' (development files)'
  depends=('qtcreator-git')
  optdepends=()

  DESTDIR="$pkgdir" cmake --install build --component Devel
}
