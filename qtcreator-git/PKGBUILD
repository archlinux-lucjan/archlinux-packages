# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Imanol Celaya <ornitorrincos@archlinux-es.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dan Vratil <progdan@progdansoft.com>
# Contributor: thotypous <matiasΘarchlinux-br·org>
# Contributor: delor <bartekpiech gmail com>

pkgname=qtcreator-git
pkgver=4.13.0.318.gf450c369a3
_clangver=10.0.1
pkgrel=1
pkgdesc='Lightweight, cross-platform integrated development environment'
arch=('x86_64')
url='http://qt-project.org'
license=('LGPL')
depends=('qt5-tools' 'qt5-quickcontrols' 'qt5-quickcontrols2' 'qt5-webengine' "clang=$_clangver" 'qbs' 'clazy' 'syntax-highlighting' 'desktop-file-utils')
makedepends=('llvm' 'python' 'git')
options=('docs')
optdepends=('qt5-doc: integrated Qt documentation'
            'qt5-examples: welcome page examples'
            'qt5-translations: for other languages'
            'gdb: debugger'
            'cmake: cmake project support'
            'x11-ssh-askpass: ssh support'
            'git: git support'
            'mercurial: mercurial support'
            'bzr: bazaar support'
            'valgrind: analyze support'
            'perf: performer analyzer')
conflicts=('qtcreator')
provides=('qtcreator')
source=("git://code.qt.io/qt-creator/qt-creator.git")
md5sums=('SKIP')

pkgver() {
    cd qt-creator
    git describe --long --match v* | sed 's/^v//;s/-/./g'
}
        
prepare() {
    mkdir -p build
    
    cd qt-creator

    # fix hardcoded libexec path
    sed -e 's|libexec\/qtcreator|lib\/qtcreator|g' -i qtcreator.pri
    sed -e 's|libexec|lib|g' -i src/tools/tools.pro
    # use system qbs
    rm -r src/shared/qbs
}

build() {
    cd build

    qmake LLVM_INSTALL_DIR=/usr QBS_INSTALL_DIR=/usr \
    KSYNTAXHIGHLIGHTING_LIB_DIR=/usr/lib KSYNTAXHIGHLIGHTING_INCLUDE_DIR=/usr/include/KF5/KSyntaxHighlighting \
    CONFIG+=journald QMAKE_CFLAGS_ISYSTEM=-I \
    DEFINES+=QBS_ENABLE_PROJECT_FILE_UPDATES \
    "$srcdir"/qt-creator/qtcreator.pro
    make
    make docs
}

package() {
    cd build

    make INSTALL_ROOT="$pkgdir/usr/" install
    make INSTALL_ROOT="$pkgdir/usr/" install_docs

    install -Dm644 "$srcdir"/qt-creator/LICENSE.GPL3-EXCEPT "$pkgdir"/usr/share/licenses/qtcreator/LICENSE.GPL3-EXCEPT
}
