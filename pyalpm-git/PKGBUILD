# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Yen Chi Hsuan <yan12125 at gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

_pkgname=pyalpm
pkgname=$_pkgname-git
pkgver=0.8.3.0.g284d480
pkgrel=1
pkgdesc="Libalpm bindings for Python 3 (Git version)"
arch=('x86_64')
url="https://projects.archlinux.org/git/pyalpm.git"
license=('GPL')
depends=('python>=3.6' 'pacman>=5.0')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
makedepends=('git')
source=(
    'git+https://projects.archlinux.org/git/pyalpm.git'
)
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --long | sed 's/^v//;s/-/./g'
}

build() {
  cd "$srcdir/$_pkgname"
  python setup.py build
}

package() {
  cd "$srcdir/$_pkgname"
  python setup.py install --root=${pkgdir}
}

