# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Yen Chi Hsuan <yan12125 at gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

_pkgname=pyalpm
pkgname=$_pkgname-git
pkgver=0.10.6.7.g54d9077
pkgrel=1
pkgdesc="Libalpm bindings for Python 3 (Git version)"
arch=('x86_64')
url="https://projects.archlinux.org/git/pyalpm.git"
license=('GPL')
depends=('python' 'pacman')
makedepends=('git' 'python-setuptools' 'python-pytest' 'python-pkgconfig' 'python-pytest-pacman')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
source=("git+https://gitlab.archlinux.org/archlinux/pyalpm.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --long | sed 's/^v//;s/-/./g'
}

prepare() {
  cd "$srcdir/$_pkgname"
}

build() {
  cd "$srcdir/$_pkgname"
  python setup.py build
}

check() {
  cd "$srcdir/$_pkgname"
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.10" pytest
}

package() {
  cd "$srcdir/$_pkgname"
  python setup.py install --root=${pkgdir}
}

