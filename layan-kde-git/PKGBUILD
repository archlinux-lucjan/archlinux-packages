# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>

pkgbase=layan-kde-git
_pkgbase=Layan-kde
pkgname=('layan-kde-git' 'kvantum-theme-layan-git')
pkgver=20200402.r29.gab5b330
pkgrel=1
pkgdesc="Layan theme for KDE Plasma 5"
arch=('any')
url="https://github.com/vinceliuice/${_pkgbase}"
license=('GPL3')
options=('!strip')
source=("${_pkgbase}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd ${_pkgbase}
  echo "$(git show --format='%cI' -q master | sed 's/T.*//g;s/-//g').r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

package_layan-kde-git() {
    optdepends=('kvantum-theme-layan-git: Layan theme for Kvantum Qt style (recommended)'
                'tela-icon-theme-git: for a more consistent and beautiful experience (recommended)')
    conflicts=('layan-kde')
    provides=('layan-kde')
    cd ${_pkgbase}
    install -d "$pkgdir"/usr/share
    
    rm sddm/{README.md,install.sh}

    cp -r aurorae "$pkgdir"/usr/share
    cp -r color-schemes "$pkgdir"/usr/share
    cp -r plasma "$pkgdir"/usr/share
    cp -r sddm "$pkgdir"/usr/share
}

package_kvantum-theme-layan-git() {
    pkgdesc="Layan theme for Kvantum"
    depends=('kvantum-qt5')
    provides=('kvantum-theme-layan')
    conflicts=('kvantum-theme-layan')
    cd ${_pkgbase}

    install -d "$pkgdir"/usr/share

    cp -r Kvantum "$pkgdir"/usr/share
}
