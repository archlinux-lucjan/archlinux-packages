# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgbase=modemmanager-test-git
pkgname=(modemmanager-test-git libmm-glib-test-git)
pkgver=1.14.0.1.g19416aa7
pkgrel=1
pkgdesc="Mobile broadband modem management service"
arch=(x86_64)
url="http://www.freedesktop.org/wiki/Software/ModemManager/"
license=(GPL2 LGPL2.1)
depends=(systemd libgudev polkit ppp libqmi libmbim)
makedepends=(intltool gtk-doc gobject-introspection autoconf-archive vala git)
source=(git://anongit.freedesktop.org/ModemManager/ModemManager#branch=mm-1-14)
sha256sums=('SKIP')

pkgver() {
  cd ModemManager

  git describe --tags | sed 's/-rc/rc/;s/-/./g'
}

prepare() {
  cd ModemManager
  NOCONFIGURE=1 ./autogen.sh

}

build() {
  cd ModemManager
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --with-dbus-sys-dir=/usr/share/dbus-1/system.d \
    --with-udev-base-dir=/usr/lib/udev \
    --with-polkit=permissive \
    --with-systemd-journal \
    --with-systemd-suspend-resume \
    --enable-compile-warnings=yes \
    --enable-gtk-doc \
    --disable-static

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

#check() {
#  cd ModemManager
#  make -k check
#}

package_modemmanager-test-git() {
  depends+=(libmm-glib.so libg{lib,object,io,module}-2.0.so libsystemd.so libgudev-1.0.so
            libqmi-glib.so libmbim-glib.so)
  optdepends=('usb_modeswitch: install if your modem shows up as a storage drive')
  options=(!emptydirs)
  conflicts=('modemmanager')
  provides=('modemmanager')

  cd ModemManager
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" -C libmm-glib uninstall
  make DESTDIR="$pkgdir" -C vapi uninstall

  # Some stuff to move is left over
  mv "$pkgdir/usr/include" ..
  mv "$pkgdir/usr/lib/pkgconfig" ..
}

package_libmm-glib-test-git() {
  pkgdesc="ModemManager library"
  depends=(libg{lib,object,io}-2.0.so)
  conflicts=('libmm-glib')
  provides=('libmm-glib' 'libmm-glib.so')

  install -d "$pkgdir/usr/lib"
  mv include "$pkgdir/usr"
  mv pkgconfig "$pkgdir/usr/lib"

  cd ModemManager
  make DESTDIR="$pkgdir" -C libmm-glib install
  make DESTDIR="$pkgdir" -C vapi install
}
