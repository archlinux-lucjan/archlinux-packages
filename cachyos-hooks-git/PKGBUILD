# Maintainer: Michael Bolden <me@sm9.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=cachyos-hooks-git
gitname=cachyos-hooks
pkgver=20230328.r25.g1f68450
pkgrel=3
pkgdesc='Cachy OS Pacman Hooks'
groups=('cachyos')
arch=('any')
license=('GPL3')
url="https://github.com/sirlucjan/$gitname"
source=("git+https://github.com/sirlucjan/cachyos-hooks")
sha512sums=('SKIP')
makedepends=('git')
replaces=('cachyos-hooks' 'systemd-boot-pacman-hook')
provides=('cachyos-hooks' 'systemd-boot-pacman-hook')
conflicts=('cachyos-hooks' 'systemd-boot-pacman-hook')

pkgver() {
  cd $gitname
  echo "$(git show --format='%cI' -q master | sed 's/T.*//g;s/-//g').r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

package() {
  cd $gitname
  local hooks="$pkgdir/usr/share/libalpm/hooks"
  local bin="$pkgdir/usr/bin"

  install -Dm644 lsb-release.hook                 "$hooks/lsb-release.hook"
  install -Dm644 os-release.hook                  "$hooks/os-release.hook"
  install -Dm644 ${gitname}.hook                  "$hooks/${gitname}.hook"
  install -Dm644 cachyos-reboot-required.hook     "$hooks/cachyos-reboot-required.hook"
  install -Dm644 cachyos-systemd-boot.hook        "$hooks/95-cachyos-systemd-boot.hook"

  install -Dm755 cachyos-reboot-required          "$bin/cachyos-reboot-required"
  install -Dm755 cachyos-check-reboot-required    "$bin/cachyos-check-reboot-required"
  install -Dm755 ${gitname}-runner                "$bin/${gitname}-runner"
}
