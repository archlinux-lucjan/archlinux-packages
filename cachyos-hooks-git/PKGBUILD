# Maintainer: Michael Bolden <me@sm9.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=cachyos-hooks-git
gitname=cachyos-hooks
pkgver=20250513.r45.g3f7f531
pkgrel=1
pkgdesc='Cachy OS Pacman Hooks'
groups=('cachyos')
arch=('any')
license=('GPL3')
url="https://github.com/sirlucjan/$gitname"
source=("git+https://github.com/sirlucjan/cachyos-hooks")
sha512sums=('SKIP')
depends=('systemd')
makedepends=('git')
replaces=('cachyos-hooks' 'systemd-boot-pacman-hook')
provides=('cachyos-hooks' 'systemd-boot-pacman-hook')
conflicts=('cachyos-hooks' 'systemd-boot-pacman-hook')

pkgver() {
  cd $gitname
  echo "$(git show --format='%cI' -q master | sed 's/T.*//g;s/-//g').r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

package() {
  cd $gitname

  install -Dm644 lsb-release.hook "$pkgdir/usr/share/libalpm/hooks/lsb-release.hook"
  install -Dm644 os-release.hook "$pkgdir/usr/share/libalpm/hooks/os-release.hook"
  install -Dm644 cachyos-hooks.hook "$pkgdir/usr/share/libalpm/hooks/cachyos-hooks.hook"
  install -Dm644 cachyos-reboot-required.hook "$pkgdir/usr/share/libalpm/hooks/cachyos-reboot-required.hook"
  install -Dm644 cachyos-systemd-boot.hook "$pkgdir/usr/share/libalpm/hooks/95-cachyos-systemd-boot.hook"
  install -Dm755 cachyos-reboot-required "$pkgdir/usr/bin/cachyos-reboot-required"
  install -Dm755 cachyos-hooks-runner "$pkgdir/usr/bin/cachyos-hooks-runner"
}
