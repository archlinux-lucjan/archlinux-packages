# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.15.r159.g596f3d37
pkgrel=2
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")

_backports=(
e5e72503530f9b1db759803b8712405388eae747 # scx_loader: Add chaos scheduler
15bba2f58e2b1263af6b8fdca097ac11ea17ec39 # meson: install scx_mitosis and scx_wd40
a1421cd6fe86087b1a7ffbbf2460bfe5202886b9 # scx_loader: Add mitosis scheduler
e278e558d87206fc478aa817f49127c7475caa1e # scx_loader: Add wd40 scheduler
40d9f20420cd7a04931e387a8b8941dedbfe9163 # scxctl: cache DBUS results to eliminate redundant calls
1cb4233be0a72fd9cab923598a9e0b025887ea2a # scxctl: optimize scheduler list display with iterator patterns
cd7a0c127dfb5cfb72ea1f33202b5f5f8035d8fa # scxctl: use &str parameters for string utility functions
f92fb32b2afd98d8c64f528435ffb3983fc4b155 # scxctl: replace unwrap() calls with proper error handling
9aabea10c30ff9af80acad39483e453c205b3148 # scxctl: extract helper functions to reduce duplication
232ba48dc1fb1ef6c57c8ad236699cd944502d24 # scxctl: simplify string processing utility functions
a814399b813132c943978f82d9da9800882bd24c # scxctl: some Rust formatting changes to satisfy the CI
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
