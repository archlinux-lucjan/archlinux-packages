# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Tejun Heo <tj@kernel.org>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.16.r244.g7b065707
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=(
  jq
  libseccomp
  libelf
  zlib
)
makedepends=(
  cargo
  clang
  git
  llvm
  llvm-libs
  python
)
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')

_backports=(
05870733132dfac6242633810acbb797768ad69f # scx_cosmos: Improve SMT handling
873b8e434c4094ca8c2b3c13b729e32bc71f1073 # scx_cosmos: Prioritize faster CPUs
83e3c9322a6ccd3f6eec209b858cc6e07a7d3522 # scx_cosmos: Drop flat/preferred idle scan
b10073281719f9dfc1055a90f726df529003c2dc # scx_bpfland: Rework idle CPU selection and scheduling policy
4286113f790abce971523ad7aa5eb372147c6db0 # scx_bpfland: Introduce --wake-affine
fed47c1c70eff6c706f92379e9a29467b981d57e # scheds: Introduce scx_beerland
1ccc3e078c1c13d8c5885f5d08fc74ebbb8a8652 # scx_loader: Add beerland scheduler
03f442a595f9ef1e8f557b01075997e32aee0dc0 # EXPERIMENTAL: scx_beerland: Use lockless peek (requires kernel patch)
955ac08e5d7c4e9b8d00d7f9af94663eb54c9ed9 # scx_beerland: Better SMT handling
38e02855ca363d5e029cb4d36a52dd1e19c1eae6 # scx_beerland: Prevent migrations for non-overloaded CPUs
27754bc22bf4f5302c29d74577593945f9b00440 # scx_beerland: Re-enqueue tasks on CPU preempted by a higher sched class
fedede518d6267713f671b3b79077159db9a513a # scx_loader: Add chaos scheduler
dbb85c816ca8a766d1066a8e780488ee42619cd5 # scx_loader: Error when non-auto modes do not have args
a66565742e50e757c2e4d6144dd5ee33f093d1a8 # scxctl: Implement error handling
cb390e9bfa4eaffd44ac24106752a4024f20b675 # scx_bpfland: some Rust formatting changes to satisfy the CI
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

   export RUSTUP_TOOLCHAIN=stable
   cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_mitosis \
     --exclude scx_wd40 \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

#check() {
#  cd $_gitname
#  export RUSTUP_TOOLCHAIN=stable
#  cargo test \
#     --frozen \
#     --all-features \
#     --workspace \
#     --exclude scx_rlfifo \
#     --exclude scx_mitosis \
#     --exclude scx_wd40 \
#     --exclude scxcash \
#     --exclude xtask \
#     --exclude vmlinux_docify \
#     --exclude scx_arena_selftests
#}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +

  # systemd service
  install -Dm644 services/systemd/scx_loader.service \
    -t "$pkgdir/usr/lib/systemd/system/"

  # D-Bus service
  install -Dm644 services/systemd/org.scx.Loader.service \
    -t "$pkgdir/usr/share/dbus-1/system-services/"

  # D-Bus configuration
  install -Dm644 tools/scx_loader/org.scx.Loader.conf \
    -t "$pkgdir/usr/share/dbus-1/system.d/"

  # scx_loader sample configuration (template)
  install -Dm644 services/scx_loader.toml \
    "$pkgdir/usr/share/scx_loader/config.toml"
}
