# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.13.r444.g77229cbd
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")

_backports=(
f182f903604ea355ceeb26de9899bcc827111737 # scx_flash: Track precise CPU load
ace1ae5a67c6900bfba752245cd16a611bbf4038 # scx_flash: Normalize CPU utilization in the capacity range
e494cea7ba4cfaffde9d44bd53652e6eae538cf7 # scx_flash: Introduce --slice-lag-scaling
5274e69c4cf0341a74b2f2bc8e76a573eb9fb2b8 # scx_flash: Add --direct-dispatch and revise --local-pcpu behavior
6721507ac9246661b705b6d2496d2cd3682444fe # scx_flash: Introduce --no-builtin-idle
4e3330cf4aaedaf2495f25ed133b767d690a0135 # scx_flash: Refactoring to avoid unnecessary task lookup
18da4930ef925cad8eb0ad179b7cd2821c9612e8 # scx_flash: Always direct dispatch in round-robin mode
11499a2f73705732d6a750425ebd1a54912f531f # scx_flash: Always scale time slice in function of the waiting tasks
b7ce64e62038d6174094d614b4500212e16e7091 # scx_loader: Update profiles for scx_flash with direct dispatch
4d8fb5409c7ca62ecc1f6509d6abf923707cedb1 # meson: install scx_mitosis and scx_wd40
ab28abe933d467b306b0b4aacb422804aa23260f # scx_loader: Add wd40 scheduler
f185f267a4e0b5bb009456d5ee220d92bdd9d255 # scx_loader: Add chaos scheduler
7aedc95559c808ecb64bbf23871f08bc4546edfd # scx_loader: Add mitosis scheduler
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
