# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Tejun Heo <tj@kernel.org>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.16.r86.g38b66a3e
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=(
  jq
  libseccomp
  libelf
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')

_backports=(
39f42d656b583980c840c07cf7014bf87c9ef0fe # scx_loader: Add chaos scheduler
f0166cc2df1521aa486fc5d52f7b39d8cd198e14 # scx_loader: Add mitosis scheduler
af8cf8edb75c15af2705ea038e9123b5ad242570 # scx_loader: Add wd40 scheduler
9ebf2a0009a6882d0b2206e61755d49c176695f2 # scxctl: cache DBUS results to eliminate redundant calls
b45174d63489a5803b0aead2a478b56e76723111 # scxctl: optimize scheduler list display with iterator patterns
bc22cb6cab98a8a9ed7a7b06df6bbf74cf5d8d38 # scxctl: use &str parameters for string utility functions
3d0e6890408ef5085f3dc2f54fb502dbe9b4d5ad # scxctl: replace unwrap() calls with proper error handling
8e6665bb90a4302c6b1db79ea4af5804f2d2c8b0 # scxctl: extract helper functions to reduce duplication
42eef6f10a76225fccd928ea42c12516ae2da963 # scxctl: simplify string processing utility functions
1f4876a4f185666a92a933d243e14ca831f7f744 # scxctl: some Rust formatting changes to satisfy the CI
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

   cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --workspace \
     --locked \
     --frozen \
     --exclude scx_rlfifo \
     --exclude vmlinux_docify \
     --exclude arenalib_selftests
}

#check() {
#  cd $_gitname
#  cargo test \
#     --workspace \
#     --locked \
#     --frozen \
#     --exclude scx_rlfifo \
#     --exclude vmlinux_docify \
#     --exclude arenalib_selftests
#}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +

  # systemd service
  install -Dm644 services/systemd/scx_loader.service \
    -t "$pkgdir/usr/lib/systemd/system/"

  # D-Bus service
  install -Dm644 services/systemd/org.scx.Loader.service \
    -t "$pkgdir/usr/share/dbus-1/system-services/"

  # D-Bus configuration
  install -Dm644 tools/scx_loader/org.scx.Loader.conf \
    -t "$pkgdir/usr/share/dbus-1/system.d/"

  # scx_loader sample configuration (template)
  install -Dm644 services/scx_loader.toml \
    "$pkgdir/usr/share/scx_loader/config.toml"
}
