# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.13.r160.g65ed48ac
pkgrel=3
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx"
        '0001-meson-install-scx_mitosis-and-scx_wd40.patch'
        '0002-scx_loader-Add-missing-schedulers.patch')
sha256sums=('SKIP'
            '3a525d3c3e46d874d659040345a544cf522f16cde5420e27171698d3fc41fc16'
            'd560e9c4e228912fc4e3ad02a715c1d8ff7c1d37bffc27b1dca6734947bb66a6')
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")

_backports=(
d36784571fc5076d1559ac4b79729968abdca159 # scx_flash: Introduce tickless mode
9b0ff70679488de2d25abc16a32e18fb6cd65573 # scx_flash: Update nvcsw statistics only on task sleep
38563ff5ed1ffbbad39a54b08757d34873db646a # scx_flash: Reduce impact of nvcsw on sleeping time budget
c1cb088a0ff9fd1ac2c09e6a23209e80d0643dbd # scx_flash: Re-enable voluntary context switch rate prioritization
d3b2730917ebebec218dfe64608cf72e96e80b5c # scx_flash: Always prefer reusing the same CPU
7829d06974bf886e385833e9ecdc31fe23749046 # scx_flash: Save some cpumask ops when primary domain includes all CPUs
d160f1ad6d9b01b7dd1526705298d01373f32192 # scx_flash: Always rebalance per-CPU DSQs
b6642c4bea11d81fa9671a64db1bd29fda41ca6b # scx_flash: Increase maximum accounted runtime by 10x
b5943dade042b083bbc47263d37e23fadc0513e9 # scx_utils: Look up a performance state by CPU utilization.
ff332fedf3453bd3e337857440d624af0d8c8ad6 # scx_utils: Add Eq, Hash, and Ord traits to PerfDomain/State.
93e7661adc2883fbb2b661b43fa56b990be711af # scx_utils: Add get_total_performance() to EnergyModel.
057c1e72d84cb9d6f7072d9bab3cd1378a35e0b2 # scx_lavd: Add EnergyModelOptimizer.
ab07cd94b9f56dc0f1c619afca1ecafc636e5003 # scx_lavd: Generate a fake perf_cpuset_table when the energy model is not available.
e38ed19c84af80f8043e2ec50c69392c5cd0f7d9 # scx_lavd: Use perf_cpu_order for the BPF scheduler.
f571c5d36eb4426dc72aa5172df418ea546207f0 # scx_p2dq: Refactor dispatch path
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
