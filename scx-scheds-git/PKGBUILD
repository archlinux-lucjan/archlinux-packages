# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.5.r43.ga673dcf
pkgrel=4
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds")
conflicts=("scx-scheds")

_backports=(
9a29547e5ba24166f4959afa54343b516eacde2b # scx_bpfland: rework lowlatency mode
f6a3d10185edbf23c723f7bf937ed971c8b3ccf7 # WIP: scx_bpfland: handle user-space futex critical sections
d6383d771ce1aa970864fc7766b7efb99377208f # scheds: introduce scx_fair
75cc8199ed125c6bcaa33cd47e1e8e219912ae79 # scx_fair: introduce lowlatency mode
34a7c0cd295d6aa865cf31c8dbd19d1774328195 # scx_fair: use cast_mask() from common.bpf.h
002346035ef6a758a4de57027be9c4776847588a # scx_fair: always give tasks a chance to run on an idle CPU
006ee2dbfc3d7aea2665e96da8a5a1fceda95e39 # scx_fair: introduce per-CPU DSQs
fe017e35182facc22a62d534526d6a1609e8d09d # scx_fair: apply minimum vruntime when task is dispatched
bc583ae7048032fa9bb2404536f28b881160ac89 # scx_fair: do not scale the assigned time slice based on task priority
1a05901f82c0510d6d0e6a404bedabf8418add04 # scx_fair: make lowlatency default and drop it as an option
a5d324cd40fcf81e501c010797341d2ce0a4d542 # scx_fair: prevent per-CPU DSQ stall with per-CPU kthreads
6ec5c4ed615895bf9477feaa58274601fb7a2c0f # WIP: scx_fair: handle user-space futex critical sections
7bd61f43342143838deb0dc1c5db280890c8143e # scx_loader: Add SwitchScheduler methods to DBUS interface
5414d052f16f520da68cb831185678c7978a409a # Drop journald from libalpn hooks
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
