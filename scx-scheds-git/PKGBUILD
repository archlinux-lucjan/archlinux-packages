# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
gitname=scx
pkgver=0.1.9.r40.g07b521b
pkgrel=2
pkgdesc="sched_ext schedulers"
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq')
makedepends=('git' 'python' 'meson' 'lucjan-meson' 'clang' 'llvm' 'llvm-libs' 'cargo' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx"
        '90-scx-scheds-install.hook'
        '90-scx-scheds-upgrade.hook')
sha256sums=('SKIP'
            '3d1723596ccc6b4774692a0bd675b74b49f8d3f88ffc833a6b74140a75744a97'
            'b9b945aba7af30be158ee9ac288499427693dd869352b35c114f116ffcd18998')
options=(!lto)
replaces=("scx-git" "scx-systemd-services-git")
provides=("scx-scheds")
conflicts=("scx-scheds")

_backports=(
)

_reverts=(
)

pkgver() {
  cd $gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
 cd $gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $gitname
  lucjan-meson . build
  meson compile -C build
}

package() {
  cd $gitname
  meson install -C build --destdir "${pkgdir}"
  install -Dm644 "${srcdir}/90-scx-scheds-install.hook" "${pkgdir}/usr/share/libalpm/hooks/90-scx-scheds-install.hook"
  install -Dm644 "${srcdir}/90-scx-scheds-upgrade.hook"  "${pkgdir}/usr/share/libalpm/hooks/90-scx-scheds-upgrade.hook"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
