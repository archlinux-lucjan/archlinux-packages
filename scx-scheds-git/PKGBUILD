# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
gitname=scx
pkgver=0.1.8.r37.g7b4d231
pkgrel=3
pkgdesc="sched_ext schedulers"
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq')
makedepends=('git' 'python' 'meson' 'lucjan-meson' 'clang' 'llvm' 'llvm-libs' 'cargo' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx"
        'meson.patch')
sha256sums=('SKIP'
            '38070638041b410cf0a87fb656679b2952b9d4459d6365d2832cbd180b81eae5')
options=(!lto)
replaces=("scx-git" "scx-systemd-services-git")
provides=("scx-scheds")
conflicts=("scx-scheds")

_backports=(
d87bb99b6a16835ce48e87e0c170ef198151e4a8 # scx_rustland_core: introduce dispatch flags
ebeed6c778ddde213c264b0887b5694205c6cee1 # scx_rustland_core: introduce preemption
4287e77697cac6eec41b263a900b60451284cf97 # scx_rustland: enable preemption
7c8121332f96afe82b5a5ef2cfb509a5a7fafe3a # scx_rustland: introduce --no-preemption
)

_reverts=(
)

pkgver() {
  cd $gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
 cd $gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $gitname
  lucjan-meson . build
  meson compile -C build
}

package() {
  cd $gitname
  meson install -C build --destdir "${pkgdir}"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
