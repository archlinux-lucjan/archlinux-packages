# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Tejun Heo <tj@kernel.org>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.16.r246.gaf9d2062
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=(
  jq
  libseccomp
  libelf
  zlib
)
makedepends=(
  cargo
  clang
  git
  llvm
  llvm-libs
  python
)
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')

_backports=(
b5a4976f6a4957fee947ef5907cca41147076730 # scx_cosmos: Improve SMT handling
ab1018c7272cd708d67338c5fee614a4b20b0dd9 # scx_cosmos: Prioritize faster CPUs
204a000049c98f487fd64ee310e04a78fd134146 # scx_cosmos: Drop flat/preferred idle scan
ce5796d3bff53cb6ea207b038f8b9f76c812c1b5 # scx_bpfland: Rework idle CPU selection and scheduling policy
c8eacda42e226089ae98961e5ebbce6d24837d5e # scx_loader: Update performance profiles for scx_bpfland
ddf5bee291dfe7fdcb79e270e9052a37decf7022 # scheds: Introduce scx_beerland
4d7b43e6cabe9621065ec1b25278c53282e29370 # scx_loader: Add beerland scheduler
2e297532588c24efab7bb05aa582d5bd4a3459b8 # EXPERIMENTAL: scx_beerland: Use lockless peek (requires kernel patch)
cd3f1068be09551d5d2ef216623b95e7520e9fe2 # scx_beerland: Better SMT handling
8ced4a03013da629e73506c4803da9e00f626b55 # scx_beerland: Prevent migrations for non-overloaded CPUs
b97d047932f33d5ab3df3649b877b805f3944d9a # scx_beerland: Re-enqueue tasks on CPU preempted by a higher sched class
6ba47d683e452438666f76a7ac9a369329a5157b # scx_loader: Add chaos scheduler
efc10410708a9f16e10b07bc557176193f922015 # scx_loader: Error when non-auto modes do not have args
772c646e69905e38ece7bfbcec5cf2dafdb1d3a1 # scxctl: Implement error handling
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

   export RUSTUP_TOOLCHAIN=stable
   cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_mitosis \
     --exclude scx_wd40 \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

#check() {
#  cd $_gitname
#  export RUSTUP_TOOLCHAIN=stable
#  cargo test \
#     --frozen \
#     --all-features \
#     --workspace \
#     --exclude scx_rlfifo \
#     --exclude scx_mitosis \
#     --exclude scx_wd40 \
#     --exclude scxcash \
#     --exclude xtask \
#     --exclude vmlinux_docify \
#     --exclude scx_arena_selftests
#}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +

  # systemd service
  install -Dm644 services/systemd/scx_loader.service \
    -t "$pkgdir/usr/lib/systemd/system/"

  # D-Bus service
  install -Dm644 services/systemd/org.scx.Loader.service \
    -t "$pkgdir/usr/share/dbus-1/system-services/"

  # D-Bus configuration
  install -Dm644 tools/scx_loader/org.scx.Loader.conf \
    -t "$pkgdir/usr/share/dbus-1/system.d/"

  # scx_loader sample configuration (template)
  install -Dm644 services/scx_loader.toml \
    "$pkgdir/usr/share/scx_loader/config.toml"
}
