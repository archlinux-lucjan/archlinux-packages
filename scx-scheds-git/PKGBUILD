# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.15.r189.g1b3dc924
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")

_backports=(
a11d0782b2cc1bb95a69be067877508fd0909180 # scx_loader: Add chaos scheduler
0c8a6f16ee1e0184c9198c8ceacd8a69629a5357 # meson: install scx_mitosis and scx_wd40
298c000e8917f653f1744add8be03fb1c5fdd126 # scx_loader: Add mitosis scheduler
7976c21cc7e383b330a2de4379e071f4ee4f9165 # scx_loader: Add wd40 scheduler
23f60e4e2dbc394dfbfb75baf4e85747c1d9c7fd # scxctl: cache DBUS results to eliminate redundant calls
6f209f941a3f0f05977dfca7d27d7a9a881bec11 # scxctl: optimize scheduler list display with iterator patterns
c4330527e5e94490611e6ee0f71de23dc99c5b3c # scxctl: use &str parameters for string utility functions
89bbf280a974db6cb9ef8c47a58b5052c2c0c9d9 # scxctl: replace unwrap() calls with proper error handling
763b96fc09577e0e36884bcdda99cfa4db034121 # scxctl: extract helper functions to reduce duplication
c9ee74c96bd2a4204488443255af11a79f7ad8cb # scxctl: simplify string processing utility functions
05588297d8802b93737dabfd1c5c3a9ba67eee70 # scxctl: some Rust formatting changes to satisfy the CI
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
