# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.12.r203.g4ca0a063
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")

_backports=(
db9a1a1d98cb255a89119ffe5e0b5dc84b5664d7 # scx_rustland_core: Initialize task dsq_vtime and slice
32ed1dcc36f88fe4a13748378c4c2eca6aa0a49b # scx_rustland_core: Dispatch per-CPU tasks to their only usable CPU
81c27af2a27af140492cb76f2cca4ae0aa9462f9 # scx_rustland_core: Avoid sched_setscheduler() in non-partial mode
682bcbc01081d36f71593f04ccbcaf452fbcfa21 # scx_rustland_core: Use BPF_RB_NO_WAKEUP
e9b2a3d5c932a5921976d370a843e94664b6629f # scx_rustland_core: Introduce a separate DSQ for the user-space scheduler
3a35f9bc6b082b765b6eb4df3e9586cd4b386050 # scx_rustland_core: Do not allow per-CPU kthreads to preempt other tasks
05a6f12425b423964c615a5953afca0f52daa244 # scx_rustland_core: Use SCX_LOCAL_DSQ / SCX_LOCAL_DSQ_ON consistently
c7f06ab74cbdbb0992129323280b91f76d74175e # scx_rustland_core: Drop cpumask generation counter
6dceee635a5229aa82948248e9a26f05878c9c9a # scx_rustland_core: Expose nr_cpus_allowed to user-space
df316a0e01113a4525bea963123fd48117d8438a # scx_rustland: Introduce --percpu-local
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
