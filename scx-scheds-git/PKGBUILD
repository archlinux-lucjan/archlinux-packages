# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.14.r418.geeb76692
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")

_backports=(
3a193d9f44a6595ab578623d3414c1525c1f00eb # scxctl: cache DBUS results to eliminate redundant calls
e4161badf9783c4fff5e518fa587dc9d7e80aba5 # scxctl: optimize scheduler list display with iterator patterns
905078f62896584c533ff73001ec3adfbc614450 # scxctl: use &str parameters for string utility functions
d5ce7e2958f8a44b2074ea5a3341bf94ba6b9a55 # scxctl: replace unwrap() calls with proper error handling
b8a3b8cbb89957c8400ba641e53f362260b1dcb3 # scxctl: extract helper functions to reduce duplication
d20984c33653c8f7b5ccbc30db6e7071386e2ab8 # scxctl: simplify string processing utility functions
ce6da1d07fcfee1eaa48d25472b261a06734c363 # scxctl: some Rust formatting changes to satisfy the CI part 2
72ca3beecd1cc8e93981c4998669f4293104da3e # scx_loader: Add chaos scheduler
75c0c1c28cb0a079598d960fb4c86b47e4ac34a4 # meson: install scx_mitosis and scx_wd40
7d5be1c38754b838b8b48765290f82da8fe018de # scx_loader: Add mitosis scheduler
ad36bdf4766a1d974c92f7259a5a86bb552d1224 # scx_loader: Add wd40 scheduler
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
