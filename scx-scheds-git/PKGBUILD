# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Tejun Heo <tj@kernel.org>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.17.r232.gfae363ac
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=(
  jq
  libseccomp
  libelf
  zlib
)
makedepends=(
  cargo
  clang
  git
  llvm
  llvm-libs
  python
)
optdepends=(
'scx-tools-git: scx_loader and scxtl - A DBUS Interface for Managing sched_ext Schedulers '
)
options=(!lto)
provides=("scx-scheds")
conflicts=("scx-scheds")
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')

_backports=(
b72049137aaf2326d69c664f63d90369516dbe42 # scx_bpfland: Rethink per-CPU tasks prioritization
b0abb05f0afd3395c40d4b5d5e654fdd95ff1637 # scx_bpfland: Dynamic time slice
0a0c7ed0ef12b1fbf3286f7d3c65d02a093a6982 # scheds: Introduce scx_beerland
e0192bae7640e4710a3982a38ec25257f98c43f9 # EXPERIMENTAL: scx_beerland: Use lockless peek (requires kernel patch)
0876f01970ffcb4feb975d1bb98d778a6d25a0b6 # scx_beerland: Better SMT handling
fb8bb7af5ab7ddcca4cdbdc58bbe9b425ad6b546 # scx_beerland: Prevent migrations for non-overloaded CPUs
1337a4e22a67e0ab433b1eec27f725828077958b # scx_beerland: Re-enqueue tasks on CPU preempted by a higher sched class
81b7bd06d8e216f2358cf724b8afbaf4a2ad7bce # scx_beerland: Attempt to wakeup idle CPUs only when needed
d875a24de16b1b22e373dce2b30951c9ab90bd5b # scx_beerland: Introduce --preferred-idle-scan / -P
c02ff7ff0ff73dba61b49f81fb426f3f57c7db33 # scx_beerland: Always dispatch from any CPU when system is busy
35ee894bb91ece57b354a661f5237007e567c3bc # scx_beerland: Keep using the same CPU if system is busy
37310c04620bdc82cb4b5174063d84e4d5c3897f # scx_beerland: Use deadline-based scheduling
fbe9be188a06e45cf44ad9a0c155acebd9a1ee34 # scx_beerland: Try to keep I/O-bound task on the same CPU
53ea0abae3d9277e51b43e4e50fab359adddc25b # scx_cosmos: Improve SMT handling
9692caaec3df8cceb4e2c0384b7f9386e2fa15d3 # scx_cosmos: CPU capacity awareness
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

   local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

   export RUSTUP_TOOLCHAIN=stable
   cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_mitosis \
     --exclude scx_wd40 \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

#check() {
#  cd $_gitname
#  export RUSTUP_TOOLCHAIN=stable
#  cargo test \
#     --frozen \
#     --all-features \
#     --workspace \
#     --exclude scx_rlfifo \
#     --exclude scx_mitosis \
#     --exclude scx_wd40 \
#     --exclude scxcash \
#     --exclude xtask \
#     --exclude vmlinux_docify \
#     --exclude scx_arena_selftests
#}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
