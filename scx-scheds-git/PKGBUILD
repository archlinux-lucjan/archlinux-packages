# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: Tejun Heo <tj@kernel.org>

# Available profiles: “release”, “release-tiny”, “release-fast“
# See: https://github.com/sched-ext/scx/blob/main/Cargo.toml
_mode=release

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.19.r184.g6b195847
pkgrel=2
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=(
  jq
  libseccomp
  libelf
  zlib
)
makedepends=(
  cargo
  clang
  git
  llvm
  llvm-libs
  python
)
optdepends=(
'scx-tools: scx_loader and scxctl - A DBUS Interface for Managing sched_ext Schedulers '
)
options=(!lto)
provides=("scx-scheds")
conflicts=("scx-scheds")
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')

_backports=(
cbee8f4ef887ff79ec774af54d85384a4e93e5b1 # Add scx_cake scheduler to workspace
9ca46d1c1b112f16c7403f6d1f60cd8da052ecac # Add scx_cake scheduler to workspace
b6e82e6fa52e07b1c91d43baecdbe7b7c829f745 # scx_cake: Use local path dependencies instead of git
1eacde1136c87213a6aba9dd5a0adf7afb48b2a7 # Update Cargo.lock for scx_cake
bfcd14e98ebe93ecb5a3be637eb860a20b11616c # scx_cake: Run cargo fmt
2d3943e9935fb58dee1a877b81b418b05cc3e8f4 # scx_cake: add kernel 6.16+ compatibility for scx_bpf_cpu_curr
433dfc7ef6e35ebc3907c48804c226d62f988eae # scx_cake: Implement Check & Skip Optimizations and SMT Fix
6863e419a6aa7412c6e0eee69515eec90fbff953 # perf sched map optimization
b44e4faedc3c6ac575ecb5b9d014b4b14359b033 # feat(scx_cake): implement state-based per-cpu direct dispatch
7e5012ee7e918c4bf301727f4c2d3d2739d41f16 # scx_cake: make clippy happy
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --profile=$_mode \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/$_mode \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
