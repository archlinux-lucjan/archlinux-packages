# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
gitname=scx
pkgver=0.1.10.r209.gfc0d1bd
pkgrel=3
pkgdesc="sched_ext schedulers"
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx' 'etc/systemd/journald@sched-ext.conf')
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds")
conflicts=("scx-scheds")

_backports=(
d440b1819f9f72da397b51b7f23af0b977b0dc60 # scx-scheds: Add After=graphical.target into service (third attempt)
c8a60e2566c6387aeed27190feb9764023035dbf # libalpm: make sure that symlink will be moved
8dd528abfdeee4f6f0c4a1754a420efa3c91bc7d # scx_bpfland: pass enqueue flags when dispatching kthreads
6a64182ef23e4083843c51551b612c661e84cfa9 # scx_bpfland: always classify interactive tasks
995577762a6b6513802126ab00ce123a6e0efc49 # scx_bpfland: refill task time slice
afe3b8875484b03fdaf8d9936a75e963d73ac868 # scx_bpfland: introduce dynamic nvcsw threshold
d1064a817fc64e4d59889fd1fc447fcd8921f27e # scx_bpfland: introduce a tunable for the max nvcsw threshold
7f3d942721d1c9babca11011791ed413fa44ed4d # scx_bpfland: prevent per-CPU kthreads starving other tasks
88752885a64962b7235f1b9c4b7866f1e7c68a4b # scx_bpfland: always give a full timeslice to interactive tasks
0ccfa724904d72d1152c04768e6082e72c3ca8b4 # scx_bpfland: fix typo in slice_ns definition
68d65f9ee01d33c1a365e4a5afdbaaa52e1d24bd # scx_bpfland: bump up max time slice to 20ms
c8d6efa15937e1b2db9af0b36d7438704020c8e9 # scx_bpfland: show cpu load in verbose mode
)

_reverts=(
)

pkgver() {
  cd $gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
 cd $gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $gitname
  arch-meson . build -D openrc=disabled
  meson compile -C build
}

package() {
  cd $gitname
  meson install -C build --destdir "${pkgdir}"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
