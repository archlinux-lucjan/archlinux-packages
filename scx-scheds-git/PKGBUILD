# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.15.r270.gdc12a846
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")

_backports=(
4b979f982de8ebcfcc96a146f3508fe3e787eccb # scx_loader: Add chaos scheduler
39c8849d57a495c72ce232d869961aa45c8bc2c7 # meson: install scx_mitosis and scx_wd40
61d6ca4fcdbd2bdc0e5adaedb928f0ad1d0699fe # scx_loader: Add mitosis scheduler
4ff43dcd1cf48959986df9d8cbc6d0da0168364c # scx_loader: Add wd40 scheduler
83ec45917fd73ff68c798e9b2096213fe38454dc # scxctl: cache DBUS results to eliminate redundant calls
c7e9523769ca38d055fc53fe311d158cf92fe85d # scxctl: optimize scheduler list display with iterator patterns
01f4a7d8cd8328ab0e014d181050c3d79a80f931 # scxctl: use &str parameters for string utility functions
84f33315fa99192f4c2030c69c5e0a0611291adc # scxctl: replace unwrap() calls with proper error handling
3c72d438ae91bfbadd928d1e7a595577223114a0 # scxctl: extract helper functions to reduce duplication
4b49f8ac648001238c0fdfb6422b172720383639 # scxctl: simplify string processing utility functions
ceba2387ed717311d40b5608f9cfd613b59947f5 # scxctl: some Rust formatting changes to satisfy the CI
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
