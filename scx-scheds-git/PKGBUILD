# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: Tejun Heo <tj@kernel.org>

# Available profiles: “release”, “release-tiny”, “release-fast“
# See: https://github.com/sched-ext/scx/blob/main/Cargo.toml
_mode=release

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.19.r274.gdb76dafa
pkgrel=2
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=(
  jq
  libseccomp
  libelf
  zlib
)
makedepends=(
  cargo
  clang
  git
  llvm
  llvm-libs
  python
)
optdepends=(
'scx-tools: scx_loader and scxctl - A DBUS Interface for Managing sched_ext Schedulers '
)
options=(!lto)
provides=("scx-scheds")
conflicts=("scx-scheds")
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')

_backports=(
e31949e9cea5c1b9cf53d94d8d44c54d8d602983 # Add scx_cake scheduler to workspace
1bc88a06cf766f6d3329c95f628d74211974a4d2 # Add scx_cake scheduler to workspace
036ff10d36da0a51a9420e45be9db7a5bcc94be0 # scx_cake: Use local path dependencies instead of git
806f29c20aeb7b24b28e0be84d74520a0f9684e2 # Update Cargo.lock for scx_cake
5d68d3570c7cc31df659cee82ca71d2127c0d6d9 # scx_cake: Run cargo fmt
9c8ec671ac7583fb9267faa5b9459dd5a11f977d # scx_cake: add kernel 6.16+ compatibility for scx_bpf_cpu_curr
416385bf4ca72eca4600f1998a886ea1a6fe748d # scx_cake: Implement Check & Skip Optimizations and SMT Fix
03f505e01ed420186b66a22f522fe83103ca95bb # perf sched map optimization
64dc7a94738761e947cd5af6f38c1dcd11569e55 # feat(scx_cake): implement state-based per-cpu direct dispatch
d4d6fdb084925bf4db860650090a55a6b4d592ce # Ghost Scheduler Optimizations: Store coalescing and bit testing
76191c300368a449eeb9d265e6b6b558de354be7 # bug fixes, floor for runtime, healthy IPC
9b8a71dd084391e1acc01903923d1f07ef66177c # Great benchmarks - need to review jitter
ae8d93a975dd9165f96588cebc299baadd9dc8ca # scx_cake: Implement Zero-Math Locality Arbiter
d6d8a9f99966dfe77206919e23446e31716dab2e # scx_cake: warning removed from build
02f8956cf1b05f9315ab7029eafcf39adf3ddf56 # struct alignment 4 byte for clang 19+
8d674bd87c9dc4ab5ddeafaebe7d4690363fa88d # BPF compat: Use semantic macros for 32-bit atomics to fix Clang 19 crash
87950a0de8c060206c2bc927e0cfa151d194c93d # BPF: Optimize MLP by migrating volatile loads to relaxed semantic macros
19ff2ff42023a331db8be2a3a265459a44be4b31 # BPF: Bulletproof compat layer with inline ASM for Clang < 21
5e74b2f0384ddc79bc0de81a07d1e3897958b976 # Checkpoint: Immediate Consumption pattern - baseline 53 spills restored
1d1772a256e4b16aca3c8f70fc6daad6c194da46 # Absolute Zero Spill: Eliminate remaining BPF spills in cake_select_cpu (53 -> 0). Verified with llvm-objdump.
23c3c80897b85a4d4d66e343dd78fa4c62efa66e # Optimize: Implement more aggressive warmth seeking (Stubborn Warmth)
e1980ac0ff55d2e1c09b867c0a8f8e179e259c15 # perf(cake): remove volatile from 15 rodata declarations for JIT constant folding
6327185c5c5b644da0d4d0f83ad2065c12eb6618 # feat(cake): v1.01 with clean ANSI startup splash
bbf34fd40a0b0a61ded1380950e7a73011842f7c # chore(cake): bump version to 1.0.1
da7af274bcd6d2ccf083542bcb21a647b06c730d # scx_cake: Remove LOCK prefixes from victim_mask ops (5 atomics → 2)
7e372b509d2ae13c03a9fa11fde1713e4a98542f # scx_cake: Phase 2 tiered masks - 40-60% reduced atomic hit rate
3eb82590555f8cfa1d25971bc3e51dd84b6d1fe8 # scx_cake: cake_update_idle zero-spill optimization
70a3f9437c8250cc19e7bfd9f9ac495a717c2097 # scx_cake: eliminate all BPF register spills via cold path optimization
4d125be9a17bc937f74fb0241c26d66851a1370d # scx_cake: add cache-line padding to cold_scratch
36c5fc942d67cf59e082d9788b65e422a83817e3 # scx_cake: Empirical Topology Discovery (ETD) System Review
cf53618273c3b3c52adb6fbc1f556aa0b5f337aa # spill fixes
2a2dbd0a502048287d5d50fc682a4ebbbb07687e # fix(cake): prioritize cache warmth over ETD to reduce jitter
f30a327be85000d1bd106657111c51c06649cb5c # cold path optimizations
36a919d88d8af24a7335a344e8af08eb9d3f42c5 # optimization leading to better fps, lower latency and better 50% frame distribution
533fa48081623eca429bb51c3f47af6816923c21 # fuse optimization round 1 passed
19ff581221781dab48386cc187d628603843d6ca # fuse round 2 pass
a40ea8a06a79b8619a48430bc9451f053eb56b20 # MESI-friendly warm-tier updates: skip-if-unchanged optimization
98a0f368e2254f828430f9e0a06e0d44309c0c5f # skip-if-unchanged optimization for packed_info in cake_stopping
62679ccd770eb06d32f7e5eaa79750a8861071ae # cache line separation for tiered_idle_mask to eliminate false sharing
ce519cbaf210acd13efb35a9295b84289df5e342 # branchless clamp in compute_sparse_score using nested ternary
be9f6898413b4233b0f50c490b45b21f20246de0 # scx_cake: fix build errors in calibrate test and improve signal handling consistency
7761ef31e77420fb47354890011973d8e17c2e07 # Refactor: Topology-Agnostic 'Physical-First' isolation and Zero-Math Arbiter implementation
ab19ef64d232dd1b350e5eb97c19fe4d65790957 # Perf: BPF atomic optimizations (TTAS) and build.rs flag adjustments
0c3386f9076e60ee811bd07b85a96d306cc1417d # scx_cake: resolved clippy warnings and applied cargo fmt
20ea378a1cf1fd2e56685d3bdba436b9b8b0cf3b # spill fixes and jitter optimizations, new highscore benchmark arc raiders
edc97ac43f51a9f5777b08fa801052de66d3e244 # schbernch improvements round 1 passed
bc3dbec7526cedd8cc2c3aecba54eb846bb4fa3f # Phase 1: Memory load hoisting in cake_running
67861aba6cec7405ddebdd5472a0c8fd7a5bbf95 # Phase 2: ANDN bit-clearing patterns in cake.bpf.c
dab2e4e04d507f2335523dec4deeac2c032922b1 # Phase 3: BEXTR Bitfield Extraction in cake.bpf.c
e58519fb373a918643420c2da11e0efb7924e791 # Phase 4: TZCNT Enforcement in cake.bpf.c
78992d2e0908439d45656a94d897ff26616ff409 # Phase 5: CMOV Cascade in cake_select_cpu
af7567d7bf1539c141896d4d33ce7bfbd13a4d80 # Phase 6: Constant Hoisting & Snapshotting in cake_select_cpu
cc6f22dec9abed3ff039b730b7a188647842cdc1 # experimental optimization via assembly
ae18b63e707b65a064ac30f2a015dc27ede66f8a # lib/pmu: make start/stop methods public
ce170e81913c56031623a93d2e847dc990b68944 # scx_cosmos: use PMU library for perf event tracking
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --profile=$_mode \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/$_mode \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
