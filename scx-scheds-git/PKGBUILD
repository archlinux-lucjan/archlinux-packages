# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.15.r272.g023cae7d
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")

_backports=(
f2a1fb46973fffe6cd6717fa2aa381cb0d8fd602 # scx_loader: Add chaos scheduler
944f0d7b06a642a22e92006ea93b075711d0d222 # meson: install scx_mitosis and scx_wd40
f13ad5bf6e8f74c4940f2f6cac8ab78cbc21808b # scx_loader: Add mitosis scheduler
ae64fe9d066d4a22ce479457c25379582845a721 # scx_loader: Add wd40 scheduler
05279c27f38d66179d4426ccade5fd2d53bd05a9 # scxctl: cache DBUS results to eliminate redundant calls
64e9c3ff85bf3c4738219e401f31cd256dde75cd # scxctl: optimize scheduler list display with iterator patterns
eba9fa75625aa076f790ecdff63fa6462849524d # scxctl: use &str parameters for string utility functions
76a3eba205a69272714441a3f3883d813bf80eaf # scxctl: replace unwrap() calls with proper error handling
933be22b884a0a08fd67d9c6939663cdc69567c5 # scxctl: extract helper functions to reduce duplication
a0602581e505ff5a58c9dc05c2af372762d44c1d # scxctl: simplify string processing utility functions
c1c6bd1a959f39167c91c771dab49eb8f91e7b92 # scxctl: some Rust formatting changes to satisfy the CI
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
