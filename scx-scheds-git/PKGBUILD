# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Tejun Heo <tj@kernel.org>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.17.r7.ge2ba5845
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=(
  jq
  libseccomp
  libelf
  zlib
)
makedepends=(
  cargo
  clang
  git
  llvm
  llvm-libs
  python
)
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')

_backports=(
d03807525df2a5574a3a995affd274317e027cc5 # scx_cosmos: Improve SMT handling
d28777eb1eef767271c1f36b6ceea984a47ebd5f # scx_cosmos: Prioritize faster CPUs
596c31021b8cac914a2d31df5ae07ded0e707174 # scx_cosmos: Drop flat/preferred idle scan
7a366aaaf4788377422866dc91e65807526f6d25 # scx_bpfland: Rework idle CPU selection and scheduling policy
6ed9d3108a1170d1d54ad4bbd62059bbb2fd0d65 # scx_loader: Update performance profiles for scx_bpfland
32bd801cca78627bf5dec8d5e891d9fb9d583bf8 # scx_bpfland: Introduce --no-sticky-tasks
dc0c871938bb580991aec54a4e2bd4043d01f700 # scheds: Introduce scx_beerland
f55a8a811ed144ef981fc0ac0cc50b3809290775 # scx_loader: Add beerland scheduler
86475894223a19d4474806c78823e8d580dbfd4c # EXPERIMENTAL: scx_beerland: Use lockless peek (requires kernel patch)
816f9a43f6cdb57386a7c07642c8d2c587bacdb5 # scx_beerland: Better SMT handling
7d72c939a2889ae35e2a6927bd7ea68e3ee0c2c0 # scx_beerland: Prevent migrations for non-overloaded CPUs
b75683eb00d6a9a7053c02fc8c076c4493a6070d # scx_beerland: Re-enqueue tasks on CPU preempted by a higher sched class
41337d0eedff8d631a52704cce8838257776190f # scx_loader: Add chaos scheduler
ce1774b20d13c57d2b2b6b91d976d45be5766e40 # scx_loader: Error when non-auto modes do not have args
e3763132f6dad9a101ea2357efd43bab5de39633 # scxctl: Implement error handling
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

   export RUSTUP_TOOLCHAIN=stable
   cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_mitosis \
     --exclude scx_wd40 \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

#check() {
#  cd $_gitname
#  export RUSTUP_TOOLCHAIN=stable
#  cargo test \
#     --frozen \
#     --all-features \
#     --workspace \
#     --exclude scx_rlfifo \
#     --exclude scx_mitosis \
#     --exclude scx_wd40 \
#     --exclude scxcash \
#     --exclude xtask \
#     --exclude vmlinux_docify \
#     --exclude scx_arena_selftests
#}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +

  # systemd service
  install -Dm644 services/systemd/scx_loader.service \
    -t "$pkgdir/usr/lib/systemd/system/"

  # D-Bus service
  install -Dm644 services/systemd/org.scx.Loader.service \
    -t "$pkgdir/usr/share/dbus-1/system-services/"

  # D-Bus configuration
  install -Dm644 tools/scx_loader/org.scx.Loader.conf \
    -t "$pkgdir/usr/share/dbus-1/system.d/"

  # scx_loader sample configuration (template)
  install -Dm644 services/scx_loader.toml \
    "$pkgdir/usr/share/scx_loader/config.toml"
}
