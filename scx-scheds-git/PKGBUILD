# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.13.r358.g12b7e451
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx"
        '0001-meson-install-scx_mitosis-and-scx_wd40.patch'
        '0002-scx_loader-Add-missing-schedulers.patch')
sha256sums=('SKIP'
            '5866c8979ac2dd6e15ef90ecce761cce75c93435e26f7739e926fb1b1cdfa29a'
            '8b615c1ab99c04a7cd42665b44e5172b82d7f5d384d9733063583c7ef770d41f')
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")

_backports=(
b7d519e2e4a155a8a72b9c9f59d9ebb496279cc4 # scx_flash: Small optimization in cpus_share_llc()
d05e6c0feeb2ed2eb0c7d2af06ee1236e3c7aa63 # scx_flash: Clarify --local-pcpu implementation
72b89dc86aa85734928f1d73df1be9f1320f5a89 # scx_flash: Soften primary domain affinity to favor full-idle SMT cores
b362da9a68fcdfd0c7644d517a2ed00232ef9d82 # scx_flash: Smooth target performance level
4703edcb7f4d9ac9808ac08194b3ea9b6da7c845 # scx_flash: Get rid of optional DSQs
56fc0eff1f03fe30558544813e3f138c1391dfda # scx_flash: Introduce --sticky-cpu
845c418265c40fb694abe1432fbc1a2ba453c532 # scx_flash: Use per-CPU DSQs in function of CPU utilization
8c947ec1b0c4d73e5deb14b4906e13b48842fef4 # scx_flash: Drop per-CPU kthread sync wakeup optimization
6e8ed1f4be885c7bfec365853178928152231c37 # scx_flash: Simplify nvcsw evaluation
17290f4da303fb4609022a581360e5fba57552fc # scx_flash: Re-enable voluntary context switch prioritization by default
6af4c8162145e5daf0e1e657152cacbf7d79e254 # scx_flash: Introduce --cpu-busy-thresh
8760b3f9fb166458a4d77fdfaf972e603be93504 # scx_flash: Tune default options for latency
493118f98d8a98e6b2a829ee759f6718086bd603 # scx_loader: Update performance profiles for scx_flash
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
