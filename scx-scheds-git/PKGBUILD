# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.13.r158.g3859f066
pkgrel=8
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx"
        '0001-meson-install-scx_mitosis-and-scx_wd40.patch'
        '0002-scx_loader-Add-missing-schedulers.patch')
sha256sums=('SKIP'
            '3a525d3c3e46d874d659040345a544cf522f16cde5420e27171698d3fc41fc16'
            'd560e9c4e228912fc4e3ad02a715c1d8ff7c1d37bffc27b1dca6734947bb66a6')
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")

_backports=(
d36784571fc5076d1559ac4b79729968abdca159 # scx_flash: Introduce tickless mode
9b0ff70679488de2d25abc16a32e18fb6cd65573 # scx_flash: Update nvcsw statistics only on task sleep
38563ff5ed1ffbbad39a54b08757d34873db646a # scx_flash: Reduce impact of nvcsw on sleeping time budget
c1cb088a0ff9fd1ac2c09e6a23209e80d0643dbd # scx_flash: Re-enable voluntary context switch rate prioritization
d3b2730917ebebec218dfe64608cf72e96e80b5c # scx_flash: Always prefer reusing the same CPU
7829d06974bf886e385833e9ecdc31fe23749046 # scx_flash: Save some cpumask ops when primary domain includes all CPUs
d160f1ad6d9b01b7dd1526705298d01373f32192 # scx_flash: Always rebalance per-CPU DSQs
d53a1414b114ada85268bc7dc4d7de4e70be9e20 # scx_utils: Look up a performance state by CPU utilization.
5491900e828c52ef02c204f98169d7747d328e9a # scx_utils: Add Eq, Hash, and Ord traits to PerfDomain/State.
6e990a672fcf20c8024aa60f0e31760f1346d847 # scx_utils: Add get_total_performance() to EnergyModel.
c307403630035e03c63b980bcfc2d1d87d4edf30 # scx_lavd: Add EnergyModelOptimizer.
0022b66b653542397eb8c1203d2c54eab3c15dea # scx_lavd: Generate a fake perf_cpuset_table when the energy model is not available.
8cdeda57e2c2024d332dc04954113beb8f4fa349 # scx_lavd: Use perf_cpu_order for the BPF scheduler.
f571c5d36eb4426dc72aa5172df418ea546207f0 # scx_p2dq: Refactor dispatch path
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
