# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
gitname=scx
pkgver=0.1.10.r213.g3df7a13
pkgrel=2
pkgdesc="sched_ext schedulers"
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx' 'etc/systemd/journald@sched-ext.conf')
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds")
conflicts=("scx-scheds")

_backports=(
d440b1819f9f72da397b51b7f23af0b977b0dc60 # scx-scheds: Add After=graphical.target into service (third attempt)
c8a60e2566c6387aeed27190feb9764023035dbf # libalpm: make sure that symlink will be moved
67ca4995e442a7dced0e9b41aa21697c6e398978 # scx_bpfland: introduce dynamic nvcsw threshold
85d114839150911b722af57195312d03c7edf398 # scx_bpfland: introduce a tunable for the max nvcsw threshold
514dcc75b240435f8313c338c9ccadd4d7f72bab # scx_bpfland: improve producer/consumer workloads
6f6db9c1e516f5cd05d75c6f1449388054c10bcc # scx_bpfland: prevent per-CPU kthreads starving other tasks
370233f81da7c075ca04bc5a29a47eff4a42bc6c # scx_bpfland: fix typo in slice_ns definition
cee7c9d007355cb93bbc5fcd137f7bdbfab15df2 # scx_bpfland: prevent slice delta overflow
ff1447c5d4b85d416446a0b95c5a04084ad259fd # scx_bpfland: account unused time slice in sched_yield()
1ea19a979958628412898546014a2c697de328a2 # scx_bpfland: report interactive tasks to stdout
0ece4f24af9e9ad9ed3b91b0e63bab109b21d3ed # scx_bpfland: kick idle CPUs after dispatch to optimize task consumption
659da3ec52850f09a13cdb2b646a0b3568e7f469 # scx_bpfland: bump up max time slice to 20ms
)

_reverts=(
)

pkgver() {
  cd $gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
 cd $gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $gitname
  arch-meson . build -D openrc=disabled
  meson compile -C build
}

package() {
  cd $gitname
  meson install -C build --destdir "${pkgdir}"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
