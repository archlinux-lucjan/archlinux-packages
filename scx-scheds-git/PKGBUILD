# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.5.r62.geb73005
pkgrel=2
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds")
conflicts=("scx-scheds")

_backports=(
721a326b9d034ff592237bde7bae478efb10e3e2 # WIP: scx_bpfland: handle user-space futex critical sections
b6a7ce500be7387532c48fa7c65120598e339801 # scheds: introduce scx_fair
88a4b64634c25c894d6e74991327b8e78622306d # scx_fair: introduce lowlatency mode
52955ffacb68f0837f501041510096183e01f2af # scx_fair: use cast_mask() from common.bpf.h
cc2c24912eba7587b0abe188f509209742db37ed # scx_fair: always give tasks a chance to run on an idle CPU
ae2570c6259dfe0ad285faa905d2b294e64a5f1e # scx_fair: introduce per-CPU DSQs
4900a2725c97b6ea6fbdb2e5d6f40f10bfde4c66 # scx_fair: apply minimum vruntime when task is dispatched
388e14af57c1fffdef3c0bd1ed68c9c574ab2060 # scx_fair: do not scale the assigned time slice based on task priority
73bb973da3bdc9a96195f318d88f4769039cc1fe # scx_fair: make lowlatency default and drop it as an option
fd07d12361443f864a6dfd7994baa1490438e856 # scx_fair: skip idle CPU selection for tasks with migration disabled
23345b43bc3f11e999818e188f734f9267aaff93 # WIP: scx_fair: handle user-space futex critical sections
6fc9c24ed63f191adb6d0b79f5361ec4fc31700c # WIP: scx_fair: prevent per-CPU DSQ stall with per-CPU kthreads
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
