# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.13.r273.gebf1a268
pkgrel=8
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx"
        '0001-meson-install-scx_mitosis-and-scx_wd40.patch'
        '0002-scx_loader-Add-missing-schedulers.patch')
sha256sums=('SKIP'
            'c695654f073f3d392874b17c987473591456aacae132684a98f7f69332f9b810'
            '34fb4e422e22b403d280b1b7327f04757bdb4e008975645a528ff01d149ca638')
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")

_backports=(
cd6cfb109bf94954ab1ed6392b4422cf36c94384 # scx_flash: Remove the tickless timer
9b823da03f05d4f2ed3c41d50752dde50bcf1bf3 # scx_flash: Fix no-SMT sync wakeup logic
1cae37ae693ff9be554d0a04d1b9262fe05319a7 # scx_flash: Introduce --interactive-us-lag
965c5057985829deeaf10bb008abc6a0126ba65b # scx_flash: Get rid of -N and -C options
74c0533a4401ef6a681b93940ca340d45dec6576 # scx_flash: Enable per-CPU kthread prioritization by default
7c9f9dace15731863cffc1b82ccbb855bce9f888 # scx_flash: Simplify nvcsw evaluation
aa6a620111d0736a825dd9e13c9bf219947fd306 # scx_flash: Re-enable voluntary context switch prioritization by default
bd88087db674d91dd33dbd58aa97d898dee9087e # scx_flash: Rely on the built-in idle CPU selection policy if available
e0f04c6e82585ce14d6e8fb2916786f79e48565f # scx_flash: Introduce --no-builtin-idle
f6074fb587bf4b5cd45eedd35f2650d99be1add1 # scx_flash: Introduce --no-sticky-cpu
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
