# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
gitname=scx
pkgver=0.1.10.r42.g3407582
pkgrel=2
pkgdesc="sched_ext schedulers"
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq')
makedepends=('git' 'python' 'meson' 'lucjan-meson' 'clang' 'llvm' 'llvm-libs' 'cargo' 'rust')
backup=('etc/default/scx' 'etc/systemd/journald@sched-ext.conf')
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
replaces=("scx-git" "scx-systemd-services-git")
provides=("scx-scheds")
conflicts=("scx-scheds")

_backports=(
bbd3132b8e12f922d6781dc6ff970d241c869a97 # scheds: Add scx_mitosis scheduler to /etc/default/scx
e9ae539dee4edd6f4932d5f2ff45d06016fe13bd # rusty: Refactor + slightly improve wake_sync
878002ee0e2d8ed2a25f6d9b58408a0ff0fe2269 # rusty: Clean up dispatch logic
fc919d78555d7158a596546508ee421294a3a913 # scx_rustland: prevent excessive starvation when system is congested
bf4c45373feee764ed69ef65e10a0c91f5e0dd34 # scx_rustland_core: use SCX_DSQ_LOCAL only with kthreads
90d7a78cec159846d84569c1d1559deddcdb0acf # scx_rustland_core: allow user-space scheduler to preempt other tasks
7046b47b9cefe3dffdfe5e8f22e9e3a3a1b23552 # scx_lavd: properly calculate task's runtime after suspend/resume
)

_reverts=(
)

pkgver() {
  cd $gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
 cd $gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $gitname
  lucjan-meson . build
  meson compile -C build
}

package() {
  cd $gitname
  meson install -C build --destdir "${pkgdir}"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
