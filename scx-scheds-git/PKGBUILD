# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
gitname=scx
pkgver=0.1.10.r209.gfc0d1bd
pkgrel=8
pkgdesc="sched_ext schedulers"
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx' 'etc/systemd/journald@sched-ext.conf')
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds")
conflicts=("scx-scheds")

_backports=(
d440b1819f9f72da397b51b7f23af0b977b0dc60 # scx-scheds: Add After=graphical.target into service (third attempt)
c8a60e2566c6387aeed27190feb9764023035dbf # libalpm: make sure that symlink will be moved
8dd528abfdeee4f6f0c4a1754a420efa3c91bc7d # scx_bpfland: pass enqueue flags when dispatching kthreads
6a64182ef23e4083843c51551b612c661e84cfa9 # scx_bpfland: always classify interactive tasks
995577762a6b6513802126ab00ce123a6e0efc49 # scx_bpfland: refill task time slice
a0fab633730cca7463c27feeaf1b4fb54240314d # scx_bpfland: introduce dynamic nvcsw threshold
5e805876e250cd83a1128055935f7f0d829b5167 # scx_bpfland: introduce a tunable for the max nvcsw threshold
abd1fd8236f6a0f83e1a37f313e10208b42c9876 # scx_bpfland: improve producer/consumer workloads
eee53d80ca6802c1fe60b45dd7c2267b6afbf442 # scx_bpfland: prevent per-CPU kthreads starving other tasks
187c6a504eb2c944090ef0d5b1f4aca563509207 # scx_bpfland: fix typo in slice_ns definition
8b55020fa761618eed770b123111b54523c5e679 # scx_bpfland: prevent slice delta overflow
2e4368d9dcf95e99f7c6a9ba85528c1b707d2b82 # scx_bpfland: report running interactive tasks to stdout
2cbeab0031af30d498efa5e5126b61b169693749 # scx_bpfland: bump up max time slice to 20ms
33ffa9df067776dc7b8643e09dee45cc45847d9a # scx_bpfland: reduce starvation threshold to 1ms
)

_reverts=(
)

pkgver() {
  cd $gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
 cd $gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $gitname
  arch-meson . build -D openrc=disabled
  meson compile -C build
}

package() {
  cd $gitname
  meson install -C build --destdir "${pkgdir}"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
