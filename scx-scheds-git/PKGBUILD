# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Tejun Heo <tj@kernel.org>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.17.r226.ge5b2e480
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=(
  jq
  libseccomp
  libelf
  zlib
)
makedepends=(
  cargo
  clang
  git
  llvm
  llvm-libs
  python
)
optdepends=(
'scx-tools-git: scx_loader and scxtl - A DBUS Interface for Managing sched_ext Schedulers '
)
options=(!lto)
provides=("scx-scheds")
conflicts=("scx-scheds")
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')

_backports=(
2e32666f23586b49f084a03e36414c708af17f5c # scheds: Introduce scx_beerland
6200a1eb43f980d72c5b277faaad9699075f770d # EXPERIMENTAL: scx_beerland: Use lockless peek (requires kernel patch)
e6daa93a0668ef121cc473b57950f6be86894ef1 # scx_beerland: Better SMT handling
29909d89c8e1bb7b1e7aa1d4cfb852f74c91cd43 # scx_beerland: Prevent migrations for non-overloaded CPUs
f84500c8a7c97700f909985ffbf166fe46c4fe88 # scx_beerland: Re-enqueue tasks on CPU preempted by a higher sched class
75d5021b53d8c6a86764fdb336ec4e144e343b57 # scx_beerland: Attempt to wakeup idle CPUs only when needed
d4322e04768246cb9e31cc0956140457891c329e # scx_beerland: Introduce --preferred-idle-scan / -P
35d79dffdb7067ab112a8af86378ec58b5e91203 # scx_beerland: Always dispatch from any CPU when system is busy
dbd68e1da48be9ed9c2d268de94f2307173fb067 # scx_beerland: Keep using the same CPU if system is busy
260be70f4523ab4d35a2ef9cd58400da0fd3a53e # scx_beerland: Use deadline-based scheduling
ce8b7ca9a19ef8d4dd45f4bb05f0a3a393d15587 # scx_beerland: Try to keep I/O-bound task on the same CPU
53ea0abae3d9277e51b43e4e50fab359adddc25b # scx_cosmos: Improve SMT handling
9692caaec3df8cceb4e2c0384b7f9386e2fa15d3 # scx_cosmos: CPU capacity awareness
c438583c69fa6035c1a6a2a7d9467216a31f7e9a # scx_bpfland: Classify sticky tasks in function of their average runtime
e9e2b21a4eaa8e236410d491797cb001508abeaa # scx_bpfland: Rethink per-CPU tasks prioritization
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

   local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

   export RUSTUP_TOOLCHAIN=stable
   cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_mitosis \
     --exclude scx_wd40 \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

#check() {
#  cd $_gitname
#  export RUSTUP_TOOLCHAIN=stable
#  cargo test \
#     --frozen \
#     --all-features \
#     --workspace \
#     --exclude scx_rlfifo \
#     --exclude scx_mitosis \
#     --exclude scx_wd40 \
#     --exclude scxcash \
#     --exclude xtask \
#     --exclude vmlinux_docify \
#     --exclude scx_arena_selftests
#}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
