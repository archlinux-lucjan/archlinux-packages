# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: Tejun Heo <tj@kernel.org>

# Available profiles: “release”, “release-tiny”, “release-fast“
# See: https://github.com/sched-ext/scx/blob/main/Cargo.toml
_mode=release

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.19.r238.g460ed4d5
pkgrel=3
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=(
  jq
  libseccomp
  libelf
  zlib
)
makedepends=(
  cargo
  clang
  git
  llvm
  llvm-libs
  python
)
optdepends=(
'scx-tools: scx_loader and scxctl - A DBUS Interface for Managing sched_ext Schedulers '
)
options=(!lto)
provides=("scx-scheds")
conflicts=("scx-scheds")
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')

_backports=(
fecef30e834770af58f9d0232da912b8ff0c9916 # Add scx_cake scheduler to workspace
abccd1f27c5660143db740e318a1f95d715d26da # Add scx_cake scheduler to workspace
0dd5121790f2580f0d275b5993460e08a5f4e510 # scx_cake: Use local path dependencies instead of git
b6a49828d1714f7a73f3864d15e516285bb641f5 # Update Cargo.lock for scx_cake
77fbb6e6620f307ea80c4d0a3afc916fbbbc4162 # scx_cake: Run cargo fmt
cf4ef7dbfbc8e8e53958fd500f4e7ab6e01b4f9b # scx_cake: add kernel 6.16+ compatibility for scx_bpf_cpu_curr
2cd6e285fb2a0d0c451f4e7ba8faebdc842baaf6 # scx_cake: Implement Check & Skip Optimizations and SMT Fix
ef2b2455c213487fb92bc7632c6bc7842847eb5e # perf sched map optimization
f84de32ab26f0dec2820e4ead8354d297bfd084d # feat(scx_cake): implement state-based per-cpu direct dispatch
3b91b2193a2af532deb74c7a37c3b7d78fe7f9e4 # Ghost Scheduler Optimizations: Store coalescing and bit testing
dc458e8af9359ec82283fe9518c6a9a482788b22 # bug fixes, floor for runtime, healthy IPC
504b9ccf545c5456fa5c62490ac1c8a42e41ab76 # Great benchmarks - need to review jitter
a12b722f61bcff79035ee380574a7de70aa62596 # scx_cake: Implement Zero-Math Locality Arbiter
e54a5e06aa1744ab165c4f1d1a85815b26217aa9 # scx_cake: warning removed from build
6a09b5ec22c065314f65fddd267cbeb5869ba9f8 # struct alignment 4 byte for clang 19+
b66da5b2abf4eae964d8a7ff028a3d643978c121 # BPF compat: Use semantic macros for 32-bit atomics to fix Clang 19 crash
df444a1559adeb87c938d6d7190293cec82221fd # BPF: Optimize MLP by migrating volatile loads to relaxed semantic macros
ae18b63e707b65a064ac30f2a015dc27ede66f8a # lib/pmu: make start/stop methods public
ce170e81913c56031623a93d2e847dc990b68944 # scx_cosmos: use PMU library for perf event tracking
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --profile=$_mode \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/$_mode \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
