# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.12.r201.gfd0dd6b4
pkgrel=3
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")

_backports=(
db9a1a1d98cb255a89119ffe5e0b5dc84b5664d7 # scx_rustland_core: Initialize task dsq_vtime and slice
25f65d0184acc3caa8b5597bac791cd18aa139ad # scx_rustland_core: Avoid sched_setscheduler() in non-partial mode
ba6bdcc53f103697ea1680ea24d24dc22c7104e8 # scx_rustland_core: Use BPF_RB_NO_WAKEUP
f28b35e11b3e665c6511f68d2b0bfa76fef8f5c2 # scx_rustland_core: Introduce a separate DSQ for the user-space scheduler
9e968604d0cc95e0c8a0486ebdf41f775dff504b # scx_rustland_core: Use default built-in idle policy for sync wakeup
6bd8b0ad7e0e3c1e349570598c6e13b6c4fc30e5 # scx_rustland_core: Do not allow per-CPU kthreads to preempt other tasks
9418c53a23430c5b2c6fffddc1176afd46635aa1 # scx_rustland_core: Use SCX_LOCAL_DSQ / SCX_LOCAL_DSQ_ON consistently
5a06e13053f9177576538f958b22fb8fdab04a9c # scx_rustland_core: Drop cpumask generation counter
f606894a8e6f7a7b4c70aee8d020231b7e45c3cb # scx_rustland_core: Expose nr_cpus_allowed to user-space
b4a98159c7eed862bd41b493a6e666371682a16d # scx_rustland: Introduce --percpu-local
02c572e2b27b8460da4a0c0478c73b8a69a61d4e # scx_rustland: Show version info
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
