# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: Tejun Heo <tj@kernel.org>

# Available profiles: “release”, “release-tiny”, “release-fast“
# See: https://github.com/sched-ext/scx/blob/main/Cargo.toml
_mode=release

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.19.r248.g386472c8
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=(
  jq
  libseccomp
  libelf
  zlib
)
makedepends=(
  cargo
  clang
  git
  llvm
  llvm-libs
  python
)
optdepends=(
'scx-tools: scx_loader and scxctl - A DBUS Interface for Managing sched_ext Schedulers '
)
options=(!lto)
provides=("scx-scheds")
conflicts=("scx-scheds")
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')

_backports=(
73df445316ca5c6a2d4c30faef2e4abb075d4faf # Add scx_cake scheduler to workspace
f90749fe1c23d9875e936981bf8a6fd446cec24f # Add scx_cake scheduler to workspace
1c8ca0830a8203d65c45f725978fcb0c7bacacc9 # scx_cake: Use local path dependencies instead of git
69f1dd77e59e70b7dbe41faee5a5c79af58d9325 # Update Cargo.lock for scx_cake
7dde3814bd77cc7f8441937125eb0f0b6b22527a # scx_cake: Run cargo fmt
9148a7f5d78206494ae9d90f793deb56d421626a # scx_cake: add kernel 6.16+ compatibility for scx_bpf_cpu_curr
200152a1c130ad35602e48f2c0aa3e1f46c53b3b # scx_cake: Implement Check & Skip Optimizations and SMT Fix
9af5ab247b2a6aa914d6c3ea4732b65705b18854 # perf sched map optimization
67ceada647f411ec05403c8a7c5520e34825c37e # feat(scx_cake): implement state-based per-cpu direct dispatch
b0dc868970cc7c1ba9ceda0906d9b65a8ed3be09 # Ghost Scheduler Optimizations: Store coalescing and bit testing
35acb69a097ad71a009318f8cf8414a5a2ca1e32 # bug fixes, floor for runtime, healthy IPC
c8967401694dba2babee8ac16f308de6feae902e # Great benchmarks - need to review jitter
c390499d43c82f03010133cdb88fbe6f57ccb234 # scx_cake: Implement Zero-Math Locality Arbiter
112538b2892d98d22b1724586040ef3a8ae279d8 # scx_cake: warning removed from build
74c63f321bcd17871422510ff226bd77224ec494 # struct alignment 4 byte for clang 19+
7a3b602605837b5745ea614e0c9f03358ac048c8 # BPF compat: Use semantic macros for 32-bit atomics to fix Clang 19 crash
9b586373c1402acc34e7a65249005e4c132cad36 # BPF: Optimize MLP by migrating volatile loads to relaxed semantic macros
62349a05cf419aa4bdc7dc753ada00b5c230f980 # BPF: Bulletproof compat layer with inline ASM for Clang < 21
85b6e24cd6e73d071c75e092f615418497495e80 # Checkpoint: Immediate Consumption pattern - baseline 53 spills restored
f835ccca28f0535e9b7746255580b2a02c760a6f # Absolute Zero Spill: Eliminate remaining BPF spills in cake_select_cpu (53 -> 0). Verified with llvm-objdump.
96a8ad40a93ad4f9f31d32900951f320f86a024e # Optimize: Implement more aggressive warmth seeking (Stubborn Warmth)
ae18b63e707b65a064ac30f2a015dc27ede66f8a # lib/pmu: make start/stop methods public
ce170e81913c56031623a93d2e847dc990b68944 # scx_cosmos: use PMU library for perf event tracking
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --profile=$_mode \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/$_mode \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
