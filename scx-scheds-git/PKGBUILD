# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.16.r6.ge70f853a
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=(
  jq
  libseccomp
  libelf
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")

_backports=(
dc7809900d0a26da657a229226b9fac1acb231fd # scx_loader: Add chaos scheduler
61884fa288f0aedab0bed3b29df720b1a7c1bbbb # scx_loader: Add mitosis scheduler
2b4cb2b82670c42cef4b9aa77da9fc33a7fea7cb # scx_loader: Add wd40 scheduler
0062ae8f8ea3af91c49ec55042b630f07e8eb607 # scxctl: cache DBUS results to eliminate redundant calls
d283dc0a63387cb022bbfe99fb7cae126eae1c07 # scxctl: optimize scheduler list display with iterator patterns
153fdc0a97707a1f16f20b1226bcde9401c315a2 # scxctl: use &str parameters for string utility functions
bc8bef51ff8d74b736f38764297e30b84091a9b9 # scxctl: replace unwrap() calls with proper error handling
f8af31cfef91314ee0fa6d5d93eb0d5e142edea3 # scxctl: extract helper functions to reduce duplication
dafc5894ab3f37e93248934baee19a0525b95a24 # scxctl: simplify string processing utility functions
709fab2b0f099483f3bebd08a0f9c6c9d48ebe7e # scxctl: some Rust formatting changes to satisfy the CI
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

   local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

   cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --workspace \
     --locked \
     --frozen \
     --exclude vmlinux_docify \
     --exclude scx_lib_selftests
}

# Only for stable releases. Compilation time increases twice
#check() {
#  cd $_gitname
#  cargo test \
#     --workspace \
#     --locked \
#     --frozen \
#     --exclude vmlinux_docify \
#     --exclude scx_lib_selftests
#}

package() {
  cd $_gitname

  # binary files
  find target/release -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +

  # systemd service
  install -Dm644 services/systemd/scx_loader.service \
    "$pkgdir/usr/lib/systemd/system/scx_loader.service"

  # dbus services
  install -Dm644 services/systemd/org.scx.Loader.service\
    "$pkgdir/usr/share/dbus-1/system-services/org.scx.Loader.service"

  # dbus config
  install -Dm644 tools/scx_loader/org.scx.Loader.conf \
    "$pkgdir/usr/share/dbus-1/system.d/org.scx.Loader.conf"

  # scx_loader config
  install -Dm644 services/scx_loader.toml \
    "$pkgdir/usr/share/scx_loader/config.toml"
}
