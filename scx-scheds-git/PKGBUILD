# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.12.r134.g113030db
pkgrel=2
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")

_backports=(
3fdd943b7e47f1bba640ebc035f64d7a2ee908c0 # scx_rustland_core: Optimize idle CPU selection
ceffc1aec58b6063f8ff40a983bc5c22deea8aa6 # scx_rustland_core: Always dispatch kswapd directly
412e607df87a2c2f1c0e70611b6dd897241bb056 # scx_rustland_core: Expose start_ts and stop_ts
d9295fcf455f2525c8e8f5d9f82ca8fd7577f3ba # scx_rustland_core: remove redundant attributes in QueuedTask
af75627e664721b612d3a0c403e6459a921a9a4b # scx_rustland_core: Bump up major version number
7cd1f754edaee0a6913cede09dedc31d2583b37f # scx_rustland: Improve accuracy of time slice measurement
80e1362ef2aa14a5ef1247db762859e5a1ff7e73 # scx_rustland: Get rid of TaskInfoMap
cf6beca33b0eef8b4ff592de50a3467a97d7c6ff # scx_rustland: Revert congestion threshold
f1dc7cefdd6e09db926a83d092ebfa9ee6086020 # scx_rustland: Slightly penalize newly created tasks
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
