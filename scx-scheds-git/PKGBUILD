# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
gitname=scx
pkgver=0.1.2.r17.g239d5d1
pkgrel=3
pkgdesc="sched_ext schedulers"
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=("GPL2")
depends=('libbpf' 'bpf' 'libelf' 'zlib')
makedepends=('git' 'python' 'meson' 'clang' 'llvm-libs' 'cargo' 'rust')
source=("git+https://github.com/sched-ext/scx"
       'rustlite.patch'
       'nest.patch')
sha256sums=('SKIP'
            '3848652ad95253000ceed0762a6b34fef947b4b1125404ce8046a298edefe733'
            'e4f32e4511a884b9ae7231b57ab8cae6f64aaf0b3cfa4a09de6f3b42b28b823e')
options=(!lto)
replaces=("scx-git")
provides=("scx-scheds")
conflicts=("scx-scheds")

pkgver() {
  cd $gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
 cd $gitname

 local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $gitname
  arch-meson . build
  meson compile -C build
}

package() {
  cd $gitname
  meson install -C build --destdir "${pkgdir}"
}
