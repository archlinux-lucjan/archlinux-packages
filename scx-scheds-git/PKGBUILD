# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.11.r153.g4252b505
pkgrel=3
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx"
        '0001-meson-install-scx_wd40.patch'
        '0002-scx_loader-Add-chaos-scheduler.patch')
sha256sums=('SKIP'
            '6debb987cc0de96ac1ee742d22ce4eb54befa46df8a15d9c6511b146200b74a8'
            '8300e86f2b76be506102c85704b3196dbd531e2887853e6611c6da9917e0fc1a')
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")

_backports=(
ef0b90a9947f9ada12834c09ff34abaa4c8b7b9b # scx_bpfland: Rework sync wakeup
65fa32dbfcdd5a4af91436ba4ffcf53f3896dc5d # scx_bpfland: Attempt task migration on remote IRQ wakeup
0b030660ff666f546e2a12c6984b4a7961de3c0a # scx_bpfland: Introduce --no-wake-sync
6923897b498086ec9dac2869d4a2e7df5816d25d # scx_lavd: Calculate scaled and invariant CPU utilization.
a13d6e5760bebfcb9a6788ddd23f3d733fb2da04 # scx_lavd: Print scaled CPU utilization with --monitor-sched-samples.
)

_reverts=(
131828a04ada00d5a8da527398dbe607d8e63cc3 # include/scx: Bump _Q_MAX_CPUs for arena spinlocks
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
