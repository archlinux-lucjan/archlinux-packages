# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: Tejun Heo <tj@kernel.org>

# Available profiles: “release”, “release-tiny”, “release-fast“
# See: https://github.com/sched-ext/scx/blob/main/Cargo.toml
_mode=release

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.19.r226.gb6de58b5
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=(
  jq
  libseccomp
  libelf
  zlib
)
makedepends=(
  cargo
  clang
  git
  llvm
  llvm-libs
  python
)
optdepends=(
'scx-tools: scx_loader and scxctl - A DBUS Interface for Managing sched_ext Schedulers '
)
options=(!lto)
provides=("scx-scheds")
conflicts=("scx-scheds")
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')

_backports=(
9a94a4781d038ce0646d91b34718c6fd2f6e903e # Add scx_cake scheduler to workspace
b4f31975fbeffc1a986cd475c6fdd4aa200179b9 # Add scx_cake scheduler to workspace
22f868d8891fbad6d7cd97acba04a3fc30508877 # scx_cake: Use local path dependencies instead of git
46922ea2a9662ab9487062d72e293e928a19a424 # Update Cargo.lock for scx_cake
86a511e53a9c866c8b17c2f49696d51213d73731 # scx_cake: Run cargo fmt
75225b7824fe74909b688af5d1bfbd8ce877823d # scx_cake: add kernel 6.16+ compatibility for scx_bpf_cpu_curr
0fbe236d0543ebc393435af3ef1ee305d552fe00 # scx_cake: Implement Check & Skip Optimizations and SMT Fix
7a355d8f93285a434784f0e6d2b136150399862a # perf sched map optimization
355daad485634483936443a7da8c0d015cdd3085 # feat(scx_cake): implement state-based per-cpu direct dispatch
b3878efeeaacaec7ece2d45cdea867b91485f73f # Ghost Scheduler Optimizations: Store coalescing and bit testing
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --profile=$_mode \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/$_mode \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
