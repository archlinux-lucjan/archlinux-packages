# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.11.r83.gd6db7818
pkgrel=2
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx"
        '0001-meson-install-scx_wd40.patch'
        '0002-scx_loader-Add-chaos-scheduler.patch')
sha256sums=('SKIP'
            '5f30136e7f668d59be514ee2f1770b389572d8824d04f60bee8e0d644d302e4b'
            '6650af78947eb097546c8e1b6549cf4f917049996e9f0f69dc65d3ddaae68568')
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")

_backports=(
230005d99fd4b0bef95fa8d38fed9eaa11185f43 # scx_bpfland: Normalize task weight
85bebffd3dda6a9b29b742a089a638bf8871b25b # scx_bpfland: Rethink preemption
c207e8062784cd79fdac8696e0db591ae2d1dcb2 # scx_bpfland: Always disable direct dispatch with preemption disabled
0cdb5fb6c572ec4b425c0aacfdb0060635625f61 # lib: remove unused include/ directory
4e14d3eadb1c108881b85688bfd3c29a09360dca # scx_rusty: copy allocator header into local code
f78591e1bef9f6c621d95fad3c28fe21bd3b74fb # lib/sdt_alloc: rename sdt_static_* functions to scx_static_*
69461a74aa5ba206b2706160488999ea0154cd43 # lib/sdt_alloc: rename sdt_alloc_* functions to scx_alloc_*
21b89d730f4b110866d6088a9e3510689d9137d9 # lib/sdt_task: rename sdt_task_* functions to sdt_*
43afeba6fea4cd30c2bfc5fe370867e2cc615f85 # lib/sdt_alloc: remove (unused, always_inline) attributes
76da26ce1899e67c8bc49b1f99cbddbbcc381000 # lib/sdt_alloc: remove sdt_* prefix from arena-related helper
9fbda1d32c8e2ff14a3b44af382f7c5436bab792 # lib/sdt_alloc: remove sdt_* prefix from internal variables/functions
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
