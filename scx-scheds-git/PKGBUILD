# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Tejun Heo <tj@kernel.org>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.17.r240.g201892c7
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=(
  jq
  libseccomp
  libelf
  zlib
)
makedepends=(
  cargo
  clang
  git
  llvm
  llvm-libs
  python
)
optdepends=(
'scx-tools-git: scx_loader and scxtl - A DBUS Interface for Managing sched_ext Schedulers '
)
options=(!lto)
provides=("scx-scheds")
conflicts=("scx-scheds")
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')

_backports=(
caa033f2bdd0523e2598881ca8774c2b6d0f2b70 # scx_bpfland: Dynamic time slice
05dd67f7e6f2f424fa13a821aac84560d7efa071 # scheds: Introduce scx_beerland
b92e770ec330eee787b8ffe176c60ed1cd8a6453 # EXPERIMENTAL: scx_beerland: Use lockless peek (requires kernel patch)
f2509766f98de78150a0c60f9df7b330529bb249 # scx_beerland: Better SMT handling
dd30ca12cb9dd593d214f6df837793f18193f406 # scx_beerland: Prevent migrations for non-overloaded CPUs
94558041df37f9bf8fbf01d77396789e747b5657 # scx_beerland: Re-enqueue tasks on CPU preempted by a higher sched class
f19af0ee3547a6524cf95cb46fec2dd5b4c857bc # scx_beerland: Attempt to wakeup idle CPUs only when needed
736ce473481874c639eedb89ff123dc31c42a660 # scx_beerland: Introduce --preferred-idle-scan / -P
32feb8baccc980b20907ccf75d098197c25a979f # scx_beerland: Always dispatch from any CPU when system is busy
9b6e8d03bb9942cd45803aeaadf900c59894c9fd # scx_beerland: Keep using the same CPU if system is busy
51582bf9213c4f6bb301cf075fe728c0a98d21f1 # scx_beerland: Use deadline-based scheduling
6058a52f4eaa57b9b6e2126214ec0cfec0f57d14 # scx_beerland: Try to keep I/O-bound task on the same CPU
65cfb5d137e78ca7d3c69c2b9672d79c72a6d023 # scx_beerland: Sticky task classification based on average runtime
53ea0abae3d9277e51b43e4e50fab359adddc25b # scx_cosmos: Improve SMT handling
9692caaec3df8cceb4e2c0384b7f9386e2fa15d3 # scx_cosmos: CPU capacity awareness
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

   local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

   export RUSTUP_TOOLCHAIN=stable
   cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_mitosis \
     --exclude scx_wd40 \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

#check() {
#  cd $_gitname
#  export RUSTUP_TOOLCHAIN=stable
#  cargo test \
#     --frozen \
#     --all-features \
#     --workspace \
#     --exclude scx_rlfifo \
#     --exclude scx_mitosis \
#     --exclude scx_wd40 \
#     --exclude scxcash \
#     --exclude xtask \
#     --exclude vmlinux_docify \
#     --exclude scx_arena_selftests
#}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
