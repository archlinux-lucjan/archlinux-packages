# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Tejun Heo <tj@kernel.org>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.16.r208.g833e8fa9
pkgrel=5
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=(
  jq
  libseccomp
  libelf
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')

_backports=(
1922f501ef7c018b6e8ab8580534d7b0d9669e2a # scx_cosmos: Improve SMT handling
5d66ad08a753cc46b84161c1680a16fd794be03c # scx_cosmos: Prioritize faster CPUs
5c739b96d96a48fe9bb0b22ea6134a33e9945473 # scx_cosmos: Drop flat/preferred idle scan
ba3c2488bc49e668d34ed4eafd116ee883ce66a0 # scheds: Introduce scx_beerland
c5ed21d5c09328e2c757f48832268a2080201a74 # scx_loader: Add beerland scheduler
b896cc633ee391ea40544be1358d9bcda826e7f0 # EXPERIMENTAL: scx_beerland: Use lockless peek (requires kernel patch)
dd3b1d03a90e2da5ef6a042b4f36b51b16790106 # scx_beerland: Better SMT handling
29436147e4197173cee6252aeb3c52e362f015c7 # scx_beerland: Prevent migrations for non-overloaded CPUs
f3e0fdd7a20b8bc365f2fab022e3e5ac74f59c79 # scx_beerland: Re-enqueue tasks on CPU preempted by a higher sched class
b14f8a5bc51caebb2259f59f4f56e8c99f34c94e # scx_loader: Add chaos scheduler
ac06004412d1d2fbab08e673b24812becd2a72f4 # scx_loader: Error when non-auto modes do not have args
5ad4f4fba815913fb3f68b6a07d62ca658c5e3a5 # scxctl: Implement error handling
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

   cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --locked \
     --frozen \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_mitosis \
     --exclude scx_wd40 \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude arenalib_selftests
}

#check() {
#  cd $_gitname
#  cargo test \
#     --locked \
#     --frozen \
#     --workspace \
#     --exclude scx_rlfifo \
#     --exclude scx_mitosis \
#     --exclude scx_wd40 \
#     --exclude xtask \
#     --exclude vmlinux_docify \
#     --exclude arenalib_selftests
#}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +

  # systemd service
  install -Dm644 services/systemd/scx_loader.service \
    -t "$pkgdir/usr/lib/systemd/system/"

  # D-Bus service
  install -Dm644 services/systemd/org.scx.Loader.service \
    -t "$pkgdir/usr/share/dbus-1/system-services/"

  # D-Bus configuration
  install -Dm644 tools/scx_loader/org.scx.Loader.conf \
    -t "$pkgdir/usr/share/dbus-1/system.d/"

  # scx_loader sample configuration (template)
  install -Dm644 services/scx_loader.toml \
    "$pkgdir/usr/share/scx_loader/config.toml"
}
