# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.4.r106.g652c923
pkgrel=6
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx"
"0001-scx_fair-append-cast_mask.patch")
sha256sums=('SKIP'
            'a96e1aa26620522723d9148949cc3ebaeeef5750d2a814a37b9febebfc808148')
options=(!lto)
provides=("scx-scheds")
conflicts=("scx-scheds")

_backports=(
e3609ac932a4e6eae91647c0a7d17d9f5fcee275 # scheds: introduce scx_fair
5912314dc03c9d1e95a2a73f698fd997c1134772 # scx_fair: introduce lowlatency mode
1b44eca9d54d1fe02faa81722d5f105f0bd50b9a # scx_bpfland: prevent stall with sched_setaffinity()
34bc6a2b649a1a3b8c1cccba63bf9e417f983c97 # Revert "scx_rustland_core: dispatch all kthreads directly from BPF"
0a57b938468131b94a69bc7d76ef76e4be193a6b # scx_rustland_core: prevent mm stall
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
