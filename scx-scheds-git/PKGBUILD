# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.13.r169.g06f1c01a
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx"
        '0001-meson-install-scx_mitosis-and-scx_wd40.patch'
        '0002-scx_loader-Add-missing-schedulers.patch')
sha256sums=('SKIP'
            'd8228564905b8de1649b4643a7366388c2514a0e64d7dd25fbfc75960b3beca5'
            'cf5f810bfa7d328c04a4df874b37a4cc3047f03dcd7bf287826844fa85cfdc6c')
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")

_backports=(
550f7582a3715a57762b504fec0ea0b8b88e581c # scx_flash: Update nvcsw statistics only on task sleep
be1cc1f867bcd8f8acd1ef3a4624cbce65bca67e # scx_flash: Reduce impact of nvcsw on sleeping time budget
65adc77a20858b7e2b62c70d13474690980b915f # scx_flash: Re-enable voluntary context switch rate prioritization
250ff7fc37ce594acf25a90613f840e0fff22bc5 # scx_flash: Always prefer reusing the same CPU
78bef1ba8e55b91247ed6c147bf14f3d874de036 # scx_flash: Save some cpumask ops when primary domain includes all CPUs
529b5dcb0b869dba9e5d8890ccb4b1c561bcb2e0 # scx_flash: Always rebalance per-CPU DSQs
3446bd3bc7b740fba59735dbf506e0281278a53a # scx_flash: Increase maximum accounted runtime by 10x
f74b8268065c383dfb74464bce32108e4a95155b # WIP: scx_flash: Fix Lucjan audio cracking without regressing everything else
f571c5d36eb4426dc72aa5172df418ea546207f0 # scx_p2dq: Refactor dispatch path
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
