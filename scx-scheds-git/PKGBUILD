# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.12.r134.g113030db
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")

_backports=(
c77cf046cf44753846b94c719bbbc85d640d9f5f # scx_rustland_core: Optimize idle CPU selection
8c4e179ab4f35797a1053a14da7b77e204f011a4 # scx_rustland_core: Always dispatch kswapd directly
0894cf40e86e40dcfdedb0fbbf1c2e18d9a0b544 # scx_rustland_core: Expose start_ts and stop_ts
95d0f0c2a50340928e3acccb9dfc58a9aeb5a8db # scx_rustland_core: remove redundant attributes in QueuedTask
39a6f3cd1724e1167fa57ab5768e7460f5d26645 # scx_rustland_core: Bump up major version number
25fe534cf81e4aae4c7a72fab9255de45ce436c6 # scx_rustland: Improve accuracy of time slice measurement
68905371e6e9d8d461ce6df6c0594c91061ca5ef # scx_rustland: Get rid of TaskInfoMap
fb5770986cb731ea59a70b1ed7c0dafb4c751564 # scx_rustland: Revert congestion threshold
24e4a0bde0aa326cf15cad79beeb522596696cff # scx_rustland: Slightly penalize newly created tasks
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
