# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.3.r213.g239b519
pkgrel=7
pkgdesc='sched_ext schedulers and tools'
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds")
conflicts=("scx-scheds")

_backports=(
1de44d918c1f470a176dfd03104dc57518d8ca73 # scx_rusty: Support domain/core cookie policy
ca1c86eb9cf87baadcbd0602db2ab5a8fbdfc89c # scx_lavd: improve pick_idle_cpu() for pinned tasks
6b717a3f3d913102a8d506f65ec21cc7ef75e0e4 # scx_lavd: add --help-stats option
e5d27d05537cd90cc50f357d5d01b1b86ee5fbe8 # scx_lavd: print basic system status when --monior is given
f490a55d542268f56fd8a203567d694928df0623 # scx_lavd: accmulate more system-wide statistics
d09f01da4bc5140e6acb2e5de0d411c96a4a5c1f # scx_lavd: print more info with --monitor
e39d69df2faa815016505c481402394f69e44900 # scx_lavd: drop time slice boost for big cores
76ad8804758de647e3d32ece72ed6defaf6aab25 # scx_layered: Fix stats formatting
844c00fd26f7d1c4c8f462564735616071f9dbd2 # scx_bpfland: enable "auto" mode by default
560b4ca12c63515f647d3c56d68352f2812893d7 # scx_bpfland: optimize producer/consumer workloads
cde5a39ae056910fb3fbe4a8647e41287fcad160 # scx-scheds: update bpflands suggested flags
)

_reverts=(
)

pkgver() {
  cd $_gitname
  # git describe
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  # alternative
  # _ver="$(cat meson.build | grep -m1 version: | cut -d "'" -f2 | tr - .)"
  # echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
