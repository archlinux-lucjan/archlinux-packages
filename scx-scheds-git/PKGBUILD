# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Tejun Heo <tj@kernel.org>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.16.r234.g8d597bf3
pkgrel=2
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=(
  jq
  libseccomp
  libelf
  zlib
)
makedepends=(
  cargo
  clang
  git
  llvm
  llvm-libs
  python
)
options=(!lto)
provides=("scx-scheds" "scxctl")
conflicts=("scx-scheds" "scxctl")
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')

_backports=(
cb48d4a384e2209706dd3095b2dfd4bfb934b2b9 # scx_cosmos: Improve SMT handling
4aa7273098fb22e37075e659281f33220212f8e3 # scx_cosmos: Prioritize faster CPUs
f44cfb886c6828fba3bc820171ce15ef796d641d # scx_cosmos: Drop flat/preferred idle scan
ca53c1687422ad9df6928c6f957cca65d3147204 # scx_bpfland: Rework idle CPU selection and scheduling policy
68cd5142c158e122624dc9e63fff83ca09d4a0e4 # scx_bpfland: Introdue --wake-affine
9de3b5c9bf7410bb60d300961fd0e139594d3fff # scheds: Introduce scx_beerland
399856599abafde0afb8712643c68cb131656f70 # scx_loader: Add beerland scheduler
d988747c899370a876424706958c1804ebb4be75 # EXPERIMENTAL: scx_beerland: Use lockless peek (requires kernel patch)
10cca9e60ffe40404607b1ecde2c129c72a23739 # scx_beerland: Better SMT handling
f55b476eda2c17b6153a35708fd5e46d61e796ee # scx_beerland: Prevent migrations for non-overloaded CPUs
bca2a14178cab3d858b5eafb7d3e42e3a469063e # scx_beerland: Re-enqueue tasks on CPU preempted by a higher sched class
6fceb01e54bf24dc28bb034e49fdc5656dd2276d # scx_loader: Add chaos scheduler
58212ecf66431d5c440369991e3c99e0e9a38356 # scx_loader: Error when non-auto modes do not have args
3ffec925933a98cbeb4cbc0a844b49de48868f6a # scxctl: Implement error handling
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

   export RUSTUP_TOOLCHAIN=stable
   cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_mitosis \
     --exclude scx_wd40 \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude arenalib_selftests
}

#check() {
#  cd $_gitname
#  export RUSTUP_TOOLCHAIN=stable
#  cargo test \
#     --frozen \
#     --all-features \
#     --workspace \
#     --exclude scx_rlfifo \
#     --exclude scx_mitosis \
#     --exclude scx_wd40 \
#     --exclude scxcash \
#     --exclude xtask \
#     --exclude vmlinux_docify \
#     --exclude arenalib_selftests
#}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +

  # systemd service
  install -Dm644 services/systemd/scx_loader.service \
    -t "$pkgdir/usr/lib/systemd/system/"

  # D-Bus service
  install -Dm644 services/systemd/org.scx.Loader.service \
    -t "$pkgdir/usr/share/dbus-1/system-services/"

  # D-Bus configuration
  install -Dm644 tools/scx_loader/org.scx.Loader.conf \
    -t "$pkgdir/usr/share/dbus-1/system.d/"

  # scx_loader sample configuration (template)
  install -Dm644 services/scx_loader.toml \
    "$pkgdir/usr/share/scx_loader/config.toml"
}
