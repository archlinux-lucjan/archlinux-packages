# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Daniel Bermond <dbermond@archlinux.org>

pkgname=intel-media-driver-stable-git
pkgver=2020.4.5.r0.g74e2f1113
pkgrel=1
pkgdesc='Intel Media Driver for VAAPI â€” Broadwell+ iGPUs (git version)'
arch=('x86_64')
url='https://github.com/intel/media-driver/'
license=('MIT' 'BSD')
depends=(gcc-libs intel-gmmlib libva libpciaccess)
makedepends=('git' 'cmake')
provides=('intel-media-driver')
conflicts=('intel-media-driver')
source=('git+https://github.com/intel/media-driver.git#branch=intel-media-20.4')
sha256sums=('SKIP')

pkgver() {
    git -C media-driver describe --long --tags | sed 's/^intel-media-//;s/^[0-9]\{2\}/20&/;s/\([^-]*-g\)/r\1/;s/-/./g;s/^v//'
}

build() {
    cmake -B build -S media-driver \
         -DCMAKE_INSTALL_PREFIX=/usr \
         -DCMAKE_INSTALL_LIBDIR=lib \
         -DINSTALL_DRIVER_SYSCONF=OFF \
         -Wno-dev
}

package() {
    make -C build DESTDIR="$pkgdir" install
    install -D -m644 media-driver/LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
