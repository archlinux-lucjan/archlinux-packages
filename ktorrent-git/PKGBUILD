# $Id$
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Georg Grabler <ggrabler@gmail.com>

pkgname=ktorrent-git
_pkgname=ktorrent
pkgver=5.2.0.1.g9747a8f5
pkgrel=1
pkgdesc="A powerful BitTorrent client for KDE"
arch=('x86_64')
url='https://www.kde.org/applications/internet/ktorrent/'
license=('GPL2')
depends=('libktorrent' 'kcmutils' 'knotifyconfig' 'kross')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost' 'plasma-workspace' 'kdnssd' 'taglib' 'syndication' 'kplotting' 'kdewebkit' 'geoip' 'git')
optdepends=('plasma-workspace: shutdown plugin' 'kdnssd: zeroconf plugin' 'taglib: mediaplayer plugin' 'geoip: info widget plugin'
            'syndication: syndication plugin' 'kdewebkit: search plugin, syndication plugin' 'kplotting: statistics plugin')
conflicts=('ktorrent')
provides=('ktorrent')
source=('git+https://anongit.kde.org/ktorrent')
sha1sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe  --long | sed 's/^v//;s/-/./g'
}

build() {
  cmake -B build -S $_pkgname \
	 -DWITH_SYSTEM_GEOIP=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
