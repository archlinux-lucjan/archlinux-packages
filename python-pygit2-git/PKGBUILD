# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=python-pygit2-git
pkgver=1.10.1.r1.g0ec4c82
pkgrel=1
pkgdesc='Python bindings for libgit2'
arch=('x86_64')
url="https://github.com/libgit2/pygit2"
license=('GPL2')
depends=('libgit2' 'libgit2.so' 'python-cffi' 'python-six' 'python-cached-property')
makedepends=('cython' 'python-build' 'python-cffi' 'python-installer' 'python-setuptools' 'git' 'python-six' 'python-wheel')
checkdepends=('python-pytest-runner')
provides=('python-libgit2' "python-pygit2=$pkgver")
conflicts=('python-libgit2' 'python-pygit2')
source=("git+https://github.com/libgit2/pygit2")
sha256sums=('SKIP')

pkgver() {

  cd "pygit2"
  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {

  cd "pygit2"
  # Disable tests that do stuff online
  sed -i -e '/has_network/s/True/False/' test/utils.py
}

build() {
  
  cd "pygit2"
  python -m build -wn
}

check() {

  cd "pygit2"

  # Skipped tests fail because upstream test suite hasn't 100% adapted to libgit2 1.4.x
  local _pyver=cpython-$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-$_pyver" pytest
}

package() {

  cd "pygit2"
  LANG=en_US.UTF8 python -m installer -d "${pkgdir}" dist/*.whl
}
