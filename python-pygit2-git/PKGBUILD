# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=python-pygit2-git
pkgver=1.0.3.r30.ge002614
pkgrel=1
pkgdesc='Python bindings for libgit2'
arch=('x86_64')
url="https://github.com/libgit2/pygit2"
license=('GPL2')
depends=("libgit2>=1:0.99.0")
provides=("python-pygit2=$pkgver")
conflicts=('python-pygit2')
makedepends=('python-cffi' 'python-setuptools' 'git')
source=("git://github.com/libgit2/pygit2")
sha256sums=('SKIP')

pkgver() {

  cd "pygit2"
  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  
  cd "pygit2"
  LANG=en_US.UTF8 python setup.py build
}

package() {
  depends+=('python-cffi')
  provides=('python-libgit2' "python-pygit2=$pkgver")
  conflicts=('python-libgit2' 'python-pygit2')

  cd "pygit2"
  LANG=en_US.UTF8 python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
