# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=python-pygit2-git
pkgver=1.0.0.r1.g19874e2
pkgrel=2
pkgdesc='Python bindings for libgit2'
arch=('x86_64')
url="https://github.com/libgit2/pygit2"
license=('GPL2')
depends=('libgit2>=1:0.28.0')
makedepends=('python-cffi' 'python-setuptools' 'git')
source=("git://github.com/libgit2/pygit2")
sha256sums=('SKIP')

pkgver() {

  cd "pygit2"
  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  depends+=('python-cffi' 'python-six')
  provides=('python-libgit2' "python-pygit2=$pkgver")
  conflicts=('python-libgit2' 'python-pygit2')

  cd "pygit2"
  LANG=en_US.UTF8 python setup.py install --root="$pkgdir" --optimize=1
}
