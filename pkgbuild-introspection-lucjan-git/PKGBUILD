# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=pkgbuild-introspection-lucjan-git
_pkgname=pkgbuild-introspection
pkgver=9.r1.gabaf713
pkgrel=1
pkgdesc='Tools for generating .SRCINFO files and PKGBUILD data extraction (mkaurball)'
url="https://github.com/falconindy/$_pkgname"
license=('MIT')
arch=('any')
depends=('bash' 'pacman')
makedepends=('git')
provides=('pkgbuild-introspection' 'pkgbuild-introspection-git')
conflicts=('pkgbuild-reflection-git' 'pkgbuild-introspection')
source=("git://github.com/sirlucjan/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd "$_pkgname"

  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$_pkgname"

  make
}

package() {
  cd "$_pkgname"

  make install PREFIX=/usr DESTDIR="$pkgdir"
}
