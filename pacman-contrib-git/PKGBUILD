# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Daniel M. Capella <polyzen@archlinux.info>
# Contributor: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=pacman-contrib-git
_gitname=pacman-contrib
pkgver=1.2.0.1.g21449e3
pkgrel=1
pkgdesc='pacman contributions (Git version)'
url=https://git.archlinux.org/$_gitname.git
arch=('x86_64')
license=('GPL')
depends=('pacman' 'fakeroot' 'perl')
makedepends=('asciidoc' 'git')
optdepends=('mlocate: pacdiff'
            'findutils: pacdiff')
provides=('pacman-contrib')
conflicts=('pacman-contrib')
source=("git+https://git.archlinux.org/pacman-contrib.git")
sha512sums=('SKIP')

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

build() {
  cd $_gitname
  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var
  make
}

check() {
  make -C $_gitname check
}

package() {
  cd $_gitname
  make DESTDIR="$pkgdir" install
}
