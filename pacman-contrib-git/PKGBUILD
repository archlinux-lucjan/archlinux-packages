# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.info>
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=pacman-contrib-git
_gitname=pacman-contrib
pkgver=0.0.1.15.gb61b9b2
pkgrel=1
pkgdesc='pacman contributions (Git version)'
url=https://git.archlinux.org/$_gitname.git
arch=('x86_64')
license=('GPL')
depends=('bash' 'pacman-git' 'perl') # Change to 'pacman' after release 5.1
makedepends=('asciidoc' 'git')
provides=("pacman-contrib")
replaces=("pacman-contrib")
source=("git+https://git.archlinux.org/pacman-contrib.git")
sha512sums=('SKIP')

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

build() {
  cd $_gitname
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --enable-git-version \
    --with-vim-dir=/usr/share/vim/vimfiles
  make V=1
}

check() {
  make -C $_gitname check
}

package() {
  cd $_gitname
  make DESTDIR="$pkgdir" install
}
