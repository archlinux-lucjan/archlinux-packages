# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgbase=mesa-dev-git
pkgname=('vulkan-mesa-layers-dev-git' 'opencl-mesa-dev-git' 'mesa-vulkan-intel-dev-git' 'mesa-vulkan-radeon-dev-git' 'mesa-vulkan-mesa-dev-git' 'libva-mesa-driver-dev-git' 'mesa-vdpau-dev-git' 'mesa-dev-git')
pkgdesc="an open-source implementation of the OpenGL specification, git version"
pkgver=20.2.0.devel.r124190.g045267d1e62
pkgrel=5
groups=('mesagit')
arch=('x86_64')
makedepends=('python-mako' 'libxml2' 'libx11' 'xorgproto' 'libdrm' 'libxshmfence' 'libxxf86vm'
             'libxdamage' 'libvdpau' 'libva' 'wayland' 'wayland-protocols' 'zstd'
             'elfutils' 'llvm-git' 'libomxil-bellagio' 'libclc' 'clang-git' 'libglvnd' 'libunwind' 'lm_sensors'
             'libxrandr' 'valgrind' 'glslang' 'meson' 'git')
url="http://mesa3d.sourceforge.net"
license=('custom')
source=('mesa::git+http://anongit.freedesktop.org/git/mesa/mesa.git'
        "LICENSE")
options=('!libtool')
md5sums=('SKIP'
         '5c65a0fe315dd347e09b1f2826a1df5a')

pkgver() {
    cd mesa
    read -r _ver <VERSION
    echo ${_ver/-/.}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)
}

prepare() {
  cd ${srcdir}/mesa

}
  
build() {

  arch-meson mesa build \
    -D b_lto=true \
    -D b_ndebug=true \
    -D platforms=x11,wayland \
    -D dri-drivers=i915,i965,r100,r200,nouveau \
    -D gallium-drivers=r300,r600,radeonsi,nouveau,virgl,svga,swrast,swr,iris,zink \
    -D vulkan-drivers=amd,intel,swrast \
    -D vulkan-overlay-layer=true \
    -D vulkan-device-select-layer=true \
    -D swr-arches=avx,avx2 \
    -D dri3=enabled \
    -D egl=enabled \
    -D gallium-extra-hud=true \
    -D gallium-nine=true \
    -D gallium-omx=bellagio \
    -D gallium-opencl=icd \
    -D gallium-va=enabled \
    -D gallium-vdpau=enabled \
    -D gallium-xa=enabled \
    -D gallium-xvmc=disabled \
    -D gbm=enabled \
    -D gles1=disabled \
    -D gles2=enabled \
    -D glvnd=true \
    -D glx=dri \
    -D libunwind=enabled \
    -D llvm=enabled \
    -D lmsensors=enabled \
    -D osmesa=gallium \
    -D shared-glapi=enabled \
    -D valgrind=enabled

  # Print config
  meson configure build

  ninja -C build
  meson compile -C build

  # fake installation to be seperated into packages
  # outside of fakeroot but mesa doesn't need to chown/mod
  DESTDIR="${srcdir}/fakeinstall" ninja -C build install
}

_install() {
  local src f dir
  for src; do
    f="${src#fakeinstall/}"
    dir="${pkgdir}/${f%/*}"
    install -m755 -d "${dir}"
    mv -v "${src}" "${dir}/"
  done
}

package_vulkan-mesa-layers-dev-git() {
  pkgdesc="Vulkan overlay layer to display information about the application"
  provides=('vulkan-mesa-layers')
  conflicts=('vulkan-mesa-layers')
  
  _install fakeinstall/usr/share/vulkan/explicit_layer.d
  _install fakeinstall/usr/lib/libVkLayer_MESA_overlay.so
  _install fakeinstall/usr/bin/mesa-overlay-control.py

  _install fakeinstall/usr/share/vulkan/implicit_layer.d
  _install fakeinstall/usr/lib/libVkLayer_MESA_device_select.so

  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}

package_opencl-mesa-dev-git() {
  pkgdesc="OpenCL support for AMD/ATI Radeon mesa drivers. Git version"
  depends=('expat' 'libdrm' 'libelf' 'libclc' 'clang-git')
  optdepends=('opencl-headers: headers necessary for OpenCL development')
  provides=('opencl-mesa' 'opencl-driver')
  conflicts=('opencl-mesa')
  
  _install fakeinstall/etc/OpenCL
  _install fakeinstall/usr/lib/lib*OpenCL*
  _install fakeinstall/usr/lib/gallium-pipe

  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}

package_mesa-vulkan-intel-dev-git() {
  pkgdesc="Intel's Vulkan mesa driver. Git version"
  depends=('wayland' 'libx11' 'libxshmfence' 'libdrm')
  optdepends=('vulkan-mesa-layer-dev-git: a vulkan layer to display information using an overlay')
  provides=('vulkan-intel' 'vulkan-driver')
  conflicts=('vulkan-intel')
  
  _install fakeinstall/usr/share/vulkan/icd.d/intel_icd*.json
  _install fakeinstall/usr/lib/libvulkan_intel.so
  _install fakeinstall/usr/include/vulkan/vulkan_intel.h

  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}

package_mesa-vulkan-radeon-dev-git() {
  pkgdesc="Radeon's Vulkan mesa driver. Git version"
  depends=('wayland' 'libx11' 'libxshmfence' 'libelf' 'libdrm' 'llvm-libs-git')
  optdepends=('vulkan-mesa-layer-dev-git: a vulkan layer to display information using an overlay')
  provides=('vulkan-radeon' 'vulkan-driver')
  conflicts=('vulkan-radeon')
  
  _install fakeinstall/usr/share/vulkan/icd.d/radeon_icd*.json
  _install fakeinstall/usr/lib/libvulkan_radeon.so

  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}

package_mesa-vulkan-mesa-dev-git() {
  pkgdesc="Vulkan swrast driver. Git version"
  depends=('wayland' 'libx11' 'libxshmfence' 'libdrm' 'zstd' 'llvm-libs-git')
  optdepends=('vulkan-mesa-layer-dev-git: additional vulkan layers')
  provides=('vulkan-driver' 'vulkan-mesa')
  conflicts=('vulkan-mesa')

  _install fakeinstall/usr/share/vulkan/icd.d/lvp_icd*.json
  _install fakeinstall/usr/lib/libvulkan_lvp.so

  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}

package_libva-mesa-driver-dev-git() {
  pkgdesc="VA-API implementation for gallium. Git version"
  depends=('libdrm' 'libx11' 'llvm-libs-git' 'expat' 'libelf' 'libxshmfence')
  provides=('libva-mesa-driver')
  conflicts=('libva-mesa-driver')

  _install fakeinstall/usr/lib/dri/*_drv_video.so
   
  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}

package_mesa-vdpau-dev-git() {
  pkgdesc="Mesa VDPAU drivers. Git version"
  depends=('libdrm' 'libx11' 'llvm-libs-git' 'expat' 'libelf' 'libxshmfence')
  provides=('mesa-vdpau')
  conflicts=('mesa-vdpau')

  _install fakeinstall/usr/lib/vdpau
   
  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}

package_mesa-dev-git() {
  depends=('libdrm' 'wayland' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'libelf' 
           'libomxil-bellagio' 'libunwind' 'llvm-libs-git' 'lm_sensors' 'libglvnd')
  optdepends=('opengl-man-pages: for the OpenGL API man pages'
              'mesa-vdpau: for accelerated video playback'
              'libva-mesa-driver: for accelerated video playback')
  provides=('mesa' 'mesa-dri' 'mesa-libgl'
            'mesa-dri-git' 'mesa-libgl-git')
  conflicts=('mesa'  'mesa-dri' 'mesa-libgl'
             'mesa-dri-git' 'mesa-libgl-git')
  replaces=('mesa-libgl-dev-git')

  _install fakeinstall/usr/share/drirc.d/00-mesa-defaults.conf
  _install fakeinstall/usr/share/glvnd/egl_vendor.d/50_mesa.json

  # ati-dri, nouveau-dri, intel-dri, svga-dri, swrast, swr
  _install fakeinstall/usr/lib/dri/*_dri.so
   
  _install fakeinstall/usr/lib/bellagio
  _install fakeinstall/usr/lib/d3d
  _install fakeinstall/usr/lib/lib{gbm,glapi}.so*
  _install fakeinstall/usr/lib/libOSMesa.so*
  _install fakeinstall/usr/lib/libxatracker.so*
  _install fakeinstall/usr/lib/libswrAVX*.so*

  # in vulkan-headers
  rm -rv fakeinstall/usr/include/vulkan

  _install fakeinstall/usr/include
  _install fakeinstall/usr/lib/pkgconfig

  # libglvnd support
  _install fakeinstall/usr/lib/libGLX_mesa.so*
  _install fakeinstall/usr/lib/libEGL_mesa.so*

  # indirect rendering
  ln -s /usr/lib/libGLX_mesa.so.0 "${pkgdir}/usr/lib/libGLX_indirect.so.0"

  # make sure there are no files left to install
  find fakeinstall -depth -print0 | xargs -0 rmdir

  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}
