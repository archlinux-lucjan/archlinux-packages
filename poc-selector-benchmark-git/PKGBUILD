# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>

# Available profiles: “release”, “release-tiny”, “release-fast“
# See: https://github.com/firelzrd/poc-selector/blob/main/benchmark/rust/Cargo.toml
_mode=release

pkgname=poc-selector-benchmark-git
_gitname='poc-selector/benchmark/rust'
pkgver=r17.g29ef135
pkgrel=1
pkgdesc='Piece-Of-Cake (POC) CPU Selector benchmark'
url='https://github.com/firelzrd/poc-selector'
arch=('x86_64')
license=('GPL-2.0-only')
makedepends=(
  cargo
  git
)
options=(!lto)
source=("git+https://github.com/firelzrd/poc-selector")
sha256sums=('SKIP')

_backports=(
)

_reverts=(
)

pkgver() {
  cd $_gitname
  printf "r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --profile=$_mode \
     --frozen \
     --all-features \
     --workspace
}

package() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  cargo install --no-track --frozen --all-features --root "$pkgdir/usr/" --path .
}
