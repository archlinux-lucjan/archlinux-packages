# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>

pkgbase=dbus-git
pkgname=('dbus-git' 'dbus-docs-git')
pkgver=1.13.18.r0.gc91ca6ed
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(x86_64)
license=('GPL' 'custom')
depends=('systemd-libs' 'expat' 'audit')
makedepends=('systemd' 'xmlto' 'docbook-xsl' 'python' 'yelp-tools' 'doxygen' 'git' 'autoconf-archive')
source=(git+https://gitlab.freedesktop.org/dbus/dbus.git
        dbus-reload.hook no-fatal-warnings.diff)
md5sums=('SKIP'
         'c3b3957a7d236ccebf84862aa6226d58'
         'c28b6b9baec725bc816ae6976c9ae722')

pkgver() {
  cd dbus
  git describe --long | sed 's/^dbus-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd dbus

  # Allow us to enable checks without them being fatal
  patch -Np1 -i ../no-fatal-warnings.diff

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd dbus
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/dbus-1.0 \
    runstatedir=/run \
    --with-console-auth-dir=/run/console/ \
    --with-dbus-user=dbus \
    --with-system-pid-file=/run/dbus/pid \
    --with-system-socket=/run/dbus/system_bus_socket \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --enable-inotify \
    --enable-libaudit \
    --enable-systemd \
    --enable-user-session \
    --enable-xml-docs \
    --enable-doxygen-docs \
    --enable-ducktype-docs \
    --disable-static \
    --without-x
  make
}

check() {
  cd dbus
  make check
}

package_dbus-git() {
  depends+=('libsystemd.so' 'libaudit.so')
  provides=('libdbus' 'dbus' 'libdbus-1.so')
  conflicts=('libdbus' 'dbus')

  DESTDIR="$pkgdir" make -C dbus install

  rm -r "$pkgdir"/{etc,var}

  # We have a pre-assigned uid (81)
  echo 'u dbus 81 "System Message Bus"' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/dbus.conf"

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 dbus/COPYING

  # Split docs
  mv "$pkgdir/usr/share/doc" "$srcdir"
}

package_dbus-docs-git() {
  pkgdesc+=" (documentation)"
  provides=('dbus-docs')
  conflicts=('dbus-docs')

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 dbus/COPYING
  mv doc "$pkgdir/usr/share"
}
