# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=pacutils-git
_pkgname=pacutils
pkgdesc='alpm front-end tools'
pkgdesc='Helper tools for libalpm'
pkgver=0.11.0.0.ge891b53
pkgrel=1
arch=('x86_64')
depends=('pacman')
makedepends=('git')
conflicts=('pacutils')
provides=("pacutils=$pkgver")
license=('MIT')
source=("git+https://github.com/andrewgregory/pacutils.git")
sha1sums=('SKIP')

pkgver() {
  cd pacutils
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

build() {
  cd pacutils
  make CFLAGS="$CFLAGS $LDFLAGS" SYSCONFDIR=/etc LOCALSTATEDIR=/var
}

check() {
  cd pacutils
  make check
}

package() {
  cd pacutils
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}
