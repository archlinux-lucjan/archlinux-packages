# $Id$
# Original Core Repo
# ==================
# Maintainer: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->glibc->binutils->gcc

# build from head of release branch as bug fix releases are rare

# Modifications to Use Git Master Source
# ======================================
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: James Harvey <jamespharvey20@gmail.com>
#    * This PKGFILE as closely as possible matches core's binutils

pkgname=binutils-git
_pkgname=binutils-gdb
pkgver=2.40.50.r113041.ea2f6fad7e1
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files (git master developmental version)"
arch=('x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL3' 'GPL' 'FDL1.3' 'custom:FSFAP')
groups=('base-devel')
depends=('glibc' 'jansson' 'libelf' 'zlib' 'zstd')
makedepends=('git')
checkdepends=('dejagnu' 'debuginfod' 'bc')
optdepends=('debuginfod: for debuginfod server/client functionality')
provides=('binutils')
conflicts=('binutils-multilib' 'binutils')
backup=('etc/gprofng.rc')
options=('staticlibs' '!distcc' '!ccache')
source=(git://sourceware.org/git/binutils-gdb.git
        gold-warn-unsupported.patch)
md5sums=('SKIP'
         '0283237e17804af1c55703d7e35557f1')

pkgver() {
   cd binutils-gdb
   echo $(cat binutils/configure | grep "PACKAGE_VERSION=" | sed "s|^PACKAGE_VERSION='||" | sed "s|'$||" | sed "s|\.51$||").r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd binutils-gdb

  # Turn off development mode (-Werror, gas run-time checks, date in sonames)
  sed -i '/^development=/s/true/false/' bfd/development.sh

  # Creds @Fedora
  # Change the gold configuration script to only warn about
  # unsupported targets.  This allows the binutils to be built with
  # BPF support enabled.
  patch -Np1 -i "${srcdir}"/gold-warn-unsupported.patch

  mkdir ${srcdir}/binutils-build
}

build() {
  cd binutils-build

  $srcdir/binutils-gdb/configure \
    --prefix=/usr \
    --sysconfdir="${pkgdir}"/etc \
    --with-lib-path=/usr/lib:/usr/local/lib \
    --with-bugurl=https://bugs.archlinux.org/ \
    --enable-cet \
    --enable-colored-disassembly \
    --enable-deterministic-archives \
    --enable-gold \
    --enable-install-libiberty \
    --enable-jansson \
    --enable-ld=default \
    --enable-new-dtags \
    --enable-pgo-build=lto \
    --enable-plugins \
    --enable-relro \
    --enable-shared \
    --enable-targets=x86_64-pep,bpf-unknown-none \
    --enable-threads \
    --disable-gdb \
    --disable-gdbserver \
    --disable-libdecnumber \
    --disable-readline \
    --disable-sim \
    --disable-werror \
    --with-debuginfod \
    --with-pic \
    --with-system-zlib

  make -O tooldir=/usr

  # Extract the FSF All Permissive License
  # <https://www.gnu.org/prep/maintain/html_node/License-Notices-for-Other-Files.html>
  # used for some linker scripts.
  tail -n 5 "${srcdir}"/binutils-gdb/ld/scripttempl/README > FSFAP
}

#check() {
#  cd binutils-build
#
#  # Use minimal flags for testsuite
#  # ld testsuite uses CFLAGS_FOR_TARGET and requires -g
#  # gold testsuite requires CXXFLAGS/CFLAGS with default PIE/PIC disabled
#  make -O CFLAGS_FOR_TARGET="-O2 -g" \
#          CXXFLAGS="-O2 -no-pie -fno-PIC" \
#          CFLAGS="-O2 -no-pie" \
#          LDFLAGS="" \
#          check || true
#}

package() {
  cd binutils-build
  make prefix="$pkgdir/usr" tooldir="$pkgdir/usr" install

  # install PIC version of libiberty
  install -m644 libiberty/pic/libiberty.a "$pkgdir/usr/lib"

  # Remove unwanted files
  rm -f "$pkgdir"/usr/share/man/man1/{dlltool,windres,windmc}*

  # No shared linking to these files outside binutils
  rm -f "$pkgdir"/usr/lib/lib{bfd,opcodes}.so
  echo 'INPUT( /usr/lib/libbfd.a -lsframe -liberty -lz -lzstd -ldl )' > "$pkgdir/usr/lib/libbfd.so"
  echo 'INPUT( /usr/lib/libopcodes.a -lbfd )' > "$pkgdir/usr/lib/libopcodes.so"

  # install FSF All Permissive License
  install -Dm644 -t "${pkgdir}"/usr/share/licenses/${pkgname}/ FSFAP
}
