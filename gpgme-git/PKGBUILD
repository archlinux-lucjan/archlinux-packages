# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>

pkgbase=gpgme-git
_pkgbase=gpgme
pkgname=(gpgme-git qgpgme-git python-gpgme-git)
pkgver=1.14.53.g0cc040e8
pkgrel=1
_python_ver=3.8
pkgdesc="A C wrapper library for GnuPG"
arch=('x86_64')
url="http://www.gnupg.org/related_software/gpgme/"
license=('LGPL')
makedepends=('libgpg-error' 'gnupg' 'qt5-base' 'python' 'swig' 'git')
source=(git://git.gnupg.org/gpgme.git)
sha1sums=('SKIP')

pkgver() {
  cd gpgme
  git describe --long --tags | sed 's/^gpgme.//;s/.base//;s/-/./g'
}

build() {
  cd gpgme
  export PYTHONHASHSEED=0
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --disable-fd-passing \
    --disable-static \
    --disable-gpgsm-test
  make

  # ensure reproducibility of .pyc files
  touch -d @$SOURCE_DATE_EPOCH lang/python/version.py
  touch -d @$SOURCE_DATE_EPOCH lang/python/python${_python_ver}-gpg/lib.linux-x86_64-${_python_ver}/gpg/gpgme.py
  
  # .pyc files will be created if check() is used - generate them here
}

#check() {
#  cd gpgme
#  this test fails with gnupg (FS#66572)
#  sed -i 's#"t-keylist-secret",##' tests/json/t-json.c

#  make check

#  ensure reproducibilty whether test-suite is run or not
#  find . -name *.pyc -exec rm {} +
#}

package_gpgme-git() {
  depends=('libgpg-error' 'gnupg>=2')
  options=('!emptydirs')
  conflicts=('gpgme')
  provides=('gpgme')

  cd gpgme
  make DESTDIR="${pkgdir}" install

  # split qgpgme
  rm -r "${pkgdir}"/usr/include/{qgpgme,QGpgME}/
  rm -r "${pkgdir}"/usr/lib/{cmake/QGpgme/,libqgpgme.*}
  rm -r "${pkgdir}"/usr/lib/python*
}

package_qgpgme-git() {
  pkgdesc="Qt bindings for GPGme"
  depends=('gpgme' 'qt5-base')
  conflicts=('qgpgme')
  provides=('qgpgme')

  cd gpgme/lang/qt
  make DESTDIR="${pkgdir}" install
}

package_python-gpgme-git() {
  pkgdesc="Python bindings for GPGme"
  depends=('gpgme' 'python')
  conflicts=('python-gpgme')
  provides=('python-gpgme')

  cd gpgme/lang/python
  make DESTDIR="${pkgdir}" install
}

