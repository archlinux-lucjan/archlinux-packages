# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor Sumner Evans <sumner.evans98 at gmail dot com>

pkgname=zsh-you-should-use-git
_pkgname=zsh-you-should-use
pkgver=1.7.3.r6.g773ae5f
pkgrel=1
pkgdesc='ZSH plugin that reminds you to use existing aliases for commands you just typed'
url='https://github.com/MichaelAquilina/zsh-you-should-use'
depends=('zsh')
makedepends=('git')
arch=('any')
license=('GPL3')
source=('git+https://github.com/MichaelAquilina/zsh-you-should-use')
install="$_pkgname.install"
conflicts=('zsh-you-should-use')
provides=('zsh-you-should-use')
sha256sums=('SKIP')


pkgver() {
    cd $_pkgname
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
    cd $_pkgname
    install -d "${pkgdir}/usr/share/zsh/plugins/${_pkgname}"
    install -Dm644 "${srcdir}/${_pkgname}/you-should-use.plugin.zsh" \
        "${pkgdir}/usr/share/zsh/plugins/${_pkgname}"

    # License
    install -Dm644 "${srcdir}/${_pkgname}/LICENSE" \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
