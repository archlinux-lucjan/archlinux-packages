# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=mupen64plus-qt-qit
_pkgname=mupen64plus-qt
pkgver=1.11.0.g6ab8666
pkgrel=1
pkgdesc="A basic launcher for Mupen64Plus"
arch=('x86_64')
url="https://github.com/dh4/mupen64plus-qt"
license=('BSD')
conflicts=('mupen64plus-qt')
provides=('mupen64plus-qt')
depends=('quazip' 'mupen64plus' 'hicolor-icon-theme')
makedepends=('git')
source=("git+https://github.com/dh4/${_pkgname}.git")
md5sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | sed 's/^v//;s/-/./g'
}  

build() {
    cd "$srcdir/$_pkgname"

    qmake-qt5
    make
}

package() {
    cd "$srcdir/$_pkgname"
    install -Dm755 "mupen64plus-qt"                     "$pkgdir/usr/bin/mupen64plus-qt"
    install -Dm644 "LICENSE"                            "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 "resources/mupen64plus-qt.desktop"   "$pkgdir/usr/share/applications/mupen64plus-qt.desktop"
    install -Dm644 "resources/images/mupen64plus.png"   "$pkgdir/usr/share/icons/hicolor/128x128/apps/mupen64plus-qt.png"
}
