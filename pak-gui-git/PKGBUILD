# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>

pkgname=pak-gui-git
pkgver=1.0.0.r288.gf386b56
pkgrel=1
pkgdesc="Pacman wrapper with GUI"
arch=('x86_64')
url='https://github.com/CachyOS/pak-gui'
license=('GPL')
depends=('pacman-contrib' 'pak-git' 'kdesu5' 'kwidgetsaddons5'
         'ksshaskpass' 'qt5-base' 'qt5-charts' 'kcoreaddons5'
         'kcrash5' 'kdbusaddons5' 'ki18n5' 'kxmlgui5' 'knotifications5'
         'kde-cli-tools')
makedepends=('extra-cmake-modules' 'kdoctools' 'git')
optdepends=('devtools: download sources from Arch Linux repositories'
            'git: download sources from POLAUR'
            'auracle-git: AUR support'
            'reflector: for mirrorlist'
            'namcap: package analysis utility'
            'downgrade:  for downgrading one or more packages'
            'libnotify:  for sending desktop notifications'
            'gcovr: tests')
provides=('pak-gui')
conflicts=('pak-gui')
source=('git+https://github.com/juliagoda/pak-gui')
sha1sums=('SKIP')

pkgver() {
  cd pak-gui
  _ver="$(cat CMakeLists.txt | grep -m3 -e MAJOR -e MINOR -e PATCH | grep -o "[[:digit:]]*" | paste -sd'.')"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S pak-gui \
    -DRUN_TESTS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
