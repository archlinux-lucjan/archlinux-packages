# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>

pkgbase=qogir-kde-git
_pkgbase=Qogir-kde
pkgname=('qogir-kde-git' 'kvantum-theme-qogir-git')
pkgver=20200506.r48.g17ff307
pkgrel=3
pkgdesc="Qogir theme for KDE Plasma 5"
arch=('any')
url="https://github.com/vinceliuice/${_pkgbase}"
license=('GPL3')
options=('!strip')
source=("${_pkgbase}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd ${_pkgbase}
  echo "$(git show --format='%cI' -q master | sed 's/T.*//g;s/-//g').r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

package_qogir-kde-git() {
    optdepends=('kvantum-theme-qogir-git: Qogir theme for Kvantum Qt style (recommended)'
                'qogir-icon-theme-git: for a more consistent and beautiful experience (recommended)')
    conflicts=('qogir-kde')
    provides=('qogir-kde')
    cd ${_pkgbase}
    install -d "${pkgdir}"/usr/share

    mkdir -p ${pkgdir}/usr/share/aurorae
    cp -r  ${srcdir}/${_pkgbase}/aurorae/themes/ ${pkgdir}/usr/share/aurorae

    mkdir -p ${pkgdir}/usr/share/color-schemes
    cp -r ${srcdir}/${_pkgbase}/color-schemes ${pkgdir}/usr/share

    mkdir -p ${pkgdir}/usr/share/plasma/desktoptheme/
    cp -r ${srcdir}/${_pkgbase}/plasma/desktoptheme ${pkgdir}/usr/share/plasma

    mkdir -p ${pkgdir}/usr/share/plasma/look-and-feel/
    cp -r ${srcdir}/${_pkgbase}/plasma/look-and-feel/ ${pkgdir}/usr/share/plasma

    mkdir -p ${pkgdir}/usr/share/sddm/themes/Qogir
    cp -r ${srcdir}/${_pkgbase}/sddm/Qogir ${pkgdir}/usr/share/sddm/themes
}

package_kvantum-theme-qogir-git() {
    pkgdesc="Qogir theme for Kvantum"
    depends=('kvantum-qt5')
    provides=('kvantum-theme-qogir')
    conflicts=('kvantum-theme-qogir')
    cd ${_pkgbase}
    install -d "${pkgdir}"/usr/share

    mkdir -p ${pkgdir}/usr/share/Kvantum/
    cp -r ${srcdir}/${_pkgbase}/Kvantum ${pkgdir}/usr/share
}
