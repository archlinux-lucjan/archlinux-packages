# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=bat-polaur-git
_pkgname=bat
pkgver=0.12.1.r0.g608cefb
pkgrel=1
pkgdesc='Cat clone with syntax highlighting and git integration'
arch=('x86_64')
url='https://github.com/sharkdp/bat'
license=('Apache-2.0')
sha256sums=('SKIP')
source=("git+https://github.com/sharkdp/${_pkgname}")
makedepends=('git' 'rust' 'clang')
depends=('libssh2')
provides=('bat' 'bat-cat-git')
conflicts=('bat' 'bat-cat-git')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${_pkgname}"
  cargo build --release
}

package() {
  cd "${srcdir}/${_pkgname}"
  install -Dm755 target/release/$_pkgname "$pkgdir/usr/bin/$_pkgname"
  install -Dm644 doc/bat.1 "$pkgdir/usr/share/man/man1/bat.1"
  install -Dm644 LICENSE-APACHE \
    "$pkgdir/usr/share/licenses/$_pkgname/LICENSE-APACHE"
  install -Dm644 LICENSE-MIT \
    "$pkgdir/usr/share/licenses/$_pkgname/LICENSE-MIT"
}
