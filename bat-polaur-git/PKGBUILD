# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=bat-polaur-git
_pkgname=bat
pkgver=0.18.3.r98.g9124271e
pkgrel=1
pkgdesc='Cat clone with syntax highlighting and git integration'
arch=('x86_64')
url='https://github.com/sharkdp/bat'
license=('Apache-2.0')
sha256sums=('SKIP')
source=("git+https://github.com/sharkdp/${_pkgname}")
makedepends=('git' 'rust' 'clang')
depends=('libssh2')
provides=('bat' 'bat-cat-git')
conflicts=('bat' 'bat-cat-git')

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cargo build \
    --manifest-path "$srcdir/$_pkgname/Cargo.toml" \
    --release
}

package() {
  install -Dm755 $_pkgname/target/release/$_pkgname "$pkgdir/usr/bin/$_pkgname"

  # Package licenses
  install -Dm644 $_pkgname/LICENSE-APACHE \
    "$pkgdir/usr/share/licenses/$_pkgname/LICENSE-APACHE"
  install -Dm644 $_pkgname/LICENSE-MIT \
    "$pkgdir/usr/share/licenses/$_pkgname/LICENSE-MIT"

  cd $_pkgname/target/release/build

  # Package the man page
  find . -name bat.1 -type f -exec install -Dm644 {} \
    "$pkgdir/usr/share/man/man1/bat.1" \;

  # Package the zsh completion file (not in zsh-completions yet)
  find . -name bat.zsh -type f -exec install -Dm644 {} \
    "$pkgdir/usr/share/zsh/site-functions/_bat" \;

  # Package the fish completion
  find . -name bat.fish -type f -exec install -Dm644 {} \
    "$pkgdir/usr/share/fish/vendor_completions.d/bat.fish" \;
}
