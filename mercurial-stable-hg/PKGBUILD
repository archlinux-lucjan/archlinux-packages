# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=mercurial-stable-hg
pkgver=4.7.2.2.m3e61146d7b24
pkgrel=1
pkgdesc="Distributed source control management tool"
arch=('i686' 'x86_64')
url="https://www.mercurial-scm.org/"
license=('GPL2')
depends=('python2')
optdepends=('tk: for the hgk GUI')
makedepends=('mercurial' 'python2-docutils')
provides=('mercurial')
conflicts=('mercurial')
backup=('etc/mercurial/hgrc')
source=("hg+https://www.mercurial-scm.org/repo/hg#branch=stable"
        "mercurial.profile")
sha256sums=('SKIP'
            '87427151713e689cd87dc50d50c048e0e58285815e4eb61962b50583532cbde5')

pkgver() {
  cd "hg"

  hg log -r . -T "{latesttag}{sub('^-0-.*', '', '-{latesttagdistance}-m{node|short}')}" | sed 's/-/./g'
}
            
prepare() {
  cd "hg"

  sed -i 's/PYTHON=python/PYTHON=python2/' "doc/Makefile"
}

build() {
  cd "hg"

  make all
  make -C "contrib/chg"
}

package() {
  cd "hg"

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -d "$pkgdir/usr/share/man/"{man1,man5}
  install -m644 doc/hg.1 "$pkgdir/usr/share/man/man1"
  install -m644 doc/{hgrc.5,hgignore.5} "$pkgdir/usr/share/man/man5"
  install -m755 contrib/hgk "$pkgdir/usr/bin"
  install -m644 -D contrib/zsh_completion "$pkgdir/usr/share/zsh/site-functions/_hg"
  install -m644 -D contrib/bash_completion "$pkgdir/usr/share/bash-completion/completions/hg"
  install -d "$pkgdir/usr/share/emacs/site-lisp"
  install -m644 contrib/{mq.el,mercurial.el} "$pkgdir/usr/share/emacs/site-lisp"

  install -Dm644 contrib/vim/HGAnnotate.vim \
    "$pkgdir/usr/share/vim/vimfiles/syntax/HGAnnotate.vim"

  # set some variables
  install -m755 -d "$pkgdir/etc/profile.d"
  install -m755 "$srcdir/mercurial.profile" "$pkgdir/etc/profile.d/mercurial.sh"

  # FS#38825 - Add certs config to package
  install -m755 -d "$pkgdir/etc/mercurial"
  cat <<-EOF > "$pkgdir/etc/mercurial/hgrc"
	[web]
	cacerts = /etc/ssl/certs/ca-certificates.crt
	EOF

  cd contrib/chg
  make DESTDIR="$pkgdir" PREFIX=/usr install
}
