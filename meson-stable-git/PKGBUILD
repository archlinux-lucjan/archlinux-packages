# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: nontlikeuname

pkgname=meson-stable-git
pkgver=0.62.0.1.g5c2d3b204
pkgrel=1
pkgdesc="SCons-like build system that use python as a front-end language and Ninja as a building backend"
arch=(any)
url="http://mesonbuild.com/"
license=('Apache')
depends=('python-setuptools' 'ninja')
makedepends=('git')
checkdepends=('gcc-objc' 'vala' 'rust' 'gcc-fortran' 'mono' 'boost' 'qt5-base' 'git' 'cython'
              'gtkmm3' 'gtest' 'gmock' 'protobuf' 'wxgtk3' 'python-gobject' 'gobject-introspection'
              'itstool' 'gtk3' 'java-environment=8' 'gtk-doc' 'llvm' 'clang' 'sdl2' 'graphviz'
              'doxygen' 'vulkan-validation-layers' 'openssh' 'mercurial' 'gtk-sharp-2' 'qt5-tools'
              'libwmf' 'valgrind' 'cmake' 'netcdf-fortran' 'openmpi' 'nasm' 'gnustep-base' 'libelf'
              'python-pytest-xdist' 'python2-setuptools') # 'cuda')
provides=('meson')
conflicts=('meson')
source=('git+https://github.com/mesonbuild/meson#branch=0.62'
        'arch-meson'
        '0001-Skip-broken-tests.patch'
        '0002-gtk-doc-fixes.patch')
md5sums=('SKIP'
         'b85d62ecca0729f0c5168f759a2ff37d'
         'fd02c6fa1e898581ddf98f81157c2ab3'
         'dba51c5e78f800d712e233e25d68feb5')

pkgver() {
	cd meson
	
	git describe --tags --long | sed 's/^v//;s/-/./g'
}
         
prepare() {
        cd meson
        
        patch -Np1 -i ../0001-Skip-broken-tests.patch

        # Fix building glib2
        patch -Np1 -i ../0002-gtk-doc-fixes.patch
}

build() {
        cd meson
        
        python setup.py build
}

# check() {
#	cd meson

#       export LC_CTYPE=en_US.UTF-8 CPPFLAGS= CFLAGS= CXXFLAGS= LDFLAGS=
#       ./run_tests.py
# }

package() {
	cd meson
	
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

        install -d "${pkgdir}/usr/share/vim/vimfiles"
        cp -rt "${pkgdir}/usr/share/vim/vimfiles" data/syntax-highlighting/vim/*/

        install -Dt "${pkgdir}/usr/share/bash-completion/completions" -m644 data/shell-completions/bash/*
        install -Dt "${pkgdir}/usr/share/zsh/site-functions" -m644 data/shell-completions/zsh/*

        # Arch packaging helper
        install -D ../arch-meson -t "${pkgdir}/usr/bin"
}
