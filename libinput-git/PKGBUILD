# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname=libinput-git
_pkgname=libinput
pkgver=1.9.0.4.g3e9e0e2
pkgrel=1
pkgdesc='A library to handle input devices in Wayland compositors'
arch=(i686 x86_64)
url='http://freedesktop.org/wiki/Software/libinput/'
provides=("libinput=${pkgver}")
license=('MIT')
depends=('mtdev' 'systemd' 'libevdev')
makedepends=('doxygen' 'graphviz' 'gtk3' 'meson' 'git')
conflicts=('libinput')
provides=('libinput')
source=(git://anongit.freedesktop.org/wayland/libinput)
sha1sums=('SKIP')

pkgver() {
	cd libinput
	git describe --long --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  cd libinput
  printf '%s\n' >>doc/libinput.doxygen.in \
    HAVE_DOT=yes DOT_IMAGE_FORMAT=svg INTERACTIVE_SVG=yes
}

build() {
  arch-meson $_pkgname build -Dtests=false
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  install -Dvm644 $_pkgname/COPYING \
    "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"

  # install doc - no Makefile target
  install -d "$pkgdir/usr/share/doc"
  cp -av build/html "$pkgdir/usr/share/doc/libinput"
}
