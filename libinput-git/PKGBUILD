# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname=libinput-git
_pkgname=libinput
pkgver=1.18.0.24.gb7aac3c1
pkgrel=1
pkgdesc='A library to handle input devices in Wayland compositors'
arch=(x86_64)
url='http://freedesktop.org/wiki/Software/libinput/'
license=('MIT')
depends=('mtdev' 'systemd' 'libevdev' 'libwacom')
# upstream doesn't recommend building docs
makedepends=('gtk3' 'meson' 'git') # 'doxygen' 'graphviz' 'python-sphinx' 'python-recommonmark'
optdepends=('gtk3: libinput debug-gui'
            'python-pyudev: libinput measure'
            'python-libevdev: libinput measure')
conflicts=('libinput')
provides=("libinput=${pkgver}")
source=(git://anongit.freedesktop.org/wayland/libinput#branch=main)
sha1sums=('SKIP')

pkgver() {
  cd libinput
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

build() {
  arch-meson $_pkgname build \
    -D b_lto=false \
    -D udev-dir=/usr/lib/udev \
    -D tests=false \
    -D documentation=false
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  install -Dvm644 $_pkgname/COPYING \
    "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}
