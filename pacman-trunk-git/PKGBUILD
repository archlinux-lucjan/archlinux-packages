# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: godane <slaxemulator@gmail.com.com>
# Contributor: Andres Perera <aepd87@gmail.com>

pkgname=pacman-trunk-git
pkgver=5.1.1.200.g024fde97
pkgrel=1
pkgdesc="A library-based package manager with dependency support. git version."
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('bash' 'glibc' 'libarchive' 'curl'
         'gpgme' 'pacman-mirrorlist' 'archlinux-keyring')
makedepends=('asciidoc' 'git' 'meson')
checkdepends=('python2' 'fakechroot')
optdepends=('perl-locale-gettext: translation support in makepkg-template'
            'xdelta3: delta support in repo-add')
backup=(etc/pacman.conf
        etc/makepkg.conf)
provides=("pacman=$pkgver")
conflicts=('pacman')
options=('!libtool' '!strip')
source=(git://git.archlinux.org/pacman.git
        pacman.conf
        makepkg.conf)
sha1sums=('SKIP'
          'fde4ac4e24405b15157382b59cee80c58b02c95b'
          'b29a926a56b9758d4116b8a8588b597d3ad074ed')

pkgver() {
  cd pacman
  git describe --long | sed 's/^v//;s/-/./g'
}

local _meson_options=(
    -Ddoc=enabled
    -Duse-git-version=true
    -Dscriptlet-shell=/usr/bin/bash
    -Dldconfig=/usr/bin/ldconfig
)

build() {
  arch-meson pacman build  "${_meson_options[@]}"
    
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  
  DESTDIR="$pkgdir" ninja -C build install

  # install Arch specific stuff
  install -dm755 "$pkgdir/etc"
  install -m644 "$srcdir/pacman.conf" "$pkgdir/etc"
  install -m644 "$srcdir/makepkg.conf" "$pkgdir/etc"
}
