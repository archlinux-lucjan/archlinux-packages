# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: godane <slaxemulator@gmail.com.com>
# Contributor: Andres Perera <aepd87@gmail.com>

pkgname=pacman-trunk-git
pkgver=5.2.0.0.g8ce20d37
pkgrel=1
pkgdesc="A library-based package manager with dependency support. git version."
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('bash' 'glibc' 'libarchive' 'curl'
         'gpgme' 'pacman-mirrorlist' 'archlinux-keyring')
makedepends=('asciidoc' 'git' 'meson')
checkdepends=('python' 'fakechroot')
optdepends=('perl-locale-gettext: translation support in makepkg-template')
backup=(etc/pacman.conf
        etc/makepkg.conf)
provides=("pacman=$pkgver" 'libalpm.so')
conflicts=('pacman')
options=('!libtool' '!strip')
source=(git://git.archlinux.org/pacman.git
        pacman.conf
        makepkg.conf)
sha1sums=('SKIP'
          'a9306b6da1f490c5ebebc4ae8661fb5800558b4c'
          'f539bf108a977a7c9875480d4c4ba3ca95070326')

pkgver() {
  cd pacman
  git describe --long | sed 's/^v//;s/-/./g'
}

local _meson_options=(
    -Ddoc=enabled
    -Duse-git-version=true
    -Dscriptlet-shell=/usr/bin/bash
    -Dldconfig=/usr/bin/ldconfig
)

build() {
  arch-meson pacman build  "${_meson_options[@]}"
    
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  
  DESTDIR="$pkgdir" ninja -C build install

  # install Arch specific stuff
  install -dm755 "$pkgdir/etc"
  install -m644 "$srcdir/pacman.conf" "$pkgdir/etc"
  install -m644 "$srcdir/makepkg.conf" "$pkgdir/etc"
}
