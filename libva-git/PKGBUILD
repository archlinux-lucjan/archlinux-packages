# $Id: PKGBUILD 257302 2015-12-27 18:51:39Z bpiotrowski $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=libva-git
_pkgname=libva
pkgver=2.2.0.2018.r1525.gf0dfe5e
pkgrel=1
pkgdesc='Video Acceleration (VA) API for Linux'
arch=('x86_64')
url='http://freedesktop.org/wiki/Software/vaapi'
license=('MIT')
depends=('libdrm' 'libgl' 'libx11' 'libxext' 'libxfixes' 'wayland')
makedepends=('git' 'libglvnd' 'mesa' 'meson')
optdepends=('libva-vdpau-driver: backend for Nvidia and AMD cards'
            'libva-intel-driver: backend for Intel cards')
conflicts=('libva')
provides=('libva')
source=('git+https://github.com/01org/libva.git')
md5sums=('SKIP')

pkgver() {
  cd libva
    _ver="$(cat NEWS | grep -m1 Version | grep -o "[[:digit:]]*" | paste -sd'.')"
    echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  arch-meson libva build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 libva/COPYING -t "${pkgdir}"/usr/share/licenses/libva
}
