# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=libxcrypt-dev-git
_pkgname=libxcrypt
pkgver=4.4.17.r1.gb475497
pkgrel=1
pkgdesc='Modern library for one-way hashing of passwords'
arch=(x86_64)
url='https://github.com/besser82/libxcrypt/'
license=(GPL)
depends=('glibc')
makedepends=('git')
provides=('libcrypt.so' 'libxcrypt')
conflicts=('libxcrypt')
source=("git+https://github.com/besser82/libxcrypt#branch=develop")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname

  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_pkgname
  autoreconf -fi
}

build() {
  cd $_pkgname
  ./configure \
    --prefix=/usr \
    --disable-static \
    --enable-hashes=strong,glibc \
    --enable-obsolete-api=no \
    --disable-failure-tokens
  make
}

check() {
  cd $_pkgname
  make check 
}

package() {
  cd $_pkgname
  make DESTDIR="$pkgdir" install
}
