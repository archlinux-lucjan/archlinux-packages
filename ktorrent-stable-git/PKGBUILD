# $Id$
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Georg Grabler <ggrabler@gmail.com>

pkgname=ktorrent-stable-git
_pkgname=ktorrent
pkgver=5.1.0.20.g3c940225
pkgrel=1
pkgdesc="A powerful BitTorrent client for KDE"
arch=(x86_64)
url='https://www.kde.org/applications/internet/ktorrent/'
license=(GPL2)
depends=(libktorrent kcmutils knotifyconfig kross)
makedepends=(extra-cmake-modules python kdoctools boost plasma-workspace kdnssd taglib kde-syndication kplotting kdewebkit)
optdepends=('plasma-workspace: shutdown plugin' 'kdnssd: zeroconf plugin' 'taglib: mediaplayer plugin'
            'kde-syndication: syndication plugin' 'kdewebkit: search plugin, syndication plugin' 'kplotting: statistics plugin')
conflicts=('ktorrent')
provides=('ktorrent')
source=('git://anongit.kde.org/ktorrent#branch=5.1')
sha1sums=('SKIP')

prepare() {
  mkdir -p build
}

pkgver() {
  cd $_pkgname
  git describe  --long | sed 's/^v//;s/-/./g'
}


build() {
	cd build
	cmake ../$_pkgname \
	  -DCMAKE_BUILD_TYPE=Release \
	  -DCMAKE_INSTALL_PREFIX=/usr \
          -DKDE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
