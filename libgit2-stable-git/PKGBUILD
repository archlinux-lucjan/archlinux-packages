# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Hilton Medeiros <medeiros.hilton@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=libgit2-stable-git
_pkgname=libgit2
pkgver=0.28.2.r0.gb3e1a56eb
pkgrel=3
epoch=1
pkgdesc='A linkable library for Git'
arch=('x86_64')
url='https://libgit2.github.com/'
depends=('http-parser' 'libssh2')
makedepends=('cmake' 'python' 'git')
provides=('libgit2' 'libgit2.so')
conflicts=('libgit2')
license=('GPL2')
source=("git://github.com/libgit2/libgit2#branch=maint/v0.28"
        "${pkgname}-0.28.2-online-tests.patch::https://github.com/libgit2/libgit2/pull/5094.patch")
sha256sums=('SKIP'
            '0d1d5ceaae10a9dc17894b8f13e957b463e5f5ccde13dc57f19dbc80f5d89de8')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"
  mkdir -p build
  # fix online tests: https://github.com/libgit2/libgit2/pull/5094
  patch -Np1 -i "../${pkgname}-0.28.2-online-tests.patch"
}

build() {
  cd "$_pkgname"
  (
    cd build
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          ..
  )
  make -C build VERBOSE=1
}

check() {
  cd "$_pkgname"
  make -C build test VERBOSE=1
}

package() {
  cd "$_pkgname"
  make -C build DESTDIR="$pkgdir" install
  install -vDm 644 {AUTHORS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
