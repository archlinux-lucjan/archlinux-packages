# Maintainter: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Aetf <aetf@unlimitedcodeworks.xyz>
# Contributor: Sameed Pervaiz <greenbagels@teknik.io>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Jakub Schmidtke <sjakub-at-gmail-dot-com>
# Contributor: mosra <mosra@centrum.cz>

pkgname=kdevelop-dev-git
pkgver=5.6.2.361.g8eae85c922
pkgrel=1
_clangver=12.0.1
pkgdesc="A C/C++ development environment for KDE. (Git version)"
arch=('x86_64')
url='http://www.kdevelop.org'
license=('GPL')
depends=("clang=$_clangver" 'libksysguard' 'kitemmodels' 'knotifyconfig' 'ktexteditor' 'grantlee' 'libkomparediff2'
         'kcmutils' 'threadweaver' 'knewstuff')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdevelop-pg-qt' 'llvm' 'qt5-tools' 'plasma-framework' 'krunner' 'okteta' 'boost' 'purpose' 'subversion' 'astyle')
optdepends=('konsole: embedded terminal'
            'git: Git support' 'subversion: SVN support'
            'gdb: GNU debugger support' 'lldb: LLDB debugger support'
            'qt5-doc: Qt documentation integration'
            'cmake: cmake integration'
            'meson: meson integration'
            'qt5-tools: qthelp plugin'
            'okteta: GNU debugger support'
            'plasma-framework: for the plasma addons'
            'purpose: patch review plugin'
            'astyle: astyle plugin'
            'cppcheck: code analyzer' 'heaptrack: memory profiler'
            'clazy: clazy analyzer plugin')
conflicts=('kdevelop' 'kdevplatform')
provides=('kdevelop' 'kdevplatform' 'kdevplatform-git')
replaces=('kdevplatform-git')
source=('git+https://anongit.kde.org/kdevelop.git#branch=master')
sha1sums=('SKIP')

pkgver() {
  cd kdevelop
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

build() {
  cmake -B build -S kdevelop \
        -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
