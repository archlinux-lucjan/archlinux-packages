# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>

pkgname=psi-clean-git
pkgver=1.3.253.g52c7d436
pkgrel=1
pkgdesc="A jabber client without webkit/webengine support. GIT version."
arch=('x86_64')
url="http://psi-im.org/"
license=('GPL2')
depends=('qt5-multimedia' 'qt5-x11extras' 'qca-qt5'
	 'libidn' 'libxss' 'qt5-svg' 'hunspell' 'qtkeychain')
makedepends=('patch' 'cmake' 'git')
provides=('psi' 'psi-qt5-git')
conflicts=('psi' 'psi-qt5-git')
source=('git+https://github.com/psi-im/psi.git')
md5sums=('SKIP')

pkgver() {
   
   cd $srcdir/psi
   
   git describe --tags | sed 's/^v//;s/-/./g'
}

prepare() {

  cd $srcdir/psi

  git submodule init
  git submodule update

}

build() {

  cd $srcdir/psi
  
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DENABLE_WEBKIT=OFF ..
  make

}

package() {
  
  cd $srcdir/psi/build

  make DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/include/psi/plugins"
  install -m644 ../src/plugins/include/*.h "$pkgdir/usr/include/psi/plugins"
}

