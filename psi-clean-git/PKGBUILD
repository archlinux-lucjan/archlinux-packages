# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>

pkgname=psi-clean-git
pkgver=1.4.916.gec85e137
pkgrel=1
pkgdesc="A XMPP client without webkit/webengine support. GIT version."
arch=('x86_64')
url="https://psi-im.org/"
license=('GPL2')
depends=('qt5-multimedia' 'qt5-x11extras' 'qca-qt5'
	 'libidn' 'libxss' 'qt5-svg' 'hunspell' 'qtkeychain')
makedepends=('patch' 'cmake' 'git')
provides=('psi' 'psi-qt5-git')
conflicts=('psi' 'psi-qt5-git')
source=('git://github.com/psi-im/psi.git'
        'git://github.com/psi-im/iris.git'
        'git://github.com/psi-im/libpsi.git')
md5sums=('SKIP'
         'SKIP'
         'SKIP')

pkgver() {
   
   cd $srcdir/psi
   
   git describe --tags | sed 's/^v//;s/-/./g'
}

prepare() {

  cd $srcdir/psi

  # makepkg doesn't support --recursive
  # so setup git modules manually
  git submodule init
  git config submodule.iris.url "$srcdir/iris"
  git config submodule.src/libpsi.url "$srcdir/libpsi"
  git submodule update
}

build() {

  cd $srcdir/psi
  
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DCHAT_TYPE=BASIC ..
  make

}

package() {
  cd psi/build

  make DESTDIR="$pkgdir" install
}

