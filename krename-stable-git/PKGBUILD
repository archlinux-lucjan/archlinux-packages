# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=krename-stable-git
pkgver=5.0.1.r455.0d5c4ea
pkgrel=1
pkgdesc="A very powerful batch file renamer for KDE. (GIT Version)"
arch=('x86_64')
url='http://www.krename.net'
license=('GPL')
depends=('kio' 'kjs' 'exiv2' 'podofo' 'taglib' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'git')
provides=('krename' 'krename-frameworks-git')
conflicts=('krename' 'krename-frameworks-git')
source=('git+https://anongit.kde.org/krename.git#branch=5.0')
sha1sums=('SKIP')

pkgver() {
  cd krename
  _ver="$(cat CMakeLists.txt | grep -m1 KRENAME_VERSION | grep -o "[[:digit:]]*" | paste -sd'.')"
  echo "${_ver}.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S krename \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
