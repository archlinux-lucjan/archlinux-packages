# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=openmp-git
_pkgname=openmp
pkgver=9.0.0.r314873.gb1ba4d8a8a3
pkgrel=1
pkgdesc="LLVM OpenMP Runtime Library"
arch=('x86_64')
url="https://openmp.llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
depends=('glibc' 'libelf' 'libffi')
makedepends=('cmake' 'ninja')
conflicts=('openmp')
provides=('openmp')
source=("llvm-project::git+https://github.com/llvm/llvm-project.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/llvm-project/llvm"

    # This will almost match the output of `llvm-config --version` when the
    # LLVM_APPEND_VC_REV cmake flag is turned on. The only difference is
    # dash being replaced with underscore because of Pacman requirements.
    local _pkgver=$(awk -F 'MAJOR |MINOR |PATCH |)' \
            'BEGIN { ORS="." ; i=0 } \
             /set\(LLVM_VERSION_/ { print $2 ; i++ ; if (i==2) ORS="" } \
             END { print "\n" }' \
             CMakeLists.txt).r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)
    echo "${_pkgver}"
}

prepare() {
	cd "${srcdir}/llvm-project/openmp"
	mkdir build
}

build() {
	cd "$srcdir/llvm-project/openmp/build"

  cmake .. -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd "$srcdir/llvm-project/openmp/build"

  DESTDIR="$pkgdir" ninja install
  install -Dm644 ../LICENSE.txt "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"

  # libgomp.so is also provided by gcc-libs; remove it to fix the conflict
  rm "$pkgdir/usr/lib/libgomp.so"

}

# vim:set ts=2 sw=2 et:
