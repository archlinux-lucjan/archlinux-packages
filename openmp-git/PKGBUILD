# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=openmp-git
_pkgname=openmp
pkgver=15.0.0.r429042.g519d7876cbee
pkgrel=1
pkgdesc="LLVM OpenMP Runtime Library"
arch=('x86_64')
url="https://openmp.llvm.org/"
license=('custom:Apache 2.0 with LLVM Exception')
depends=('glibc' 'libelf' 'libffi')
makedepends=('cmake' 'ninja' 'git')
conflicts=('openmp')
provides=('openmp')
source=("llvm-project::git+https://github.com/llvm/llvm-project.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/llvm-project/llvm"

    local _pkgver=$(awk -F 'MAJOR |MINOR |PATCH |)' \
            'BEGIN { ORS="." ; i=0 } \
             /set\(LLVM_VERSION_/ { print $2 ; i++ ; if (i==2) ORS="" } \
             END { print "\n" }' \
             CMakeLists.txt).r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)
    echo "${_pkgver}"
}

prepare() {
	cd "${srcdir}/llvm-project/openmp"
	mkdir build
}

build() {
	cd "$srcdir/llvm-project/openmp/build"

  local cmake_args=(
    -G Ninja
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=/usr
    -DLIBOMP_INSTALL_ALIASES=OFF
  )
  cmake .. "${cmake_args[@]}"
  ninja
}
package() {
  cd "$srcdir/llvm-project/openmp/build"

  DESTDIR="$pkgdir" ninja install
  install -Dm644 ../LICENSE.TXT "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"

  rm "$pkgdir/usr/lib/libarcher_static.a"

}

# vim:set ts=2 sw=2 et:
