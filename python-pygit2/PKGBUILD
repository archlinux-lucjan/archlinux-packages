# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgbase=python-pygit2
pkgname=('python-pygit2-git' 'python2-pygit2-git')
pkgver=0.27.4.r0.gda7ebb5
pkgrel=1
pkgdesc='Python bindings for libgit2'
arch=('x86_64')
url="https://github.com/libgit2/pygit2"
license=('GPL2')
depends=('libgit2>=1:0.27.0')
makedepends=('python-cffi' 'python2-cffi' 'python-setuptools' 'python2-setuptools' 'git' 'python-six' 'python2-six')
source=("git://github.com/libgit2/pygit2")
sha256sums=('SKIP')

pkgver() {

  cd "pygit2"
  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package_python-pygit2-git() {
  depends+=('python-cffi' 'python-six')
  replaces=('python-libgit2' 'python-pygit2')
  conflicts=('python-libgit2' 'python-pygit2')

  cd "pygit2"
  LANG=en_US.UTF8 python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pygit2-git() {
  depends+=('python2-cffi' 'python2-six')
  replaces=('python2-libgit2' 'python2-pygit2')
  conflicts=('python2-libgit2' 'python2-pygit2')

  cd "pygit2"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
