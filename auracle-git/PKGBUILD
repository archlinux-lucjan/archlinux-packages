# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>

pkgname=auracle-git
_pkgname=auracle
pkgver=20191207.r305.g941b5aa
pkgrel=1
pkgdesc='A flexible client for the AUR'
arch=('x86_64')
url="https://github.com/falconindy/auracle.git"
license=('MIT')
depends=('pacman' 'libcurl.so' 'libsystemd.so')
makedepends=('meson' 'git' 'perl')
checkdepends=('gtest' 'gmock')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+https://github.com/falconindy/auracle.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"

  echo "$(git show --format='%cI' -q master | sed 's/T.*//g;s/-//g').r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cd "$_pkgname"

  local meson_args=(
    --prefix=/usr
    --buildtype=plain
    --default-library=static
  )

  [[ -d build ]] && meson_args+=(--wipe)

  meson build "${meson_args[@]}"

  ninja -C build
}

check() {
  meson test -C "$_pkgname/build"
}

package () {
  cd "$_pkgname"

  DESTDIR=$pkgdir ninja -C build install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: et ts=2 sw=2
