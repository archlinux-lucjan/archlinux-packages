# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>

pkgbase=matcha-kde-git
_pkgbase=Matcha-kde
pkgname=('matcha-kde-git' 'kvantum-theme-matcha-git')
pkgver=20190703.r9.g283b5b1
pkgrel=1
pkgdesc="Matcha theme for KDE Plasma 5"
arch=('any')
url="https://github.com/vinceliuice/${_pkgbase}"
license=('GPL3')
options=('!strip')
source=("${_pkgbase}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd ${_pkgbase}
  echo "$(git show --format='%cI' -q master | sed 's/T.*//g;s/-//g').r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

package_matcha-kde-git() {
    optdepends=('kvantum-theme-matcha-git: Matcha theme for Kvantum Qt style (recommended)'
                'tela-icon-theme-git: for a more consistent and beautiful experience (recommended)')
    conflicts=('matcha-kde')
    provides=('matcha-kde')
    cd ${_pkgbase}
    install -d "$pkgdir"/usr/share
    
    rm sddm/{README.md,install.sh}
    
    # Temporary fix for upstream filestructure
    mv aurorae themes
    mkdir -p aurorae
    mv themes aurorae/
    
    mv sddm themes
    mkdir -p sddm
    mv themes sddm/

    cp -r aurorae "$pkgdir"/usr/share
    cp -r color-schemes "$pkgdir"/usr/share
    cp -r plasma "$pkgdir"/usr/share
    cp -r sddm "$pkgdir"/usr/share
}

package_kvantum-theme-matcha-git() {
    pkgdesc="Matcha theme for Kvantum"
    depends=('kvantum-qt5')
    provides=('kvantum-theme-matcha')
    conflicts=('kvantum-theme-matcha')
    cd ${_pkgbase}
    install -d "${pkgdir}"/usr/share

    cp -r Kvantum "$pkgdir"/usr/share
}
