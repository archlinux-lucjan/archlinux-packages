# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Joel Teichroeb <joel@teichroeb.net>
# Contributor: Scimmia

pkgbase=wayland-git
pkgname=(wayland-git wayland-git-docs)
pkgver=1.20.0.34.g1078ee4
pkgrel=1
pkgdesc='A computer display server protocol.'
arch=(x86_64)
url='http://wayland.freedesktop.org'
license=('MIT')
depends=('glibc' 'libffi' 'expat' 'libxml2')
makedepends=('meson' 'libxslt' 'doxygen' 'xmlto' 'graphviz' 'docbook-xsl')
source=(git://anongit.freedesktop.org/wayland/wayland#branch=main)
sha1sums=('SKIP')

pkgver() {
  cd wayland

  git describe --long --tags | sed 's/^v//;s/-/./g'
}


build() {
  arch-meson wayland build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_wayland-git() {
  conflicts=('wayland')
  provides=('wayland' libwayland-{client,cursor,egl,server}.so)

  meson install -C build --destdir "$pkgdir"
  mkdir -p docs/share
  mv "$pkgdir"/usr/share/{doc,man} docs/share
  install -Dm 644 wayland/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_wayland-git-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  conflicts=('wayland-docs')
  provides=('wayland-docs')

  mv docs "$pkgdir/usr"
  install -Dm 644 wayland/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
