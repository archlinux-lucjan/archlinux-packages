# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Joel Teichroeb <joel@teichroeb.net>
# Contributor: Scimmia

pkgname=wayland-git
pkgname=(wayland-git wayland-git-docs)
pkgver=1.17.0.5.gde64489
pkgrel=1
pkgdesc='A computer display server protocol.'
arch=(x86_64)
url='http://wayland.freedesktop.org'
license=('MIT')
depends=('glibc' 'libffi' 'expat' 'libxml2')
makedepends=('libxslt' 'doxygen' 'xmlto' 'graphviz' 'docbook-xsl' 'git')
source=(git://anongit.freedesktop.org/wayland/wayland)
sha1sums=('SKIP')

pkgver() {
  cd wayland

  git describe --long --tags | sed 's/^v//;s/-/./g'
}


build() {
  cd wayland

  ./autogen.sh --prefix=/usr \
               --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_wayland-git() {
  conflicts=('wayland')
  provides=('wayland')
  cd wayland

  make DESTDIR="$pkgdir" install

  mkdir -p "$srcdir/docs/share"
  mv "$pkgdir"/usr/share/{doc,man} "$srcdir/docs/share"

  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_wayland-git-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  cd wayland

  mv "$srcdir/docs" "$pkgdir/usr"

  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
