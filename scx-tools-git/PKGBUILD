# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>

# Available profiles: “release”, “release-tiny”, “release-fast“
# See: https://github.com/sched-ext/scx-loader/blob/main/Cargo.toml
_mode=release

pkgname=scx-tools-git
_gitname=scx-loader
pkgver=1.0.18.r7.gb60b6a9
pkgrel=3
epoch=1
pkgdesc='scx_loader: A DBUS Interface for Managing sched_ext Schedulers'
url='https://github.com/sched-ext/scx-loader'
arch=('x86_64')
license=('GPL-2.0-only')
depends=(
  polkit
  scx-scheds
)
makedepends=(
  cargo
  clang
  git
  llvm
  llvm-libs
)
options=(!lto)
provides=("scxctl" "scx-tools")
conflicts=("scxctl" "scx-tools")
source=("git+https://github.com/sched-ext/scx-loader")
sha256sums=('SKIP')

_backports=(
4799af2bf79d094d78481d382e753fbb24654515 # scx_loader: Add chaos scheduler
d99f3759cace0b08b7eac8128c3d04c564dd199c # scx_loader: Add mitosis scheduler
aee061086bdd047d51cd7dd3e89e9728a354e5d3 # scx_loader: Add wd40 scheduler
cdfc0fc4d58f3081e2605922bd3e70fdef1a3f03 # scx_loader: some Rust formatting changes
962829500dbb17043cc385a9f9f21f9708c69f4c # scx_loader: add RestoreDefault D-Bus method and properties
d0dcb83924a4ffb38df23b0f57d95e146c4a6a61 # scx_loader: add client proxy for RestoreDefault
6e89c580cc390dd9df3f85c73e9bec937f5398ee # scxctl: add restore command to switch to default scheduler
c99c0adf4c606e444cd987371002d013e47e7b1a # scxctl: add RestoreDefault to polkit
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --profile=$_mode --frozen --all-features --workspace
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/$_mode \
    -maxdepth 1 -type f -executable ! -name '*.so' ! -name 'xtask' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +

  # Install runtime assets via xtask
  # (systemd units, D-Bus services, configs, sample files)
  ./target/$_mode/xtask install --destdir "$pkgdir"
}
