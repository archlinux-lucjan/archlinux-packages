# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-tools-git
_gitname=scx-loader
pkgver=20251102.r364.g75366b4
pkgrel=4
pkgdesc='scx_loader: A DBUS Interface for Managing sched_ext Schedulers'
url='https://github.com/sched-ext/scx-loader'
arch=('x86_64')
license=('GPL-2.0-only')
depends=(
  scx-scheds-git
)
makedepends=(
  cargo
  clang
  git
  llvm
  llvm-libs
)
options=(!lto)
provides=("scxctl")
conflicts=("scxctl")
source=("git+https://github.com/sched-ext/scx-loader")
sha256sums=('SKIP')

_backports=(
3c46094a1dbf03a3a7d0a6803381f921e5732b0c # lint: run clippy
1721913104bc3ad9e10d607773b247ef9fd4f7f9 # scx_loader: tokio spawn doesn't return result
86f7c5132d0199d2e5256e853bc8236f767ce7ad # scx_loader: sync function marked async
d4e16c698f18163d46bf756ede58e222e5c2d7c8 # scx_loader: make clippy happy
a81b5bfc1e0ee785f37a11147fd2562c15171f36 # scx_loader: suppress clippy warnings
319481927c54ffa633c6c047e5ea35f1eecd3b32 # scx_loader: add missing `# Errors` doc
8b47bcc05ebd8036213d8192b36fea7408a2dd74 # scx_loader: MESON_VENDORDIR->VENDORDIR
720cfa00d692ddd89f6cc9288f2c4d02f09ccdeb # scxctl: make clippy happy
3583ba33afdd19ecc198c65eed82117072010f58 # scx_loader: Add chaos scheduler
1a64d5b9e23fba1b619ccbe36707c6f93b89609e # scx_loader: Add mitosis scheduler
32d8d4297a72c85f16cb5aed888428ff6ddf5323 # scx_loader: Add wd40 scheduler
a54fa4d2662f6de50ab7532897a008d91cb15b4b # scx_loader: some Rust formatting changes
)

_reverts=(
)

pkgver() {
  cd $_gitname
  echo "$(git show --format='%cI' -q main | sed 's/T.*//g;s/-//g').r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --frozen --all-features
}

#check() {
#  cd $_gitname
#  export RUSTUP_TOOLCHAIN=stable
#  cargo test --frozen --all-features
#}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +

  # systemd service
  install -Dm644 services/scx_loader.service \
    -t "$pkgdir/usr/lib/systemd/system/"

  # D-Bus service
  install -Dm644 services/org.scx.Loader.service \
    -t "$pkgdir/usr/share/dbus-1/system-services/"

  # D-Bus configuration
  install -Dm644 configs/org.scx.Loader.conf \
    -t "$pkgdir/usr/share/dbus-1/system.d/"

  # scx_loader sample configuration (template)
  install -Dm644 configs/scx_loader.toml \
    "$pkgdir/usr/share/scx_loader/config.toml"
}
