# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor : Lone_Wolf <lonewolf at xs4all dot nl>
# Contributor : Jan de Groot <jgc@archlinux.org>

pkgname=libdrm-git
_pkgname=libdrm
pkgver=2.4.85.2.g61ff9779
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services, master git version"
arch=(x86_64)
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('valgrind' 'xorg-util-macros')
checkdepends=('cairo' 'cunit')
url="https://dri.freedesktop.org/"
provides=('libdrm')
conflicts=('libdrm')

source=('libdrm::git://anongit.freedesktop.org/mesa/drm#branch=master'
        COPYING)
sha256sums=('SKIP'
            '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93')

pkgver() {
  cd ${_pkgname}
  git describe --long | sed 's/^libdrm-//;s/-/./g'
}

prepare() {
  cd ${_pkgname}
}

build() {
  cd ${_pkgname}
  ./autogen.sh --prefix=/usr --enable-udev
  make
}



package() {
  cd ${_pkgname}
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 ${srcdir}/COPYING "$pkgdir/usr/share/licenses/$pkgname/"
}
