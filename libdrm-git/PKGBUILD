# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor : Lone_Wolf <lonewolf at xs4all dot nl>
# Contributor : Jan de Groot <jgc@archlinux.org>

pkgname=libdrm-git
_pkgname=libdrm
pkgver=2.4.100.0.g0190f49a
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services, master git version"
arch=(x86_64)
license=('custom')
depends=('libpciaccess')
makedepends=('valgrind' 'libxslt' 'docbook-xsl' 'meson')
checkdepends=('cairo')
url="http://dri.freedesktop.org/"
provides=('libdrm')
conflicts=('libdrm')

source=('libdrm::git://anongit.freedesktop.org/mesa/drm#branch=master'
        COPYING)
sha256sums=('SKIP'
            '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93')

pkgver() {
  cd $_pkgname
  git describe --long | sed 's/^libdrm-//;s/-/./g'
}

prepare() {
  cd $_pkgname
}

build() {
  arch-meson $_pkgname build \
    -Dudev=false \
    -Dvalgrind=false
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$_pkgname" -m644 COPYING
}
