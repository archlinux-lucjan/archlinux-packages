# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: Johan Förberg <johan@forberg.se>

pkgname=zstd-git
_pkgname=zstd
pkgver=1.5.2.r9596.g3b1bd9185
pkgrel=1
pkgdesc='A fast and efficient compression algorithm.'
arch=('x86_64')
url='https://github.com/facebook/zstd'
license=('BSD' 'GPL2')
depends=('glibc' 'gcc-libs' 'zlib' 'xz' 'lz4')
makedepends=('cmake' 'gtest' 'ninja' 'git')
conflicts=('zstd')
provides=('zstd' 'libzstd.so')
options=('staticlibs')
# Use the dev branch by default; upstream merges only full releases to master.
source=('git+https://github.com/facebook/zstd.git#branch=dev')
md5sums=('SKIP')

pkgver() {
    cd $_pkgname
    # git describe
    # git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g'

    # other
    _ver="$(cat lib/zstd.h | grep -m3 -e _MAJOR -e _MINOR -e _RELEASE | grep -o "[[:digit:]]*" | paste -sd'.')"
    echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
    cmake -S $_pkgname/build/cmake -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DZSTD_BUILD_CONTRIB=ON \
    -DZSTD_BUILD_TESTS=ON
  cmake --build build
}

#check() {
#    cd build/tests
#    ctest
#}

package() {
     DESTDIR="${pkgdir}" cmake --install build
  ln -sf /usr/bin/zstd "${pkgdir}/usr/bin/zstdmt"
  install -Dm 644 $_pkgname/LICENSE -t "${pkgdir}/usr/share/licenses/$_pkgname"
}
