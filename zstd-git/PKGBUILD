# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: Johan Förberg <johan@forberg.se>

pkgname=zstd-git
_pkgname=zstd
pkgver=1.5.0.r8924.gb3e372c1
pkgrel=1
pkgdesc='A fast and efficient compression algorithm.'
arch=('x86_64')
url='https://github.com/facebook/zstd'
license=('BSD' 'GPL2')
depends=('glibc' 'gcc-libs' 'zlib' 'xz' 'lz4')
makedepends=('git' 'gtest')
conflicts=('zstd')
provides=('zstd' 'libzstd.so')
# Use the dev branch by default; upstream merges only full releases to master.
source=('git://github.com/facebook/zstd.git#branch=dev')
md5sums=('SKIP')

pkgver() {
    cd $_pkgname
    # git describe
    # git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g'

    # other
    _ver="$(cat lib/zstd.h | grep -m3 -e _MAJOR -e _MINOR -e _RELEASE | grep -o "[[:digit:]]*" | paste -sd'.')"
    echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
    cd $_pkgname
    make
    make -C contrib/pzstd
}

check() {
    cd $_pkgname
    make check
    make -C contrib/pzstd test
}

package() {
    cd $_pkgname
    make PREFIX=/usr DESTDIR="${pkgdir}" install
    ln -sf /usr/bin/zstd "${pkgdir}/usr/bin/zstdmt"
    install -Dm 755 contrib/pzstd/pzstd -t "${pkgdir}/usr/bin"
    install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}"
}
