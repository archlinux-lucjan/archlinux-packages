# Maintainer: Tejun Heo <tj@kernel.org>
# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds
_gitname=scx
pkgver=1.0.14
pkgrel=1.1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
options=(!lto)
provides=("scxctl")
conflicts=("scxctl")
source=(#"git+https://github.com/sched-ext/scx?signed#tag=v$pkgver"
        "git+https://github.com/sched-ext/scx#tag=v$pkgver"
        '0001-meson-install-scx_mitosis-and-scx_wd40.patch'
        '0002-scx_loader-Add-missing-schedulers.patch'
        '0003-etc-default-scx-set-scx_flash-as-default-scheduler.patch')
sha256sums=('f469c6396455572832bff13a3ede140f42b3293f4239364947e5a785d3a00714'
            '4e65504781e3cf20d5f65d82f9629c87df1122073d1b4440368a92c7d692afa5'
            '7a05ea1c3a131d0af8bb74738713e2c741543f52fc6ff6a746eb167327299d5e'
            'bbef69a3447a82aeccf1e44bb5314b1d1402f61ffed1074f355a2f79fd669f51')
validpgpkeys=(
  697C63013E65270255EBC2608744DC1EB26B5A9A  # Tejun Heo <tj@kernel.org>
  F5504C7B7B8107B40EF9E97AA1148BB3207BCC33  # David Vernet
)

_backports=(
)

_reverts=(
)

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
