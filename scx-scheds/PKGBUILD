# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Maintainer: Tejun Heo <tj@kernel.org>

pkgname=scx-scheds
gitname=scx
pkgver=0.1.9
pkgrel=12
pkgdesc="sched_ext schedulers"
arch=('x86_64')
url="https://github.com/sched-ext/scx"
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq')
makedepends=('git' 'python' 'meson' 'lucjan-meson' 'clang' 'llvm' 'llvm-libs' 'cargo' 'rust')
backup=('etc/default/scx' 'etc/systemd/journald@sched-ext.conf')
source=("git+https://github.com/sched-ext/scx#tag=v$pkgver")
sha256sums=('4b226e717366a2d11561f72c9bde85b89390dbeb3f909504e5986e8751d9ef9e')
options=(!lto)
replaces=("scx-git" "scx-systemd-services-git")

_backports=(
1fbf4f4f9b0e3dedcb9be25dcabc84a8bfa6fa50 # Add pacman hooks for systemd
6c4101f30bf041b6b740051bb96f3cd9ef0a7f90 # journal.conf: increase the size of the logs and drop unneeded options
951a3d66bcb149254c16aee954ca4eb793f92d67 # Add pacman hooks for openrc
)

_reverts=(
)

prepare() {
 cd $gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $gitname
  lucjan-meson . build
  meson compile -C build
}

package() {
  cd $gitname
  meson install -C build --destdir "${pkgdir}"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
