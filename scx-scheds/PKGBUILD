# Maintainer: Tejun Heo <tj@kernel.org>
# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds
_gitname=scx
pkgver=1.0.14
pkgrel=1.8
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
options=(!lto)
provides=("scxctl")
conflicts=("scxctl")
source=("git+https://github.com/sched-ext/scx?signed#tag=v$pkgver"
        '0001-etc-default-scx-set-scx_flash-as-default-scheduler.patch'
        '0002-scx_loader-set-scx_flash-as-default-scheduler.patch'
        '0003-meson-install-scx_mitosis-and-scx_wd40.patch'
        '0004-scx_loader-Add-missing-schedulers.patch'
        '0005-scx_loader-add-bundled-configuration-like-for-scx.se.patch'
        '0006-scx_loader-add-config-search-path-for-distributions.patch'
        '0007-scx_loader-Support-build-time-configuration-of-vendo.patch'
        '0008-meson-drop-libalpm-support.patch')
sha256sums=('f469c6396455572832bff13a3ede140f42b3293f4239364947e5a785d3a00714'
            '2a6c2ea616c52c5c28e9c54644bffe39aa659da874b0c080df5833261aa1b1be'
            '6ecb8db02addf01f530b4e80a23e5978f42aa20d7b52f2e180f7514a2e3ca4d6'
            '552c57d451036649c47abebe26200e115bfaa730e9115fe73b0d062d7605535d'
            'b3aa1d34550c3382391c8b0e8357e9cc60797049c13c7c45a2c435fc7b749b59'
            '89300e883eb7871484add4e176d7878dba716a43a3cdd6330cc610e0c307e3a8'
            'b6370da4d167285b47bedc177b179bab3a4928523d4f4a08022ec36a88b48a1e'
            'e4cd6e2314ecf088bc0d8270b67883d55a9ab74a7e785e1f5e78a5d3d0bfcd5c'
            '6d995d8508661eed03e116ba2f189299d3cacb6b7d04fb4d01c574812f22064a')
validpgpkeys=(
  697C63013E65270255EBC2608744DC1EB26B5A9A  # Tejun Heo <tj@kernel.org>
  F5504C7B7B8107B40EF9E97AA1148BB3207BCC33  # David Vernet
  A8775C60262EEF2AA34A864F5DAD005C4BFEA770  # Jake Hilton
)

_backports=(
)

_reverts=(
)

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
