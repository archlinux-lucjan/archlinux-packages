# Maintainer: Tejun Heo <tj@kernel.org>
# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds
pkgver=0.1.9
pkgrel=5
pkgdesc='Sched_ext schedulers'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq')
makedepends=('python' 'meson' 'lucjan-meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
options=(!lto)
source=(https://github.com/sched-ext/scx/archive/refs/tags/v${pkgver}.tar.gz
       0001-Add-pacman-hooks.patch
       0002-Exclude-hooks-to-systemd.patch
       0003-Make-place-for-openrc-support.patch
       0004-Add-hooks-for-openrc.patch)
sha512sums=('dabee9d4fc5ef4b5bbd8b1e7d681d918d3006499f000d33a1a419b2595415ec1beb454be0d97b22a84ebf56bd1ace444162eec6cfc14c972fb38fceaf6477337'
            '66262a9dd325a1321e2e9ff9cb240751ef3cebaba1f32670e14e190fe4bce260a279f0bb3f411dfa2dd8c0279705570d044b28621c5a783698217dde6eab0d52'
            'add867475baf3fc0493096e9699f2058629410b551c1423292df913105a765ab31826d0c4c3bcf7e1d03766971d13c8a3d07472cdfa7a745409abf26f646f2ad'
            '46a505198fb8f6703943b589eda93e4d6b1f0cba7b426b8f937b81f9fa96d450462530e00ef4d6be1c5049e3bed3435151295d71adf1e35700fe7b185278bd84'
            '4b0777b6833544d07107f8d29ecc8b9a0bbf826fe9096ab9df013fd8fc93ba138736559debc03a40a8fd909dfd139cb3c631d8142b6d71b7141cd7dd3495cab9')

prepare() {
 cd scx-${pkgver}

 local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd scx-${pkgver}
  lucjan-meson . build
  meson compile -C build
}

package() {
  cd scx-${pkgver}
  meson install -C build --destdir "${pkgdir}"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
