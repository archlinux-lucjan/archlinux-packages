# Maintainer: Tejun Heo <tj@kernel.org>
# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds
_gitname=scx
pkgver=1.0.14
pkgrel=1.10
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
options=(!lto)
provides=("scxctl")
conflicts=("scxctl")
source=("git+https://github.com/sched-ext/scx?signed#tag=v$pkgver"
        '0001-etc-default-scx-set-scx_flash-as-default-scheduler.patch'
        '0002-scx_loader-add-bundled-configuration-like-for-scx.se.patch'
        '0003-scx_loader-add-config-search-path-for-distributions.patch'
        '0004-scx_loader-Support-build-time-configuration-of-vendo.patch'
        '0005-meson-drop-libalpm-support.patch'
        '0006-scx_loader-move-to-tools.patch'
        '0007-meson-install-scx_mitosis-and-scx_wd40.patch'
        '0008-scx_loader-Add-missing-schedulers.patch')
sha256sums=('f469c6396455572832bff13a3ede140f42b3293f4239364947e5a785d3a00714'
            '5ed6283922e0859015555e98d75c175b0775791bda325e45c98db3f0b91812c1'
            'eeca4337c842a217b8a5930c6e8e7b9aee10fa80153cbda1269f20bd024ab436'
            '9ecb33d9c50df66ca555f6ad9a1b2897c056e84faae1ba04c17e727d4ae9f095'
            '54233db897e9ccc1b08bb5ecea116ecf1b088e8d34612178769f8efa1287ffb1'
            'e90aee8c63580ef5860e414366415bbc4239c416713c4e568732a70d675b1411'
            '21a30e8490ee9b9550553d31cdf2573f334b85b6c4ff80cbeba65629aeb9ac83'
            '986fcd26e7d4619ff940f4dd64baf8a75f765eb9749ae87ee228e339ea418fa5'
            '71cb96030c616e7d7fe45d97e024b8d0530483e2dc21b476bf651e56139dd73d')
validpgpkeys=(
  697C63013E65270255EBC2608744DC1EB26B5A9A  # Tejun Heo <tj@kernel.org>
  F5504C7B7B8107B40EF9E97AA1148BB3207BCC33  # David Vernet
  A8775C60262EEF2AA34A864F5DAD005C4BFEA770  # Jake Hilton
)

_backports=(
)

_reverts=(
)

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
