# Maintainer: Tejun Heo <tj@kernel.org>
# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds
_gitname=scx
pkgver=1.0.7
pkgrel=1.4
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
options=(!lto)
source=("git+https://github.com/sched-ext/scx?signed#tag=v$pkgver")
sha256sums=('896434924faa43412c6e362978cf4c0a2e4437ec37fd3e5973ead39261e15e57')
validpgpkeys=(
  697C63013E65270255EBC2608744DC1EB26B5A9A  # Tejun Heo <tj@kernel.org>
  F5504C7B7B8107B40EF9E97AA1148BB3207BCC33  # David Vernet
)

_backports=(
59ad67152035319ecaa73e3c4e57ac0ab20a499a # feat: enable LTO
ee0d293189488feeb3a92e2e43934344672e114f # scx_loader: provide library crate to be used by other crates
22d45e6ddbea81efc17a91ca6d713a7e396cea52 # meson: Update bpftool usage in compile_scx_lib
a72c24e7e5670b4533c4508d6d6c980d8487cb50 # meson: Add scx_lib dep to gen_bpf_o
21cf3ccd1d263cf7aac3afe337911f18ba329dca # scx_lib: use the proper bpftool
38035e009eef0a1f57a0e0ac24b4cda5d190ec16 # add rustfmt.toml to avoid lookup to $HOME/.rustfmt.toml
aeab91b0111cf4cf8d75f0889d2fd5f73a866111 # fix header install path for library
d75710d22f068aae1cd4ae874493fa2c724784a2 # scx_bpfland: correctly handle nvcsw_max_thresh in task_weight()
e71cc7dc3097ca5cb573618ef33698486c73e77e # scx_bpfland: always kick CPUs using SCX_KICK_IDLE
d352015176b6b4a690f0fda1260a6bb362c067ea # scx_bpfland: do not try to wake-up CPUs if the system is already busy
7d8b8e75812ab62454c734683de4944938b3edc2 # scx_bpfland: consider wakeups by per-CPU kthreads as synchronous
c8b246ea63d0ab1632c7358cf535cb5f49943ec6 # scx_bpfland: give per-CPU tasks a chance to be directly dispatched
0bc6c5fb2d7394bf3d9862dba07e12299e22bff9 # scx_bpfland: boost batch workload when nvcsw_max_thresh is disabled
13ec357444b05c331cf5b86eb964c7b1578afc8f # scx_loader: add mode for server-oriented workloads
74fd6e0a63c21edd35bff36e3568258122067b75 # scx_loader: fix recursion of type convertion
)

_reverts=(
)

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
