# Maintainer: Tejun Heo <tj@kernel.org>
# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds
pkgver=0.1.9
pkgrel=6
pkgdesc='Sched_ext schedulers'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq')
makedepends=('python' 'meson' 'lucjan-meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
options=(!lto)
source=(https://github.com/sched-ext/scx/archive/refs/tags/v${pkgver}.tar.gz
       0001-Add-pacman-hooks.patch
       0002-Exclude-hooks-to-systemd.patch
       0003-Make-place-for-openrc-support.patch
       0004-Add-hooks-for-openrc.patch
       0005-Fix-upgrade-hook-for-openrc.patch)
sha512sums=('dabee9d4fc5ef4b5bbd8b1e7d681d918d3006499f000d33a1a419b2595415ec1beb454be0d97b22a84ebf56bd1ace444162eec6cfc14c972fb38fceaf6477337'
            'e22571b9da94affb16fec15745983a88e137b84eba65ff41373d4af1d3354d6cd21824b0c1221ee1e1e4d01dbf4eb5584b21167451c178c73398ac3b9530f35a'
            '6f45c053ebb9b127d5c12e9627ec19dce570b92ea428f1591c783386a3e20a66bca43343ef1d43a84a2438c051b39abbbc90d2620209ec4d8f002ee8a9ddb2e6'
            '5e34d1f28b09dfe139b1d3ede4818757cef5a2b77a3358c4757f1da6c69f9de08650c61f9c6b6c830efd152e158f18d75573f1c6e65f8daed90bfa346faebdef'
            '6b126bbd8461da62bace9d8fbc9bf879804a7afc9d7a912cbbaf5fc6ded00c71750ca046bf909f3f378bb89ffc723e282230c1e2839e182c75e83baed7809f0e'
            'b0c8d581d84c8391fd27108b2e2a833ffa375efadc4d71d27cd6175f2da55860b81c07ac0f66ecf1c4ee298355df93960a2ebc9ef6a8a86ce2e961c7ba18341d')

prepare() {
 cd scx-${pkgver}

 local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd scx-${pkgver}
  lucjan-meson . build
  meson compile -C build
}

package() {
  cd scx-${pkgver}
  meson install -C build --destdir "${pkgdir}"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
