# Maintainer: Tejun Heo <tj@kernel.org>
# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds
pkgver=0.1.9
pkgrel=11
pkgdesc='Sched_ext schedulers'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq')
makedepends=('python' 'meson' 'lucjan-meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx' 'etc/systemd/journald@sched-ext.conf')
options=(!lto)
source=(https://github.com/sched-ext/scx/archive/refs/tags/v${pkgver}.tar.gz
        0001-Add-pacman-hooks-for-systemd.patch
        0002-journal.conf-increase-the-size-of-the-logs-and-drop-.patch
        0003-Add-pacman-hooks-for-openrc.patch)
sha512sums=('dabee9d4fc5ef4b5bbd8b1e7d681d918d3006499f000d33a1a419b2595415ec1beb454be0d97b22a84ebf56bd1ace444162eec6cfc14c972fb38fceaf6477337'
            'a53c655f5d0be8cf4aa097cbd9cde692d61b72c0fd61d4e994f5750b295fb502998a02caf2e53f48819990a52ac60cebe2adeace6d972a8f750fe9d7756e7da3'
            '38faab550461f2d7af80e20ae9456b1331f05ec5b78c3705b7e133a1c804e799b5605cefd2ac55c7121da381d39e85496ed61754e429d2a4c283a0742eb01e18'
            '23f11d931d1119ba90d2d01143f4941575d21e787a31e265839917d6534a8eb58e40234b70eccd40cd778eb67a54f268bcd9b259b5facb63d7b9cf27763dc78a')

prepare() {
 cd scx-${pkgver}

 local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd scx-${pkgver}
  lucjan-meson . build
  meson compile -C build
}

package() {
  cd scx-${pkgver}
  meson install -C build --destdir "${pkgdir}"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
