# Maintainer: Tejun Heo <tj@kernel.org>
# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds
pkgver=0.1.9
pkgrel=4
pkgdesc='Sched_ext schedulers'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq')
makedepends=('python' 'meson' 'lucjan-meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
options=(!lto)
source=(https://github.com/sched-ext/scx/archive/refs/tags/v${pkgver}.tar.gz
       0001-Add-pacman-hooks.patch
       0002-Exclude-hooks-to-systemd.patch
       0003-Make-place-for-openrc-support.patch)
sha512sums=('dabee9d4fc5ef4b5bbd8b1e7d681d918d3006499f000d33a1a419b2595415ec1beb454be0d97b22a84ebf56bd1ace444162eec6cfc14c972fb38fceaf6477337'
            'fa4c510d461b81f258d34fcaa24cdfb14ae078e66c55179c989e9e1c2e56a5e7038d3adb1facbe91c9189866d0c200174e4047ce9ec69d508f235e3c38b6ec90'
            'a1c88a4c6cceb5ec168f146b01976b502d2becf9e5fa99331e8acbbf34aedb434d5ea0414389a5b9753bd64dde3518f7af04ba253e5bbcc4b25fbc90011c429d'
            'c9a66bb3620ccdd55ea96d886c5d49304ed5eef31e3a07dfd2b5ee7bb828c367c7d9e87fd8fb77c3caf71eac1dfba0b7514ee77eec857b3daaba40e8c218196d')

prepare() {
 cd scx-${pkgver}

 local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd scx-${pkgver}
  lucjan-meson . build
  meson compile -C build
}

package() {
  cd scx-${pkgver}
  meson install -C build --destdir "${pkgdir}"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
