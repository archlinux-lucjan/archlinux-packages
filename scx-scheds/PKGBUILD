# Maintainer: Tejun Heo <tj@kernel.org>
# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds
_gitname=scx
pkgver=1.0.14
pkgrel=1.11
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
backup=('etc/default/scx')
options=(!lto)
provides=("scxctl")
conflicts=("scxctl")
source=("git+https://github.com/sched-ext/scx?signed#tag=v$pkgver"
        '0001-etc-default-scx-set-scx_flash-as-default-scheduler.patch'
        '0002-scx_loader-add-bundled-configuration-like-for-scx.se.patch'
        '0003-scx_loader-add-config-search-path-for-distributions.patch'
        '0004-scx_loader-Support-build-time-configuration-of-vendo.patch'
        '0005-meson-drop-libalpm-support.patch'
        '0006-scx_loader-move-to-tools.patch'
        '0007-scx.service-drop-ConditionPathIsDirectory.patch'
        '0008-meson-install-scx_mitosis-and-scx_wd40.patch'
        '0009-scx_loader-Add-missing-schedulers.patch')
sha256sums=('f469c6396455572832bff13a3ede140f42b3293f4239364947e5a785d3a00714'
            '26b82902cbf97a86fdcedcef6ffc67838c1caa21f53cb8ecb93f02f491ab362e'
            '095d71e25432cf2df660cd97f1f57f396833fe67f457b3cea03846e487ff1305'
            'df91756ec2cc1c35f0dbea3ad3dd389af6789db3743c72e03112cafcd5b8626f'
            '24651fad2f32df190520caccdeac471d01851c69f1b3a7f3c8eadf6efc85db4d'
            '4356b3e3ff5f4fccc79fbbf472b12ac31fabe0e18eb1e7bf0bdfd8b585e53eb9'
            'cd2e2e6e7b3131c35a341970f6d75f7438229c023088e339b357259e6fc6bfac'
            '7123c0e018203c6fc56c644fba726025422611f3e73fadf79739b3ccc0f1ea0d'
            'b0cbea8edeadff4603d62f31e9720f3a0fa71865bb4fcceb1e340cb454f2debf'
            '20820c6c258261d0109cbcdb4ec3c8335d6fb18d62c12b0bf524951099d13abf')
validpgpkeys=(
  697C63013E65270255EBC2608744DC1EB26B5A9A  # Tejun Heo <tj@kernel.org>
  F5504C7B7B8107B40EF9E97AA1148BB3207BCC33  # David Vernet
  A8775C60262EEF2AA34A864F5DAD005C4BFEA770  # Jake Hilton
)

_backports=(
)

_reverts=(
)

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin \
    -D cargo_home="$srcdir"/scx
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
