# Maintainer: Tejun Heo <tj@kernel.org>
# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds
_gitname=scx
pkgver=1.0.15
pkgrel=1.4
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64')
license=('GPL-2.0-only')
depends=('libelf' 'zlib' 'jq' 'protobuf' 'libseccomp' 'bpf' 'libbpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'llvm-libs' 'rust')
options=(!lto)
provides=("scxctl")
conflicts=("scxctl")
source=("git+https://github.com/sched-ext/scx?signed#tag=v$pkgver"
        '0001-services-show-options-instead-of-setting-any.patch'
        '0002-meson-install-scx_mitosis-and-scx_wd40.patch'
        '0003-scx_loader-Add-missing-schedulers.patch'
        '0004-scxctl-sync-with-Joe-Maple-s-branch.patch'
        '0005-scx-scheds-drop-openrc-support-and-scx.service.patch')
sha256sums=('e2966b2915c9f8f09204bf23710cdb2089c66c0cf953916208a94d64c853fc43'
            '34e7c810b76d0170267d7738f3978d90deaf24ef8455f82473891a6f5316c591'
            '542922602ab12dc0d26392d36404e03ad27adec834e4ab8e63db710481fcbd02'
            '57b8532826f0ca53dd21df609cc7f67acbebb0f7bfc0ed75e104e460b43b9d75'
            '7b359d40ae5a3922fbb2ee6f5b9ba503d1080a296166f4679a3487e9e98267e8'
            'a7aef656894798b0a2c8a587f17f998bb968520d102d716e095af962ecb6a0a8')
validpgpkeys=(
  697C63013E65270255EBC2608744DC1EB26B5A9A  # Tejun Heo <tj@kernel.org>
  F5504C7B7B8107B40EF9E97AA1148BB3207BCC33  # David Vernet
  A8775C60262EEF2AA34A864F5DAD005C4BFEA770  # Jake Hilton
)

_backports=(
)

_reverts=(
)

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D cargo_home="$srcdir"/scx \
    -D libbpf_a=disabled \
    -D bpftool=disabled \
    -D b_lto=true \
    -D b_lto_mode=thin
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
