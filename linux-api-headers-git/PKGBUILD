# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->glibc->binutils->gcc

pkgname=linux-api-headers-git
_major=5.19
_srcname=linux
pkgver=5.19.r0.g3d7cb6b04c3f
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace"
arch=('any')
url="http://www.gnu.org/software/libc"
license=('GPL2')
makedepends=('rsync')
provides=("linux-api-headers=$pkgver")
conflicts=('linux-api-headers')
source=("git://git.kernel.org/pub/scm/linux/kernel/git/stable/${_srcname}.git#branch=linux-${_major}.y")
md5sums=('SKIP')

pkgver() {
  cd ${_srcname}

  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd ${_srcname}

  make mrproper
}

package() {
  cd ${_srcname}
  make INSTALL_HDR_PATH="$pkgdir/usr" headers_install

  # use headers from libdrm
  rm -r "$pkgdir/usr/include/drm"
}
