# Based on linux-firmware by Thomas Bächler <thomas@archlinux.org>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: Victor Dmitriyev <mrvvitek@gmail.com>
# Contributor: xduugu

pkgbase=linux-firmware-git
_pkgbase=linux-firmware
pkgname=(linux-firmware-git amd-ucode-git)
pkgver=20191220.r1535.g6871bff
pkgrel=2
pkgdesc="Firmware files for Linux"
makedepends=('git')
arch=('any')
url="http://git.kernel.org/?p=linux/kernel/git/firmware/linux-firmware.git;a=summary"
license=('GPL2' 'GPL3' 'custom')
options=(!strip)
source=("git+https://git.kernel.org/pub/scm/linux/kernel/git/firmware/linux-firmware.git"
        '0001-Update-iwlwifi-firmware.patch')
md5sums=('SKIP'
         '570b6a2a923d4b5f3542d37b6120ae5f')

pkgver() {
  cd "${_pkgbase}"
  
  echo "$(git show --format='%cI' -q master | sed 's/T.*//g;s/-//g').r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${_pkgbase}"
  
  # https://bugs.archlinux.org/task/64703
  git apply -3 ../0001-Update-iwlwifi-firmware.patch
}

build() {
  mkdir -p kernel/x86/microcode
  cat ${_pkgbase}/amd-ucode/microcode_amd*.bin > kernel/x86/microcode/AuthenticAMD.bin

  # Reproducibility: set the timestamp on the bin file
  if [[ -n $SOURCE_DATE_EPOCH ]]; then 
    touch -d @$SOURCE_DATE_EPOCH kernel/x86/microcode/AuthenticAMD.bin
  fi

  # Reproducibility: strip the inode and device numbers from the cpio archive
  echo kernel/x86/microcode/AuthenticAMD.bin |
    bsdtar --uid 0 --gid 0 -cnf - -T - |
    bsdtar --null -cf - --format=newc @- > amd-ucode.img
}

package_linux-firmware-git() {
  conflicts=('linux-firmware'
             'kernel26-firmware'
             'ar9170-fw'
             'iwlwifi-1000-ucode'
             'iwlwifi-3945-ucode'
             'iwlwifi-4965-ucode'
             'iwlwifi-5000-ucode'
             'iwlwifi-5150-ucode'
             'iwlwifi-6000-ucode'
             'rt2870usb-fw'
             'rt2x00-rt61-fw'
             'rt2x00-rt71w-fw')
  replaces=('kernel26-firmware'
            'ar9170-fw'
            'iwlwifi-1000-ucode'
            'iwlwifi-3945-ucode'
            'iwlwifi-4965-ucode'
            'iwlwifi-5000-ucode'
            'iwlwifi-5150-ucode'
            'iwlwifi-6000-ucode'
            'rt2870usb-fw'
            'rt2x00-rt61-fw'
            'rt2x00-rt71w-fw')
  provides=("linux-firmware")

  cd "${_pkgbase}"

  make DESTDIR="${pkgdir}" FIRMWAREDIR=/usr/lib/firmware install

  install -Dt "${pkgdir}/usr/share/licenses/${pkgname}" -m644 LICEN* WHENCE

  # Trigger a microcode reload for configurations not using early updates
  echo 'w /sys/devices/system/cpu/microcode/reload - - - - 1' |
    install -Dm644 /dev/stdin "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}

package_amd-ucode-git() {
  pkgdesc='Microcode update files for AMD CPUs'
  provides=("amd-ucode")
  conflicts=("amd-ucode")

  install -Dt "${pkgdir}/boot" -m644 amd-ucode.img
  install -Dm644 ${_pkgbase}/LICENSE.amd-ucode "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
