# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>

pkgname=uksmtools-git
pkgver=20150925.r54.g9f59a3a
pkgrel=1
pkgdesc="Small set of tools to control UKSM"
arch=('x86_64')
url="https://github.com/kernelOfTruth/uksmtools"
license=('GPL')
makedepends=('git')
conflicts=('uksmtools' 'uksmstat')
provides=('uksmtools' 'uksmstat')
source=("git+https://github.com/kernelOfTruth/uksmtools")
sha256sums=('SKIP')

pkgver() {
    cd ${pkgname%-*}
    echo "$(git show --format='%cI' -q master | sed 's/T.*//g;s/-//g').r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
    cd ${pkgname%-*}
    git submodule init
    git submodule update --remote
}

build() {
    cd build
    cmake ../${pkgname%-*} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
    make
}

package() {
    cd build
    make install DESTDIR="$pkgdir"
}
