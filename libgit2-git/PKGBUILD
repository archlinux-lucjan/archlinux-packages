# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Hilton Medeiros <medeiros.hilton@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=libgit2-git
_pkgname=libgit2
pkgver=1.0.0.r105.g2f8d0cf24
pkgrel=1
epoch=1
pkgdesc='A linkable library for Git'
arch=('x86_64')
url='https://libgit2.github.com/'
depends=('glibc' 'http-parser' 'openssl' 'pcre' 'zlib')
makedepends=('cmake' 'libssh2' 'python' 'git')
provides=('libgit2' 'libgit2.so')
conflicts=('libgit2')
license=('GPL2')
source=("git://github.com/libgit2/libgit2"
        'libgit2-0.99.0-remove_http-parse_incompatible_tests.patch')
sha256sums=('SKIP'
            '0a597176a551dfe8dda56a46362ff1ef614928206a162d78fdf2d3074c9273ca')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"
  # removing tests that are only compatible with the (modified) vendored
  # version of http-parser, but not with upstream http-parser
  patch -Np1 -i "../libgit2-0.99.0-remove_http-parse_incompatible_tests.patch"
}

build() {
  cd "$_pkgname"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DUSE_HTTP_PARSER=system \
        -DTHREADSAFE=ON \
        -Wno-dev \
        -B build \
        -S .
  make -C build VERBOSE=1
}

check() {
  cd "$_pkgname"
  make -C build test VERBOSE=1
}

package() {
  depends+=('libssh2.so')
  cd "$_pkgname"
  make -C build DESTDIR="$pkgdir" install
  install -vDm 644 {AUTHORS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
