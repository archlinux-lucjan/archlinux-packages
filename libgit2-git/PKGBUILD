# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Hilton Medeiros <medeiros.hilton@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=libgit2-git
_pkgname=libgit2
pkgver=0.28.0.r269.gff7652c15
pkgrel=1
epoch=1
pkgdesc='A linkable library for Git'
arch=('x86_64')
url='https://libgit2.github.com/'
depends=('curl' 'http-parser' 'libssh2' 'openssl' 'zlib')
makedepends=('cmake' 'python' 'git')
provides=('libgit2')
conflicts=('libgit2')
license=('GPL2')
source=("git://github.com/libgit2/libgit2")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"
  mkdir -p build
}

build() {
  cd "$_pkgname/build"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DTHREADSAFE=ON \
        ..
  make VERBOSE=1
}

check() {
  cd "$_pkgname/build"
  make test VERBOSE=1
}

package() {
  cd "$_pkgname/build"
  make DESTDIR="$pkgdir" install
}
