# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: : Rémy Oudompheng <remy@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=namcap-git
pkgver=3.2.10.4.g28fdd26
pkgrel=1
pkgdesc="A Pacman package analyzer (git)"
arch=('any')
url="http://projects.archlinux.org/namcap.git/"
depends=('python' 'pyalpm>=0.5' 'licenses'
         'binutils' 'elfutils' 'python-pyelftools')
makedepends=('python-setuptools' 'git')
provides=('namcap')
conflicts=('namcap')
source=("$pkgname::git+https://gitlab.archlinux.org/pacman/namcap")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --tags --long | sed 's/-/./g'
}

build() {
  cd $pkgname
  python setup.py build
}

check() {
  cd $pkgname
  env PARSE_PKGBUILD_PATH="${srcdir}/$pkgname" \
      PATH="${srcdir}/$pkgname:$PATH" \
      python setup.py test
}

package() {
  cd $pkgname
  python setup.py install --root="${pkgdir}"
}

# vim:set ts=2 sw=2 et:
