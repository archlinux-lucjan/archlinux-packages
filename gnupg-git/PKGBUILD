# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=gnupg-git
pkgver=2.2.7.974.g074ab108e
pkgrel=1
pkgdesc='Complete and free implementation of the OpenPGP standard'
url='http://www.gnupg.org/'
license=('GPL')
arch=('x86_64')
checkdepends=('openssh')
makedepends=('libldap' 'libusb-compat' 'pcsclite' 'git' 'fig2dev')
depends=('npth' 'libgpg-error' 'libgcrypt' 'libksba' 'libassuan'
         'pinentry' 'bzip2' 'readline' 'gnutls' 'sqlite')
optdepends=('libldap: gpg2keys_ldap'
            'libusb-compat: scdaemon'
            'pcsclite: scdaemon')
source=('git://git.gnupg.org/gnupg.git'
        'drop-import-clean.patch'
        'avoid-beta-warning.patch')
sha1sums=('SKIP'
          'dfb6aeaa84ee76ab9f5383a9fd61e816caf65fdb'
          '9dbb09a7446408447cd2b3ea9ad48bf7058ab67a')

install=install

conflicts=('gnupg')
provides=('gnupg')

pkgver() {
        cd gnupg
        git describe --long --tags | sed 's/^gnupg.//;s/-/./g'
}


prepare() {
	cd gnupg
	patch -p1 -i ../avoid-beta-warning.patch
	patch -p1 -i ../drop-import-clean.patch

	./autogen.sh
}

build() {
	cd gnupg
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/gnupg \
		--enable-maintainer-mode \
		--enable-symcryptrun \

	make
}

#check() {
#	cd gnupg
#	make check
#}

package() {
	cd gnupg
	make DESTDIR="${pkgdir}" install
	ln -s gpg "${pkgdir}"/usr/bin/gpg2
	ln -s gpgv "${pkgdir}"/usr/bin/gpgv2

	cd doc/examples/systemd-user
	for i in *.*; do
		install -Dm644 "$i" "${pkgdir}/usr/lib/systemd/user/$i"
	done
}
