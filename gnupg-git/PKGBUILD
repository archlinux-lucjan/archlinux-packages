# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=gnupg-git
pkgver=2.2.1.r21.g98c260e05
pkgrel=1
pkgdesc='Complete and free implementation of the OpenPGP standard'
url='http://www.gnupg.org/'
license=('GPL')
arch=('x86_64')
optdepends=('libldap: gpg2keys_ldap'
            'libusb-compat: scdaemon')
makedepends=('libldap' 'libusb-compat' 'git')
checkdepends=('openssh')
depends=('npth' 'libgpg-error' 'libgcrypt' 'libksba' 'libassuan'
         'pinentry' 'bzip2' 'readline' 'gnutls' 'sqlite')
#validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'
#              '46CC730865BB5C78EBABADCF04376F3EE0856959'
#              '031EC2536E580D8EA286A9F22071B08A33BD3F06'
#              'D238EA65D64C67ED4C3073F28A861B1C7EFD60D9')
source=(git://git.gnupg.org/gnupg.git)
sha1sums=('SKIP')

install=install

conflicts=('dirmngr' 'gnupg2' 'gnupg')
provides=('dirmngr' "gnupg2=${pkgver}" 'gnupg')
replaces=('dirmngr' 'gnupg2')

pkgver() {
  cd gnupg
  git describe --tags --long | sed 's/^gnupg.//;s/\([^-]*-g\)/r\1/;s/-/./g'
}


prepare() {
	cd gnupg
	./autogen.sh
	sed '/noinst_SCRIPTS = gpg-zip/c sbin_SCRIPTS += gpg-zip' -i tools/Makefile.in
}

build() {
	cd gnupg
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/gnupg \
		--enable-maintainer-mode \
		--enable-symcryptrun \

	make
}

#check() {
#	cd gnupg
#	make check
#}

package() {
	cd gnupg
	make DESTDIR="${pkgdir}" install
	ln -s gpg "${pkgdir}"/usr/bin/gpg2
	ln -s gpgv "${pkgdir}"/usr/bin/gpgv2

	cd doc/examples/systemd-user
	for i in *.*; do
		install -Dm644 "$i" "${pkgdir}/usr/lib/systemd/user/$i"
	done
}
