# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>

pkgbase=canta-kde-git
_pkgbase=Canta-kde
pkgname=('canta-kde-git' 'kvantum-theme-canta-git')
pkgver=20191124.r12.gc476fc5
pkgrel=1
pkgdesc="Canta theme for KDE Plasma 5"
arch=('any')
url="https://github.com/vinceliuice/${_pkgbase}"
license=('GPL3')
options=('!strip')
source=("${_pkgbase}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd ${_pkgbase}
  echo "$(git show --format='%cI' -q master | sed 's/T.*//g;s/-//g').r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

package_canta-kde-git() {
    optdepends=('kvantum-theme-canta-git: Canta theme for Kvantum Qt style (recommended)'
                'tela-icon-theme-git: for a more consistent and beautiful experience (recommended)')
    conflicts=('canta-kde')
    provides=('canta-kde')
    cd ${_pkgbase}
    install -d "$pkgdir"/usr/share
    
    rm sddm/{README.md,install.sh}
    
    # Temporary fix for upstream filestructure
    mv aurorae themes
    mkdir -p aurorae
    mv themes aurorae/
    
    mv sddm themes
    mkdir -p sddm
    mv themes sddm/

    cp -r aurorae "$pkgdir"/usr/share
    cp -r color-schemes "$pkgdir"/usr/share
    cp -r plasma "$pkgdir"/usr/share
    cp -r sddm "$pkgdir"/usr/share
}

package_kvantum-theme-canta-git() {
    pkgdesc="Canta theme for Kvantum"
    depends=('kvantum-qt5')
    provides=('kvantum-theme-canta')
    conflicts=('kvantum-theme-canta')
    cd ${_pkgbase}
    install -d "${pkgdir}"/usr/share

    cp -r Kvantum "$pkgdir"/usr/share
}
