# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Maintainer: pavbaranov <pavbaranov at gmail dot com>
 
pkgname=kgtk-git
_pkgname=kgtk
epoch=1
pkgver=0.11.0.r35.ge586217
pkgrel=2
pkgdesc="KGTK Qt5/KF5 port"
arch=('x86_64')
url="https://github.com/sandsmark/$_pkgname"
license=('GPL2')
depends=('kio' 'ki18n' 'kwindowsystem' 'kdbusaddons' 'gtk2' 'gtk3')
makedepends=('git' 'extra-cmake-modules')
conflicts=('kgtk')
provides=('kgtk')
source=("git+https://github.com/sandsmark/$_pkgname.git")
sha256sums=('SKIP')
 
pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m3 -e CPACK_PACKAGE_VERSION_MAJOR -e CPACK_PACKAGE_VERSION_MINOR -e CPACK_PACKAGE_VERSION_PATCH | grep -o "[[:digit:]]*" | paste -sd'.')"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build
}
 
package(){
  DESTDIR="$pkgdir" cmake --install build
}

