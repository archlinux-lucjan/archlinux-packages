# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Maintainer: pavbaranov <pavbaranov at gmail dot com>
 
pkgname=kgtk-git
_pkgname=kgtk
pkgver=20200529.r35.ge586217
pkgrel=1
pkgdesc="KGTK Qt5/KF5 port"
arch=('x86_64')
url="https://github.com/sandsmark/$_pkgname"
license=('GPL2')
depends=('qt5-base')
makedepends=('git' 'cmake')
source=("git+https://github.com/sandsmark/$_pkgname.git")
sha256sums=('SKIP')
 
pkgver() {
  cd $_pkgname
  echo "$(git show --format='%cI' -q master | sed 's/T.*//g;s/-//g').r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build
}
 
package(){
  DESTDIR="$pkgdir" cmake --install build
}

