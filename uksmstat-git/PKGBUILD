# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# based on archival uksmstat PKGBUILD by:
# Maintainer: Leonardo Dagnino <leodag dot sch at gmail dot com>

pkgname=uksmstat-git
pkgver=20120923.r39.g06854fd
pkgrel=2
pkgdesc="Simple tool to monitor and control UKSM"
url="https://github.com/CSRedRat/uksmstat"
arch=('x86_64')
license=('GPL')
makedepends=('git')
depends=('glibc')
conflicts=('uksmstat')
provides=('uksmstat')
source=("git+https://github.com/CSRedRat/uksmstat")
md5sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}
  echo "$(git show --format='%cI' -q master | sed 's/T.*//g;s/-//g').r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cd ${pkgname%-*}
  make
}

package() {
  cd "${pkgname%-*}"
  make install DESTDIR="${pkgdir}/"  
  mv ${pkgdir}/usr/local/* ${pkgdir}/usr
  rm -r ${pkgdir}/usr/local
}
