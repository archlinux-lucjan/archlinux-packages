# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

pkgname=linux-api-headers-rc-git
_major=4.18
_srcname=linux-stable-rc
pkgver=4.18.16.r0.g6b3252287aa2
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace"
arch=('any')
url="http://www.gnu.org/software/libc"
license=('GPL2')
provides=('linux-api-headers=$pkgver')
conflicts=('linux-api-headers')
source=("git://git.kernel.org/pub/scm/linux/kernel/git/stable/linux-stable-rc.git#branch=linux-${_major}.y")
md5sums=('SKIP')

pkgver() {
  cd ${_srcname}

  git describe --long --tags | sed -E 's/^v//;s/([^-]*-g)/r\1/;s/-/./g;s/\.rc/rc/'
}
   
build() {
  cd ${_srcname}

  make mrproper
  make headers_check
}

package() {
  cd ${_srcname}
  make INSTALL_HDR_PATH="$pkgdir/usr" headers_install

  # use headers from libdrm
  rm -r "$pkgdir/usr/include/drm"
  
  # clean-up unnecessary files generated during install
  find "$pkgdir" \( -name .install -o -name ..install.cmd \) -delete
}
