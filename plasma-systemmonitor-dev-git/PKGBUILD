# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Maintanier: pavbaranov <pavbaranov at gmail dot com>

pkgname=plasma-systemmonitor-dev-git
_pkgname=plasma-systemmonitor
pkgver=5.19.0.r274.g575b738
pkgrel=1
pkgdesc="An interface for monitoring system sensors, process information and other system resources"
arch=('x86_64')
license=('GPL' 'LGPL')
url='https://invent.kde.org/plasma/plasma-systemmonitor.git'
makedepends=('extra-cmake-modules' 'git') 
depends=('libksysguard' 'kirigami2')
conflicts=('plasma-systemmonitor-git' 'plasma-systemmonitor')
provides=('plasma-systemmonitor')
source=('git+https://invent.kde.org/plasma/plasma-systemmonitor.git#branch=Plasma/5.20')
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'project(systemmonitor VERSION' | grep -o "[[:digit:]]*" | paste -sd'.')"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
  # git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -B build -S $_pkgname
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
