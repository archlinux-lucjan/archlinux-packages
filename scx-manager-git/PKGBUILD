# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-manager-git
pkgver=1.14.1.r1.g1eb3a3e
pkgrel=1
epoch=1
pkgdesc="Simple GUI for managing sched-ext schedulers via scx_loader"
arch=(aarch64 x86_64)
url="https://github.com/cachyos/scx-manager"
license=(GPL-3.0-or-later)
depends=('qt6-base' 'polkit' 'scx-scheds')
makedepends=('cmake' 'ninja' 'git' 'qt6-tools' 'cargo' 'lld' 'clang' 'llvm')
source=("${pkgname}::git+$url.git")
sha256sums=('SKIP')
provides=('scx-manager')
conflicts=('scx-manager')

_backports=(
)

_reverts=(
)

pkgver() {
  cd "${srcdir}/${pkgname}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
 cd "${srcdir}/${pkgname}"

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd "${srcdir}/${pkgname}"

  # we are building with llvm stack here, due to rust
  export AR=llvm-ar
  export CC=clang
  export CXX=clang++
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/ld.lld"

  cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build
}

package() {
  cd "${srcdir}/${pkgname}"

  # we are building with llvm stack here, due to rust
  export AR=llvm-ar
  export CC=clang
  export CXX=clang++
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/ld.lld"

  DESTDIR="${pkgdir}" cmake --build build --target install
}

# vim:set sw=2 sts=2 et:
