# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: argymeg <argymeg at gmail dot com>
# Contributor Tondu < thomas.gastine at wanadoo dot fr >
# Contributor: buddabrod < buddabrod at gmail dot com >

pkgname=kaffeine-git
pkgver=2.0.18.19.g4e9c817
pkgrel=2
pkgdesc='KDE media player'
license=('GPL')
arch=('x86_64')
url="http://kaffeine.kde.org"
depends=('kio' 'libxss' 'vlc')
makedepends=('git' 'extra-cmake-modules' 'kidletime' 'kdoctools')
provides=('kaffeine')
conflicts=('kaffeine')
source=('git://anongit.kde.org/kaffeine.git')
md5sums=('SKIP')

pkgver() {
  cd kaffeine
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

build() {
  cmake -B build -S kaffeine \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
