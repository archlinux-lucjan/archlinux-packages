# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: argymeg <argymeg at gmail dot com>
# Contributor Tondu < thomas.gastine at wanadoo dot fr >
# Contributor: buddabrod < buddabrod at gmail dot com >

pkgname=kaffeine-git
pkgver=2.0.14.4.g3ab91ca
pkgrel=1
pkgdesc='KDE media player'
license=(GPL)
arch=(i686 x86_64)
url="http://kaffeine.kde.org"
depends=(kio libxss vlc)
makedepends=(git extra-cmake-modules kidletime kdoctools)
provides=('kaffeine')
conflicts=('kaffeine')
source=('git://anongit.kde.org/kaffeine.git')
md5sums=('SKIP')

pkgver() {
  cd kaffeine
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kaffeine \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
