# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=git-delta-git
_pkgname=delta
pkgver=0.8.2.r13.gb6d221c
pkgrel=1
pkgdesc="A syntax-highlighting pager for git"
arch=('any')
url="https://github.com/dandavison/delta"
license=('custom')
depends=('libgit2')
makedepends=('rust' 'git')
checkdepends=('git')
optdepends=(
  'bash-completion: for shell auto-completion'
  'zsh-completions: for shell auto-completion'
)
options=('!lto')
provides=('delta')
conflicts=('delta')
source=(git+https://github.com/dandavison/$_pkgname.git)
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $_pkgname
  RUSTUP_TOOLCHAIN=stable cargo build --release --locked --all-features --target-dir=target
}

check() {
  cd $_pkgname
  # some tests hang for 60+ seconds, find out why
  RUSTUP_TOOLCHAIN=stable cargo test --locked --target-dir=target
}

package() {
  cd $_pkgname
  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/delta

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # shell auto-completions
  install -vDm644 etc/completion/completion.bash \
    "$pkgdir/usr/share/bash-completion/completions/$_pkgname"
  install -vDm644 etc/completion/completion.zsh \
    "$pkgdir/usr/share/zsh/site-functions/_$_pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
