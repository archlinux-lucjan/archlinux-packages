# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=gnupg-stable-git
pkgver=2.2.35.10.g91acbdc93
pkgrel=1
pkgdesc='Complete and free implementation of the OpenPGP standard'
url='http://www.gnupg.org/'
license=(BSD custom CC0 GPL2 GPL3 LGPL3 LGPL2.1 MIT)
arch=('x86_64')
checkdepends=('openssh')
makedepends=('libldap' 'libusb-compat' 'pcsclite' 'git' 'fig2dev')
depends=('npth' 'libgpg-error' 'libgcrypt' 'libksba' 'libassuan'
         'pinentry' 'bzip2' 'readline' 'libreadline.so' 'gnutls'
         'sqlite' 'zlib' 'glibc')
optdepends=('libldap: gpg2keys_ldap'
            'libusb-compat: scdaemon'
            'pcsclite: scdaemon')
source=('git://git.gnupg.org/gnupg.git#branch=STABLE-BRANCH-2-2'
        'avoid-beta-warning.patch')
sha1sums=('SKIP'
          '9dbb09a7446408447cd2b3ea9ad48bf7058ab67a')

install=install

conflicts=('gnupg')
provides=('gnupg')

pkgver() {
        cd gnupg
        git describe --long --tags | sed 's/^gnupg.//;s/-/./g'
}


prepare() {
	cd gnupg
	patch -p1 -i ../avoid-beta-warning.patch

	./autogen.sh
}

build() {
	cd gnupg
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/gnupg \
		--enable-maintainer-mode \
		--enable-symcryptrun \

	make
}

#check() {
#	cd gnupg
#	make check
#}

package() {
	cd gnupg
	make DESTDIR="${pkgdir}" install
	ln -s gpg "${pkgdir}"/usr/bin/gpg2
	ln -s gpgv "${pkgdir}"/usr/bin/gpgv2

	install -Dm 644 doc/examples/systemd-user/*.* -t "${pkgdir}/usr/lib/systemd/user"
	install -Dm 644 COPYING.{CC0,other} -t "${pkgdir}/usr/share/licenses/$pkgname/"
}
