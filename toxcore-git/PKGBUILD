# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor Ivan Shapovalov <intelfx@intelfx.name>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Håvard Pettersson <mail@haavard.me>
# Contributor: naxuroqa <naxuroqa at gmail.com>
# Contributor: Boohbah <boohbah at gmail.com>
# Contributor: Kevin MacMartin <prurigro at gmail dot com>

pkgname=toxcore-git
_pkgname=c-toxcore
__pkgname=toxcore
pkgver=0.1.10.15.g020e77fb
pkgrel=1
pkgdesc='Secure, configuration-free, P2P Skype replacement backend'
arch=('i686' 'x86_64')
url='https://tox.chat'
license=('GPL3')
depends=('systemd' 'libconfig' 'libsodium' 'libvpx' 'opus')
makedepends=('git' 'check')
conflicts=("tox" "toxcore")
provides=("tox" "toxcore")
backup=('etc/tox-bootstrapd.conf')
install=$__pkgname.install
source=("git+https://github.com/TokTok/c-toxcore.git"
        'toxcore.conf')
sha512sums=('SKIP'
            'aa1dcfbdf9b613f5d89e238ff5d01e0ea150ad3162792acb806f51ce07fd9ade1270b310b1285a828dcdf578549b95a89fd9bd198fb205c83f5a5be2c969ea63')

pkgver() {
  cd $_pkgname
  git describe --long | sed 's/^v//;s/-/./g'
}

prepare() {
  cd $_pkgname
  sed -i "s|/usr/local|/usr|" other/bootstrap_daemon/tox-bootstrapd.service
}

build() {
  cd $_pkgname
  # autoreconf -if
  # ./configure \
  #   --prefix=/usr \
  #   --enable-daemon \
  #   --disable-ntox \
  #   --enable-tests
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DDHT_BOOTSTRAP=on \
        -DBOOTSTRAP_DAEMON=on \
        -DBUILD_NTOX=off
  make
}

package() {
  cd $_pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/toxcore.conf" "$pkgdir/usr/lib/sysusers.d/toxcore.conf"
  install -Dm644 ./other/bootstrap_daemon/tox-bootstrapd.service "$pkgdir/usr/lib/systemd/system/tox-bootstrapd.service"
  install -Dm644 ./other/bootstrap_daemon/tox-bootstrapd.conf "$pkgdir/etc/tox-bootstrapd.conf"
}
