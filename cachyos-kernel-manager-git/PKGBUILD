# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=cachyos-kernel-manager-git
pkgver=1.12.0.r0.gab2176b
pkgrel=1
epoch=1
pkgdesc="Simple kernel manager"
arch=(aarch64 x86_64 x86_64_v3)
url="https://github.com/cachyos/kernel-manager"
license=(GPL-3.0-or-later)
depends=('qt6-base' 'polkit' 'chwd' 'scx-scheds')
makedepends=('cmake' 'ninja' 'git' 'python' 'polkit-qt6' 'qt6-tools' 'cargo' 'lld' 'clang' 'llvm')
source=("${pkgname}::git+$url.git"
        fix-installed-db.patch)
sha256sums=('SKIP'
            '455c136513110b88e8234b8e4b25fd739804122ba6a492cde369b1f76c90960c')
provides=('cachyos-kernel-manager')
conflicts=('cachyos-kernel-manager')
options=(strip)


_backports=(
)

_reverts=(
)

pkgver() {
  cd "${srcdir}/${pkgname}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
 cd "${srcdir}/${pkgname}"

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd "${srcdir}/${pkgname}"

  CFLAGS=${CFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}
  CXXFLAGS=${CXXFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}

  _cpuCount=$(grep -c -w ^processor /proc/cpuinfo)

  # Compile with clang
  export AR=llvm-ar
  export CC=clang
  export CXX=clang++
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/ld.lld"

  cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build --parallel $_cpuCount
}

package() {
  cd "${srcdir}/${pkgname}"

  # Compile with clang
  export AR=llvm-ar
  export CC=clang
  export CXX=clang++
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/ld.lld"

  DESTDIR="${pkgdir}" cmake --build build --target install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname/-git}/LICENSE"
}

# vim:set sw=2 sts=2 et:
