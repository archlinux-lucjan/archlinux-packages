# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>

pkgname=cachyos-kernel-manager-git
pkgver=1.8.0.r8.gdd99ab9
pkgrel=1
epoch=1
pkgdesc="Simple kernel manager"
arch=(aarch64 x86_64 x86_64_v3)
url="https://github.com/cachyos/kernel-manager"
license=(GPLv3)
depends=('qt5-base' 'polkit-qt5')
makedepends=('cmake' 'ninja' 'git' 'python')
source=("${pkgname}::git+$url.git"
        'fix-installed-db.patch')
sha512sums=('SKIP'
            '553e7bd81c3600ec95009efcc595ace2e7c51ab5a34028f4f619f4b26be7dad6c106e51b2aa9c504b6064d3af313414a4e390a4c182f50752db94227e1798b01')
provides=('cachyos-kernel-manager')
conflicts=('cachyos-kernel-manager')
options=(strip !lto)

_backports=(
d68adbfe37d12522a3678c86611f22c34be80fc7 # Add scx_asdf
)

_reverts=(
dd99ab97d1b5c92b663eb9f0c66acac1b48d115c # conf-window: refactor async build using QProcess instead of glib impl
41175d52cb9d7f73f288b8addf0d47dd4eb698d2 # CI: add missing dependency
23536a0d3a9e37134912e69041e5506716c36a38 # conf-window: implement option configuration save/load
)

pkgver() {
  cd "${srcdir}/${pkgname}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
 cd "${srcdir}/${pkgname}"

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd "${srcdir}/${pkgname}"

  CFLAGS=${CFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}
  CXXFLAGS=${CXXFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}

  _cpuCount=$(grep -c -w ^processor /proc/cpuinfo)

  cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build --parallel $_cpuCount
}

package() {
  cd "${srcdir}/${pkgname}"
  DESTDIR="${pkgdir}" cmake --build build --target install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname/-git}/LICENSE"
}

# vim:set sw=2 sts=2 et:
