# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: dr460nf1r3 <dr460nf1r3 at garudalinux dot org>
# Contributor: Antoine Viallon <antoine@lesviallon.fr>

pkgname=ananicy-cpp-git
_pkgname=ananicy-cpp
pkgver=1.0.1.r7.g5cb17be
pkgrel=2
pkgdesc="Ananicy Cpp is a full rewrite of Ananicy in C++, featuring lower CPU and RAM usage."
source=("git+https://gitlab.com/ananicy-cpp/ananicy-cpp.git"
        "git+https://gitlab.com/vnepogodin/std-format.git"
        "bpf-refactor.patch::https://gitlab.com/ananicy-cpp/ananicy-cpp/-/commit/3ce87adae3b1554d05f24c5a2dcede7345eb6b2d.patch")
md5sums=('SKIP'
         'SKIP'
         'b64d3025d4804f7a5a41aa7838a6a754')
arch=('x86_64')
depends=('fmt' 'spdlog' 'nlohmann-json' 'systemd' 'libelf' 'zlib' 'libbpf')
makedepends=('cmake' 'clang' 'git' 'bpf')
conflicts=('ananicy-cpp')
provides=('ananicy-cpp')
optdepends=("cachyos-ananicy-rules: community rules")

pkgver() {
  cd "$_pkgname"
  git describe --tags --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done

  git submodule init
  git config submodule."external/std-format".url "${srcdir}/std-format"
  git -c protocol.file.allow=always submodule update

  cmake -B build -S . \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DUSE_EXTERNAL_SPDLOG=ON \
        -DUSE_EXTERNAL_JSON=ON \
        -DUSE_EXTERNAL_FMTLIB=ON \
        -DENABLE_SYSTEMD=ON \
        -DUSE_BPF_PROC_IMPL=ON \
        -DBPF_BUILD_LIBBPF=OFF \
        -DVERSION=${pkgver}
}

build() {
  cd "$_pkgname"

  cmake --build build
}

package() {
  cd "$_pkgname"

  export DESTDIR="$pkgdir"
  cmake --install build --component Runtime

  install -m755 -d "$pkgdir/etc/ananicy.d"
}
