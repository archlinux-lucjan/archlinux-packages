# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix

pkgname=strawberry-git
_pkgname=strawberry
pkgver=0.3.1.29.g36563cd
pkgrel=1
pkgdesc="Bitperfect audio player and music collection organizer, fork of Clementine. gstreamer engine only"
arch=('x86_64')
url="http://www.strawbs.org/"
license=(GPL3)
depends=('chromaprint' 'protobuf' 'gst-plugins-base' 'gst-plugins-good' 'qt5-base' 'qt5-x11extras'
         'sqlite3' 'dbus' 'alsa-lib' 'libcdio' 'taglib')
#depends+=('libimobiledevice' 'libplist' 'libusbmuxd' 'libgpod' 'libmtp' 'phonon-qt5' 'vlc' 'xine-lib')
makedepends=('git' 'cmake' 'boost')
optdepends=('gst-plugins-bad'
            'gst-plugins-ugly')
provides=('strawberry')
conflicts=('strawberry' 'strawberry-full-git')
source=("git://github.com/jonaski/strawberry.git")
sha256sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  cd "${srcdir}/${_pkgname}"
  install -d strawberry-build
}

build() {
  cd "${srcdir}/${_pkgname}/strawberry-build"
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_SYSTEM_TAGLIB=ON \
    -DENABLE_IMOBILEDEVICE=OFF \
    -DENABLE_LIBGPOD=OFF \
    -DENABLE_LIBMTP=OFF \
    -DENABLE_LIBLASTFM=OFF \
    -DENABLE_PHONON=OFF \
    -DENABLE_SPARKLE=OFF \
    -DENABLE_VLC=OFF \
    -DENABLE_XINE=OFF
  make
}

package() {
  cd "${srcdir}/${_pkgname}/strawberry-build"
  make DESTDIR="${pkgdir}" install
}
