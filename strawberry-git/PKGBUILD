# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix

pkgname=strawberry-git
_pkgname=strawberry
pkgver=0.9.1.6.gf5bb15f7
pkgrel=1
pkgdesc="Bitperfect audio player and music collection organizer, fork of Clementine. gstreamer, xine and vlc engines"
arch=('x86_64')
url="http://www.strawbs.org/"
license=(GPL3)
depends=('qt6-base' 'chromaprint' 'protobuf' 'gst-plugins-base' 'gst-plugins-good' 'udisks2' 'libcdio' 'libgpod' 'libmtp' 'fftw')
makedepends=('cmake' 'boost' 'qt6-tools' 'git')
optdepends=('gst-libav: additional codecs'
            'gst-plugins-bad: additional codecs'
            'gst-plugins-ugly: additional codecs')
provides=('strawberry')
conflicts=('strawberry')
source=("git+https://github.com/strawberrymusicplayer/strawberry.git")
sha256sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

build() {
  cmake -B build -S $_pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_WITH_QT6=ON 
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
