# Maintainter: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: João Silva <mwksoul@gmail.com>

pkgname=kdesvn-stable-git
pkgver=2.0.0.227.g91aa8e69
pkgrel=1
pkgdesc="A pure C++ SVN-client for KDE. (GIT Version)"
arch=('x86_64')
url='http://projects.kde.org/kdesvn'
license=('GPL')
depends=('subversion' 'kcoreaddons' 'kdoctools' 'kio' 'knotifications' 'kparts' 'ktexteditor')
makedepends=('extra-cmake-modules' 'git')
conflicts=('kdesvn')
provides=('kdesvn')
source=('git+https://anongit.kde.org/kdesvn.git#branch=2.1')
sha1sums=('SKIP')

pkgver() {
  cd kdesvn
  #_ver="$(cat CMakeLists.txt | grep -m3 -e KDESVN_VERSION_MAJOR -e KDESVN_VERSION_MINOR -e KDESVN_VERSION_PATCH | grep -o "[[:digit:]]*" | paste -sd'.')"
  #echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
  git describe --long | sed 's/^v//;s/-/./g'
}

build() {
  cmake -B build -S kdesvn \
   -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="-DQT_THREAD_SUPPORT" \
    -DLIB_SUFFIX=
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
