# Maintainter: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: João Silva <mwksoul@gmail.com>

pkgname=kdesvn-stable-git
pkgver=2.0.0.5.gfc8f707b
pkgrel=1
pkgdesc="A pure C++ SVN-client for KDE. (GIT Version)"
arch=('x86_64')
url='http://projects.kde.org/kdesvn'
license=('GPL')
depends=('kbookmarks' 'kconfig' 'kconfigwidgets' 'kcoreaddons'
         'kdbusaddons' 'kdoctools' 'ki18n' 'kiconthemes' 'kitemviews'
         'kjobwidgets' 'kio' 'knotifications' 'kparts' 'kservice'
         'ktexteditor' 'kwallet' 'kwidgetsaddons'
         'qt5-base'
         'subversion')
optdepends=('git')
makedepends=('git' 'extra-cmake-modules')
conflicts=('kdesvn')
provides=('kdesvn')
source=('git://anongit.kde.org/kdesvn.git#branch=2.0')
sha1sums=('SKIP')


pkgver() {
  cd kdesvn
  #_ver="$(cat CMakeLists.txt | grep -m3 -e KDESVN_VERSION_MAJOR -e KDESVN_VERSION_MINOR -e KDESVN_VERSION_PATCH | grep -o "[[:digit:]]*" | paste -sd'.')"
  #echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
  git describe --long | sed 's/^v//;s/-/./g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kdesvn \
   -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="-DQT_THREAD_SUPPORT" \
    -DLIB_SUFFIX=
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
