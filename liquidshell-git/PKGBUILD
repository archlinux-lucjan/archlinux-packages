# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=liquidshell-git
_pkgname=liquidshell
pkgver=20180423.r168.g48157f9
pkgrel=1
pkgdesc="Plasmashell alternative"
arch=(x86_64)
url='https://cgit.kde.org/liquidshell.git/'
license=(LGPL)
depends=('networkmanager-qt' 'bluez-qt' 'kcmutils' 'knewstuff' 'packagekit-qt5')
makedepends=('extra-cmake-modules' 'git')
source=("git://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  echo "$(git show --format='%cI' -q master | sed 's/T.*//g;s/-//g').r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package(){
  cd build
  make DESTDIR="$pkgdir" install
}
