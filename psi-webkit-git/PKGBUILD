# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>

pkgname=psi-webkit-git
pkgver=1.3.r3026.g8693e2e0
pkgrel=1
pkgdesc="A jabber client. GIT version"
arch=('i686' 'x86_64')
url="http://psi-im.org/"
license=('GPL2')
depends=('qca-qt5' 'qt5-x11extras' 'qt5-multimedia' 'aspell' 'libxss' 'minizip' 'desktop-file-utils' 'hicolor-icon-theme' 'qt5-webkit')
makedepends=('git' 'qconf-git')
provides=("psi=$pkgver" "psi-qt5-git=$pkgver" "psi-git=$pkgver")
conflicts=('psi' 'psi-qt5-git' 'psi-git')
source=('git+https://github.com/psi-im/psi.git')
md5sums=('SKIP')

pkgver() {
    cd $srcdir/psi
    _ver="$(cat version | grep -o "[[:digit:]]*" | paste -sd'.')"
    echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {

  cd $srcdir/psi

  git submodule init
  git submodule update

}

build() {

  cd $srcdir/psi

  qconf

 ./configure --prefix=/usr \
             --libdir=/usr/lib \
             --enable-webkit \
             --disable-enchant 
  make
}

package() {
  cd $srcdir/psi

  make INSTALL_ROOT="$pkgdir" install
}

