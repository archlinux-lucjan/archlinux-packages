# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: maz-1 <ohmygod19993 at gmail dot com>

pkgname=kuser-frameworks-git
_pkgname=kuser
pkgver=17.0.0.r808.gcd1b9d9
pkgrel=1
epoch=1
pkgdesc='User Manager'
url='http://kde.org/applications/system/kuser/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeadmin')
depends=('kdelibs4support' 'kio' 'kde-cli-tools' 'kldap')
makedepends=('cmake' 'extra-cmake-modules' 'kdesignerplugin' 'kdoctools' 'kinit' 'git')
provides=('kdeadmin-kuser' 'kuser' 'kuser-frameworks')
conflicts=('kdeadmin-kuser' 'kuser' 'kuser-frameworks')
source=("git+https://github.com/juliagoda/kuser")
md5sums=('SKIP')

pkgver() {
  cd kuser
  _ver="$(cat CMakeLists.txt | grep -m1 'project(kuser' | grep -o "[[:digit:]]*" | paste -sd'.')"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S kuser \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
