# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=devtools-git
_pkgname=devtools
pkgver=20191227.1.g4c08847
pkgrel=1
pkgdesc='Tools for Arch Linux package maintainers'
arch=('any')
license=('GPL')
url='http://projects.archlinux.org/devtools.git/'
depends=('bash' 'openssh' 'subversion' 'rsync' 'arch-install-scripts'
         'git' 'bzr' 'mercurial' 'diffutils' 'util-linux' 'awk')
makedepends=('asciidoc' 'git')
optdepends=('btrfs-progs: btrfs support')
provides=("devtools=$pkgver")
conflicts=('devtools')
source=(git://projects.archlinux.org/devtools.git)
md5sums=('SKIP')

pkgver() {
        cd $_pkgname
        git describe --long --tags | sed 's/^v//;s/-/./g'
}

prepare() {
        cd $_pkgname
        sed 's|makepkg --packagelist|makepkg --config /usr/share/devtools/makepkg-x86_64.conf --packagelist|' -i offload-build
}

build() {
	cd $_pkgname
	make PREFIX=/usr
}

package() {
	cd $_pkgname
	make PREFIX=/usr DESTDIR=${pkgdir} install
}
