# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>

pkgbase=chromeos-kde-git
_pkgbase=ChromeOS-kde
pkgname=('chromeos-kde-git' 'kvantum-theme-chromeos-git')
pkgver=20200512.r23.ge62ac34
pkgrel=1
pkgdesc="ChromeOS theme for KDE Plasma 5"
arch=('any')
url="https://github.com/vinceliuice/${_pkgbase}"
license=('GPL3')
options=('!strip')
source=("${_pkgbase}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd ${_pkgbase}
  echo "$(git show --format='%cI' -q master | sed 's/T.*//g;s/-//g').r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

package_chromeos-kde-git() {
    optdepends=('kvantum-theme-chromeos-git: ChromeOS theme for Kvantum Qt style (recommended)'
                'tela-icon-theme-git: for a more consistent and beautiful experience (recommended)')
    conflicts=('chromeos-kde')
    provides=('chromeos-kde')
    cd ${_pkgbase}
    install -d "$pkgdir"/usr/share
    
    rm sddm/{README.md,install.sh}
    
    # Temporary fix for upstream filestructure
    mv aurorae themes
    mkdir -p aurorae
    mv themes aurorae/
    
    mv sddm themes
    mkdir -p sddm
    mv themes sddm/

    cp -r aurorae "$pkgdir"/usr/share
    cp -r color-schemes "$pkgdir"/usr/share
    cp -r plasma "$pkgdir"/usr/share
    cp -r sddm "$pkgdir"/usr/share
}

package_kvantum-theme-chromeos-git() {
    pkgdesc="ChromeOS theme for Kvantum"
    depends=('kvantum-qt5')
    provides=('kvantum-theme-chromeos')
    conflicts=('kvantum-theme-chromeos')
    cd ${_pkgbase}
    install -d "${pkgdir}"/usr/share

    cp -r Kvantum "$pkgdir"/usr/share
}
